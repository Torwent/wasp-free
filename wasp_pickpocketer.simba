{$UNDEF SCRIPT_ID}{$DEFINE SCRIPT_ID := '003b8e09-e988-42ca-b261-b2b76d131800'}
{$UNDEF SCRIPT_REVISION}{$DEFINE SCRIPT_REVISION := '21'}
{$IFDEF WINDOWS}{$DEFINE FORMS_V2}{$DEFINE SCRIPT_GUI}{$ENDIF}
{$I SRL-T/osr.simba}
{$I WaspLib/osr.simba}

begin
  Login.PlayerIndex := 0;
  StatsPayload.SetUsername('');
end;

type
  ENPC = (
    MAN, MASTER_FARMER,
    GUARD, ELF, TZHAAR_CITY
  );

var
  PickedNPC: ENPC := ENPC.ELF;       //Use one of the above NPCs.
  SuicideMode: Boolean := True;      //Pickpocket until death. Useful if you respawn nearby.
  DiaryLevel: Int32 := 3;
  FoodAmount: Int32 := 10;
  NeckAmount: Int32 := 2;
  MinHealth: Int32 := 20;

type
  EThieverState = (
    OPEN_BANK,
    DEPOSIT_LOOT,
    WITHDRAW_FOOD,
    WITHDRAW_NECKLACES,
    CLOSE_INTERFACE,

    OPEN_CHAT,
    CHANGE_CHAT_FILTER,

    WALK_TO_NPC,
    EQUIP_ROGUE,

    CAST_SHADOW_VEIL,
    PICKPOCKET,
    OPEN_POUCH,

    EAT_FOOD,
    EQUIP_NECKLACE,
    CHECK_NECKLACE,
    CLOSE_CONTEXT,

    DROP_ITEMS,

    ENABLE_REDEMPTION,
    LOOT_GRAVE,

    END_SCRIPT
  );

  TThiever = record(TBaseBankScript)
    //init variables (to be set while the script is initiates)
    State: EThieverState;

    UseShadowVeil: Boolean;
    UseRedemption: Boolean;

    CurrentNPC: TRSNPC;

    CoinPouch: TRSItem;
    Necklace: TRSItem;
    ValuableItem: TRSItem;

    LootArray, StackableArray, DropArray, RogueEquipment: TRSItemArray;

    GearAmount, ValuableItemValue: Int32;

    NeckTimer: TCountDown;
    ShadowVeilTimer: TCountDown;
    LasPickPosition: TPoint;

    //run variables (to be used while the script is running)
    RenderDisabled: Boolean;
    HasNeck: Boolean;
    FreshNeck: Boolean;
    RedemptionEnabled: Boolean;

    CoinPouchLimit, MaxHit, NextHeal: Int32;
  end;


procedure TAntiban.Setup(); override;
begin
  Self.Skills := [ERSSkill.THIEVING, ERSSkill.TOTAL];
  Self.MinZoom := 30;
  Self.MaxZoom := 50;

  inherited;
end;

procedure TThiever.SetupNPC();
begin
  FoodHandler.Amount := FoodAmount;

  case PickedNPC of
    MAN:
      begin
        Self.RSW.Setup([RSRegions.LUMBRIDGE]);
        CurrentNPC := RSNPCs.Man;

        LootArray += CoinPouch;
        LootArray += 'Coins';

        StackableArray := LootArray;

        MaxHit := 1;

        if not SuicideMode then
          SuicideMode := True;

        ActionProfit := 3;
      end;

    MASTER_FARMER:
      begin
        Self.RSW.Setup([RSRegions.DRAYNOR_VILLAGE]);
        CurrentNPC := RSNPCs.MasterFarmer;
        LootArray := [
          'Potato seed', 'Onion seed', 'Cabbage seed', 'Tomato seed',
          'Sweetcorn seed', 'Strawberry seed', 'Watermelon seed',
          'Snape grass seed',

          'Barley seed', 'Hammerstone seed', 'Asgarnian seed', 'Jute seed',
          'Yanillian seed', 'Krandorian seed', 'Wildblood seed',

          'Marigold seed', 'Nasturtium seed', 'Rosemary seed', 'Woad seed',
          'Limpwurt seed',

          'Redberry seed', 'Cadavaberry seed', 'Dwellberry seed',
          'Jangerberry seed', 'Whiteberry seed', 'Poison ivy seed',

          'Ranarr seed',

          'Mushroom spore', 'Belladonna seed', 'Cactus seed', 'Seaweed spore',
          'Potato cactus seed'
        ];

        DropArray := [
          'Potato seed', 'Onion seed', 'Cabbage seed', 'Tomato seed',
          'Sweetcorn seed', 'Strawberry seed', 'Watermelon seed',

          'Barley seed', 'Hammerstone seed', 'Asgarnian seed', 'Jute seed',
          'Yanillian seed', 'Krandorian seed', 'Wildblood seed',

          'Marigold seed', 'Nasturtium seed', 'Rosemary seed', 'Woad seed',
          'Limpwurt seed',

          'Redberry seed', 'Cadavaberry seed', 'Dwellberry seed',
          'Jangerberry seed', 'Whiteberry seed', 'Poison ivy seed',

          'Mushroom spore', 'Belladonna seed', 'Cactus seed',
          'Potato cactus seed'
        ];

        MaxHit := 3;

        if SuicideMode then
          SuicideMode := False;
      end;

    GUARD:
      begin
        Self.RSW.Setup([RSRegions.FALADOR]);
        CurrentNPC := RSNPCs.Guard;

        LootArray += CoinPouch;
        LootArray += 'Coins';

        StackableArray := LootArray;

        MaxHit := 2;
        ActionProfit := 30;
      end;

    ELF:
      begin
        Self.RSW.Setup([RSRegions.PRIFDDINAS]);
        CurrentNPC := RSNPCs.Elf;

        LootArray := [
          CoinPouch, 'Coins', 'Death rune', 'Jug of wine', 'Nature rune',
          'Fire orb', 'Diamond', 'Gold ore', 'Crystal shards',
          'Enhanced crystal teleport seed'
        ];

        ActionProfit := 560;
        ValuableItem := 'Enhanced crystal teleport seed';
        ValuableItemValue := ItemData.GetAverage(ValuableItem);


        DropArray := ['Gold ore'];

        StackableArray := [CoinPouch, 'Coins', 'Death rune', 'Nature rune',
                           'Crystal shards'];

        MaxHit := 5;
      end;

    TZHAAR_CITY:
      begin
        Self.RSW.Setup([RSRegions.MOR_UL_REK]);
        CurrentNPC := RSNPCs.TzhaarHur;
        LootArray := [
          'Tokkul', 'Uncut sapphire', 'Uncut emerald',
          'Uncut ruby', 'Uncut diamond'
        ];

        StackableArray := LootArray;

        Self.MaxHit := 4;

        if SuicideMode then
          SuicideMode := False;
      end;
  end;

  DropArray += 'Jug';
  DropArray += 'Pie dish';

  StackableArray += Necklace; //not stackable but this array is used to not bank items
  StackableArray += 'Cosmic rune';

  CurrentNPC.Filter.Walker := False;
end;

procedure TThiever.CheckRogueEquipment();
var
  ProfitMultiplier: Double;
  Item: TRSItem;
begin
  if Inventory.Open() then
  begin
    if Inventory.ContainsItem('Rogue mask') then
    begin
      ProfitMultiplier += 0.2;
      Self.RogueEquipment += 'Rogue mask';
    end;

    if Inventory.ContainsItem('Rogue top') then
    begin
      ProfitMultiplier += 0.2;
      Self.RogueEquipment += 'Rogue top';
    end;

    if Inventory.ContainsItem('Rogue trousers') then
    begin
      ProfitMultiplier += 0.2;
      Self.RogueEquipment += 'Rogue trousers';
    end;

    if Inventory.ContainsItem('Rogue gloves') then
    begin
      ProfitMultiplier += 0.2;
      Self.RogueEquipment += 'Rogue gloves';
    end;

    if Inventory.ContainsItem('Rogue boots') then
    begin
      ProfitMultiplier += 0.2;
      Self.RogueEquipment += 'Rogue boots';
    end;

    //This are not part of rogue equipment but are useful while thieving
    if Inventory.ContainsItem('Thieving cape') then
      Self.RogueEquipment += 'Thieving cape'
    else if Inventory.ContainsItem('Thieving cape(t)') then
      Self.RogueEquipment += 'Thieving cape(t)'
    else if Inventory.ContainsItem('Max cape') then
      Self.RogueEquipment += 'Max cape';

    //Lava staves for shadow veil spell
    if Inventory.ContainsItem('Lava battlestaff') then
      Self.RogueEquipment += 'Lava battlestaff'
    else if Inventory.ContainsItem('Mystic lava staff') then
      Self.RogueEquipment += 'Mystic lava staff'
    else if Inventory.ContainsItem(21200) then
      Self.RogueEquipment += 21200
    else if Inventory.ContainsAll(['Earth battlestaff', 'Tome of fire']) then
    begin
      Self.RogueEquipment += 'Earth battlestaff';
      Self.RogueEquipment += 'Tome of fire';
    end;
  end;

  if Equipment.Open() then
  begin
    if Equipment.ContainsItem('Rogue mask') then
    begin
      ProfitMultiplier += 0.2;
      Self.RogueEquipment += 'Rogue mask';
    end;

    if Equipment.ContainsItem('Rogue top') then
    begin
      ProfitMultiplier += 0.2;
      Self.RogueEquipment += 'Rogue top';
    end;

    if Equipment.ContainsItem('Rogue trousers') then
    begin
      ProfitMultiplier += 0.2;
      Self.RogueEquipment += 'Rogue trousers';
    end;

    if Equipment.ContainsItem('Rogue gloves') then
    begin
      ProfitMultiplier += 0.2;
      Self.RogueEquipment += 'Rogue gloves';
    end;

    if Equipment.ContainsItem('Rogue boots') then
    begin
      ProfitMultiplier += 0.2;
      Self.RogueEquipment += 'Rogue boots';
    end;


    //This are not part of rogue equipment but are useful while thieving
    if Equipment.ContainsItem('Thieving cape') then
      Self.RogueEquipment += 'Thieving cape'
    else if Equipment.ContainsItem('Thieving cape(t)') then
      Self.RogueEquipment += 'Thieving cape(t)';

    //Lava staves for shadow veil spell
    if Equipment.ContainsItem('Lava battlestaff') then
      Self.RogueEquipment += 'Lava battlestaff'
    else if Equipment.ContainsItem('Mystic lava staff') then
      Self.RogueEquipment += 'Mystic lava staff'
    else if Equipment.ContainsItem(21200) then
      Self.RogueEquipment += 21200
    else if Equipment.ContainsAll(['Earth battlestaff', 'Tome of fire']) then
    begin
      Self.RogueEquipment += 'Earth battlestaff';
      Self.RogueEquipment += 'Tome of fire';
    end;
  end;

  GearAmount := Equipment.CountGear();
  if not Equipment.IsSlotUsed(ERSEquipmentButton.AMULET) then
    GearAmount += 1;

  ActionProfit += Round(ActionProfit * ProfitMultiplier);

  for Item in Self.RogueEquipment do
    StackableArray += Item; //Adding Self.RogueEquipment to the non banking item list
end;

procedure TThiever.Init(maxActions: Int32; maxTime: Int64); override;
begin
  inherited;

  Self.UseRedemption := SuicideMode;

  if DiaryLevel = 4 then
    DiaryLevel += 1;

  CoinPouch := 'Coin pouch';
  Necklace := 'Dodgy necklace';

  Self.SetupNPC();
  MinHealth := Max(Self.MaxHit, MinHealth);

  //NeckTimer to recheck necklace in case the script messes up.
  NeckTimer.Init(140000);

  if Antiban.BioDice() then
    Options.SetNPCAttackOption(ERSAttackOption.HIDDEN)
  else
    Options.SetNPCAttackOption(ERSAttackOption.ALWAYS_RIGHT_CLICK);

  Self.CheckRogueEquipment();
  HasNeck := Equipment.ContainsItem(Necklace);
  if Magic.CanActivate(ERSSpell.SHADOW_VEIL) then
  begin
    UseShadowVeil := True;
    Magic.CastSpell(ERSSpell.SHADOW_VEIL);
    ShadowVeilTimer.Init(1000 * Round(0.6 * Stats.GetCurrentLevel(ERSSkill.MAGIC)));
  end;

  if not SuicideMode and ((FoodHandler.Amount + NeckAmount + 2) >= 26) then
  begin
    if Self.UseShadowVeil then
      FoodHandler.Amount := 26 - NeckAmount - 1
    else
      FoodHandler.Amount := 26 - NeckAmount;
  end
  else if ((FoodHandler.Amount + NeckAmount + 2 + Length(Self.StackableArray)) >= (28 - Length(Self.StackableArray))) then
    FoodHandler.Amount := 28 - NeckAmount - 2 - Length(Self.StackableArray);

  if WLSettings.RemoteInput.HUDDebug then
    DebugMMDotArray += CurrentNPC;
end;


function TRSInventory.HoverItems(Items: TRSItemArray): Boolean;
var
  Item: TRSItem;
  Slot: Int32;
begin
  for Item in Items do
    if Result := Self.FindItem(Item, Slot) then Break;

  if Result then Mouse.Move(Self.GetSlotBox(Slot));
end;

function TRSInventory.ClickItems(Items: TRSItemArray; Option: String = ''): Boolean;
begin
  if Self.HoverItems(Items) then
  begin
    if (Option <> '') then Result := ChooseOption.Select(Option)
    else
    begin
      Mouse.Click(MOUSE_LEFT);
      Result := True;
    end;
  end;
end;

function TRSInventory.CountItems(Items: TRSItemArray): Int32;
var
  Item: TRSItem;
begin
  for Item in Items do Result += Self.CountItem(Item);
end;


function TThiever.HasRogue(): Boolean;
begin
  Result := Inventory.ContainsAny(Self.RogueEquipment);
end;

function TThiever.EquipRogue(): Boolean;
var
  Item: TRSItem;
begin
  for Item in Self.RogueEquipment do
    if Inventory.ClickItem(Item) then
      Wait(200, 300);

  if Inventory.ClickItem(21200) then
    Wait(200, 300);

  Result := not Self.HasRogue and not Inventory.ContainsItem('Lava battlestaff');
end;


function TThiever.HasSpace(): Boolean;
begin
  if SuicideMode then //Makes sure we always have space to loot
  begin               //everything from the gravestone on suicide mode.
    if (Length(LootArray) > 2) then
      Result := (Inventory.CountEmptySlots > GearAmount)
    else
      Result := (Inventory.CountEmptySlots > GearAmount) or
                (Inventory.ContainsItem(CoinPouch) and
                 Inventory.ContainsItem('Coins')
                 and not Inventory.ContainsAny(DropArray));
  end
  else
  begin
    if (Length(LootArray) > 2) then
      Result := (Inventory.CountEmptySlots >= 2) //if has things oher than coins
    else                                         //we need at least 2 spaces always.
      Result := (Inventory.CountEmptySlots >= 2) or
                (Inventory.ContainsItem(CoinPouch) and
                 Inventory.ContainsItem('Coins')
                 and not Inventory.ContainsAny(DropArray));
  end;
end;


function TThiever.IsLowHP(): Boolean;
begin
  if Self.NextHeal < 1 then
  begin
    Self.NextHeal := MinHealth + Random(-Self.MaxHit, Self.MaxHit) * Round(Antiban.GetUniqueNumber(2, 0, 4));
    Self.NextHeal := EnsureRange(Self.NextHeal, Self.MaxHit, Round(BaseStats.GetCurrentLevel(ERSSkill.HITPOINTS) * 0.8));
  end;

  Result := Minimap.GetHPLevel() <= Self.NextHeal;
end;

function TThiever.IsPouchFull(): Boolean;
begin
  if Self.CoinPouchLimit < 1 then
    Self.CoinPouchLimit := SRL.TruncatedGauss(28 * DiaryLevel, 1);

  Result := Inventory.CountItemStack(Self.CoinPouch) >= Self.CoinPouchLimit;
end;


function TThiever.CheckFailed(startingHP: Int32): Boolean;
var
  hasHitSplat: Boolean;
  currentHP: Int32 := Minimap.GetHPLevel();
begin
  hasHitSplat := not Self.RenderDisabled and
                (MainScreen.FindHitsplats(MainScreen.GetPlayerBox()) <> []);

  Result := (currentHP < startingHP) or hasHitSplat;

  if Result then
  begin
    if HasHitSplat then
    begin
      Options.RenderSelf();
      Self.RenderDisabled := True;
    end
    else
      Self.OpenCoinPouch();
  end;
end;

function TThiever.NecklaceExpired(): Boolean;
begin
  Result := Chat.FindMessage('crumbles', [CHAT_COLOR_LIGHT_RED]);
  HasNeck := not Result;
end;

function TThiever.Dodged(): Boolean;
begin
  Result := Chat.FindMessage('dodgy');
end;


function TThiever.OpenCoinPouch(): Boolean;
var
  pouchCount: Int32;
begin
  pouchCount := Inventory.CountItemStack(Self.CoinPouch);
  if Inventory.ClickItem(Self.CoinPouch) then
  begin
    Wait(600);
    Result := WaitUntil(not Inventory.ContainsItem(Self.CoinPouch), 100, 2000);
  end;

  if Result then
  begin
    Self.CoinPouchLimit := SRL.TruncatedGauss(28 * DiaryLevel, 1);
    Self.TotalActions += pouchCount;
    Self.TotalProfit := Self.TotalActions * Self.ActionProfit;
    WL.Activity.Restart();
  end;
end;

function TThiever.Heal(): Boolean;
var
  oldHP, newHP, hpLevel: Int32;
begin
  newHP := Minimap.GetHPLevel();
  hpLevel := BaseStats.GetCurrentLevel(ERSSkill.HITPOINTS) - 1;
  repeat
    oldHP := newHP;
    if not Inventory.Consume(FOOD_CONSUMABLE) then
      Exit;

    newHP := Minimap.GetHPLevel();
    Result := newHP > Random(oldHP, hpLevel);
  until Result;

  if Result then
    Self.NextHeal := 0; //This will be reset when TThiever.IsLowHP() is called.
end;

function TThiever.EquipNeck(): Boolean;
begin
  if Result := Inventory.ClickItem(Necklace) then Wait(300, 400);

  FreshNeck := HasNeck := Result;
end;

function TThiever.CheckNeck(): Boolean;
begin
  Result := HasNeck := Equipment.ContainsItem(Necklace);

  NeckTimer.Restart(Random(-30000, 60000));
end;


function TThiever.CastShadowVeil(): Boolean;
begin
  if not UseShadowVeil or not Magic.Open then
    Exit;

  if not Magic.CanActivate(ERSSpell.SHADOW_VEIL) then
  begin
    Result := UseShadowVeil := False;
    Exit(False);
  end;

  Result := Magic.CastSpell(ERSSpell.SHADOW_VEIL);
  ShadowVeilTimer.Restart(Random(-5000, 5000));
end;

function TThiever.PickNPC(): Boolean;
var
  Slot: Int32 := Inventory.GetSelectedSlot;
  CurrentHP: Int32 := Minimap.GetHPLevel;
begin
  if Slot > -1 then Inventory.ClickSlot(Slot);

  if Result := CurrentNPC.SelectOption(['Pick', 'pocket']) then
  begin
    Minimap.WaitPlayerMoving(300, 10000);

    RSObjects.Gravestone.Coordinates.Insert(Self.RSW.GetMyPos, 0);
    if Length(RSObjects.Gravestone.Coordinates) > 4 then
      RSObjects.Gravestone.Coordinates.Pop;

    if not Self.NecklaceExpired then
      FreshNeck := False;

    Self.CheckFailed(CurrentHP);
    WL.Activity.Restart();
  end;
end;

function TThiever.Deposit(): Boolean;
var
  ItemCount: Int32;
begin
  if ToStr(ValuableItem) <> '' then
    ItemCount := Inventory.CountItem(ValuableItem);
  Result := Bank.DepositAllBut(BankTab, StackableArray);
  if ItemCount > 0 then
  begin
    if Inventory.CountItem(ValuableItem) = 0 then
      TotalProfit += (ItemCount * ValuableItemValue);
  end;
end;

function TThiever.WithdrawAny(items: TRSItemArray; Quantity: Int32): Boolean;
var
  Item: TRSItem;
  ItemCount: Int32 := Inventory.CountItems(items);
  QuantityNeeded: Int32 := Quantity - ItemCount;
  InvCount: Int32;
begin
  for Item in items do
    if Bank.ContainsItem(Item) then Exit(Self.Withdraw([Item, Quantity, False]));

  for Item in items do
  begin
    if BankTab = -1 then
      BankTab := Bank.FindItemTab(Item);

    InvCount := Inventory.Count;
    if Result := Bank.WithdrawItem(BankTab, [Item, QuantityNeeded, False], False) then
    begin
      WaitUntil(Inventory.Count > InvCount, 100, 3000);
      Exit;
    end;
  end;

  FoodHandler.Amount := 0;
end;

function TThiever.LootGrave(): Boolean;
var
  TPA, GraveTPA: TPointArray;
  P: TPoint;
  UpTextFound: Boolean;
begin
  HasNeck := False;
  FreshNeck := False;
  RedemptionEnabled := False;

  if not Minimap.WaitArrow(TPA, 3000) then
    Exit;

  while not MainScreen.WaitArrow(GraveTPA, 3000) do
  begin
    Mouse.Click(TPA.Grow(6).RandomValue, MOUSE_LEFT);
    Minimap.WaitMoving;
  end;

  if not MainScreen.WaitArrow(GraveTPA, 3000) then
    Exit;

  repeat
    if not Minimap.WaitArrow(TPA, 3000) then
      Exit;  
    GraveTPA.SortByY(False);
    P := GraveTPA[0];
    P.Y += 5;
    Mouse.Move(P);
    UpTextFound := MainScreen.IsUpText('Grave');
    if not UpTextFound then
      MainScreen.WaitArrow(GraveTPA, 3000);
  until UpTextFound;

  Result := ChooseOption.Select('Loot');

  if Result then RSObjects.Gravestone.Coordinates := [];
end;

function TThiever.EnableRedemption(): Boolean;
begin
  Result := Prayer.ActivatePrayer(ERSPrayer.REDEMPTION);
  UseRedemption := RedemptionEnabled := Result;
end;


function TThiever.GetState(): EThieverState;
begin
  if WL.Activity.IsFinished() then
    Exit(END_SCRIPT);

  if ChooseOption.IsOpen() then
    Exit(CLOSE_CONTEXT);

  if MainScreen.FindGrave() then
    Exit(LOOT_GRAVE);

  if MainScreen.HasInterface() then
  begin
    if Bank.IsOpen() then
    begin
      if Inventory.HasRandomItem(Self.StackableArray) then
        Exit(DEPOSIT_LOOT);
      if Inventory.CountItem(Necklace) < NeckAmount then
        Exit(WITHDRAW_NECKLACES);
      if not Inventory.HasEnoughConsumable(FOOD_CONSUMABLE) then
        Exit(WITHDRAW_FOOD);
    end;

    Exit(CLOSE_INTERFACE);
  end;

  if Self.IsPouchFull then
    Exit(OPEN_POUCH);

  if ChatButtons.GetState(ERSChatButton.GAME_CHAT) <> ERSChatButtonState.ENABLED then
    Exit(CHANGE_CHAT_FILTER);

  if not ChatButtons.IsActive(ERSChatButton.GAME_CHAT) then
    Exit(OPEN_CHAT);

  if NeckTimer.IsFinished() then
    Exit(CHECK_NECKLACE);

  if Self.HasRogue() then
    Exit(EQUIP_ROGUE);

  if not Self.HasSpace() then
  begin
    if Inventory.ContainsAny(DropArray) then
      Exit(DROP_ITEMS);
    if Inventory.FindConsumable(FOOD_CONSUMABLE) then
      Exit(EAT_FOOD);

    Exit(OPEN_BANK);
  end;

  if (NeckAmount > 0) and not FreshNeck and (not HasNeck or Self.NecklaceExpired) then
  begin
    if Inventory.ContainsItem(Necklace) then
      Exit(EQUIP_NECKLACE);

    Exit(OPEN_BANK);
  end;

  if Self.IsLowHP then
  begin
    if not RedemptionEnabled and Inventory.ContainsConsumable(FOOD_CONSUMABLE) then
      Exit(EAT_FOOD);
    if not SuicideMode then
      Exit(OPEN_BANK);
    if not Self.RSW.AtTile(CurrentNPC.Coordinates, 40) then
      Exit(WALK_TO_NPC);
    if UseRedemption and not RedemptionEnabled then
      Exit(ENABLE_REDEMPTION);
    if UseShadowVeil and ShadowVeilTimer.IsFinished() then
      Exit(CAST_SHADOW_VEIL);

    Exit(PICKPOCKET);
  end;

  if not Self.RSW.AtTile(CurrentNPC.Coordinates, 40) then
    Exit(WALK_TO_NPC);

  if UseShadowVeil and ShadowVeilTimer.IsFinished() then
    Exit(CAST_SHADOW_VEIL);

  Exit(PICKPOCKET);
end;

procedure TThiever.Run(maxActions: Int32; maxTime: Int64);
begin
  Self.Init(maxActions, maxTime);

  repeat
    State := Self.GetState();
    Self.SetAction(ToStr(State));

    case State of
      OPEN_BANK: Bank.WalkOpen();
      DEPOSIT_LOOT: Self.Deposit();
      WITHDRAW_FOOD: Bank.WithdrawConsumable(FOOD_CONSUMABLE);
      WITHDRAW_NECKLACES: Self.Withdraw([Necklace, NeckAmount, False]);
      CLOSE_INTERFACE: MainScreen.CloseInterface();

      OPEN_CHAT: ChatButtons.Open(ERSChatButton.GAME_CHAT);
      CHANGE_CHAT_FILTER: ChatButtons.ChangeState(ERSChatButton.GAME_CHAT, ERSChatButtonState.ENABLED);

      CAST_SHADOW_VEIL: Self.CastShadowVeil();
      PICKPOCKET: Self.PickNPC();
      OPEN_POUCH: Self.OpenCoinPouch();

      EAT_FOOD: Self.Heal();
      EQUIP_NECKLACE: Self.EquipNeck();
      CHECK_NECKLACE: Self.CheckNeck();
      CLOSE_CONTEXT: ChooseOption.Close();
      DROP_ITEMS: Inventory.ShiftDrop(Self.DropArray, Inventory.RandomPattern);
      WALK_TO_NPC: Self.RSW.WebWalk(CurrentNPC.Coordinates, 30, 0.2);
      EQUIP_ROGUE: Self.EquipRogue();
      LOOT_GRAVE: Self.LootGrave();
      ENABLE_REDEMPTION: Self.EnableRedemption();

      END_SCRIPT: Break;
    end;

    Self.DoAntiban();
  until Self.ShouldStop();
end;

var
  Thiever: TThiever;

function TConsumableHandler.FindInBank(): TRSConsumableArray; override;
var
  consumable: TRSConsumable;
  item: TRSItem;
begin
  Self.DebugLn('Setting up consumable of type: ' + ToStr(Self.ConsumableType));

  for item in GetConsumableArray(Self.ConsumableType) do
  begin
    if Self.ConsumableArray.Contains(item) then //avoid resetting items.
      Continue;

    if Bank.ContainsItem(item.Reorder(False)) then
    begin
      Self.DebugLn('Consumable found: ' + ToStr(item));
      consumable := Self.Setup(item);
      consumable.BankTab := Bank.GetCurrentTab(); //TODO: Search parent maybe?
      Thiever.StackableArray += item;
      Result += consumable;
    end;
  end;
end;

function TConsumableHandler.FindInInventory(): TRSConsumableArray; override;
var
  consumable: TRSConsumable;
  item: TRSItem;
begin
  Self.DebugLn('Setting up consumable of type: ' + ToStr(Self.ConsumableType));

  for item in GetConsumableArray(Self.ConsumableType) do
  begin
    if Self.ConsumableArray.Contains(item) then //avoid resetting leftover items.
      Continue;

    if Inventory.ContainsItem(item) then
    begin
      Self.DebugLn('Consumable found: ' + ToStr(item));
      consumable := Self.Setup(item);
      Thiever.StackableArray += item;
      Result += consumable;
    end;
  end;
end;

function TRSChooseOption.Select(Text: TStringArray; MouseAction: Int32 = MOUSE_LEFT; CaseSensitive: Boolean = True; CloseIfNotFound: Boolean = True): Boolean; override;
var
  Choices: TRSChooseOption_OptionArray;
  I, J: Int32;
begin
  if not Self.Open then
    Exit;

  Wait(0, 1000 - Round(1000*BioHash), wdLeft);

  Choices := Self.GetOptions;

  for I := 0 to High(Choices) do
    for J := 0 to High(Text) do
      if (CaseSensitive and (Text[J] in Choices[I].Text)) or
         ((not CaseSensitive) and (Lowercase(Text[J]) in Lowercase(Choices[I].Text))) then
      begin
        Self.Select(Choices[I], MouseAction);
        Exit(True);
      end;

  if CloseIfNotFound then
  begin
    WaitEx(350 - Round(350*BioHash), 50);
    Self.Close;
  end;

end;

function TRSInventory.HasEnoughConsumable(ConsumableType: ERSConsumable): Boolean; override;
begin
  Result := Self.CountPoints(ConsumableType) >= FoodHandler.Amount;
end;


{$IFDEF SCRIPT_GUI}
type
  TThieverConfig = record(TScriptForm)
    NPCSelector: TLabeledCombobox;
    DiaryLevelSelector: TLabeledComboBox;
    NeckAmountEdit: TLabeledEdit;
    SuicideState: TLabeledCheckbox;
  end;

procedure TThieverConfig.StartScript(sender: TObject); override;
var
  foodAmountEdit, minHealthEdit: TEdit;
begin
  foodAmountEdit := Self.Form.GetChild('cm_food_amount_edit');
  minHealthEdit  := Self.Form.GetChild('cm_food_minimum_edit');

  PickedNPC := ENPC(Self.NPCSelector.GetItemIndex());
  SuicideMode := Self.SuicideState.IsChecked();

  DiaryLevel := Self.DiaryLevelSelector.GetItemIndex() + 1;
  FoodAmount := StrToInt(foodAmountEdit.GetText());
  NeckAmount := StrToInt(Self.NeckAmountEdit.GetText());
  MinHealth  := StrToint(minHealthEdit.GetText());

  inherited;
end;

procedure TThieverConfig.NPCSelectorOnChange({$H-}sender: TObject);{$H+}
begin
  case ENPC(Self.NPCSelector.GetItemIndex()) of
    MAN:
      begin
        Self.SuicideState.SetChecked(False);
        Self.SuicideState.setEnabled(False);
      end;

    MASTER_FARMER:
      begin
        Self.SuicideState.SetChecked(False);
        Self.SuicideState.setEnabled(False);
      end;

    TZHAAR_CITY:
      begin
        Self.SuicideState.SetChecked(False);
        Self.SuicideState.setEnabled(False);
      end;

    else
    begin
      Self.SuicideState.SetChecked(True);
      Self.SuicideState.setEnabled(True);
    end;
  end;
end;

function TScriptForm.CreateConsumableSettings(owner: TControl; consumableType: ERSConsumable; addInfo: Boolean = True): TPanel; override;
var
  typeStr: String;
  consumableAmount: TLabeledEdit;
  fullWidth, w, space, y: Int32;
begin
  fullWidth := Floor(Self.Size.X/3);
  space := Floor(fullWidth * 0.1);
  w := Floor(fullWidth - space * 2);
  if addInfo then
    y := Floor(Self.Size.Y/2.5)
  else
    y := TControl.AdjustToDPI(15);

  case consumableType of
    FOOD_CONSUMABLE:   typeStr := 'Food';
    PRAYER_CONSUMABLE: typeStr := 'Prayer';
    ENERGY_CONSUMABLE: typeStr := 'Energy';
    BOOST_CONSUMABLE:  typeStr := 'Boost';
  end;

  Result := inherited;

  with consumableAmount do
  begin
    Create(Result);
    SetCaption('Withdraw ' + LowerCase(typeStr) + ' amount:');
    SetName('cm_' + typeStr + '_amount');
    SetLeft(fullWidth * 2 + space);
    SetTop(y);
    SetWidth(w);
    SetText('8');
    Edit.setOnKeyPress(@Edit.NumberField);
  end;
end;


procedure TThieverConfig.Run(); override;
var
  tab: TTabSheet;
  panel: TPanel;
  foodAmountEdit, minHealthEdit: TEdit;
begin
  Self.Setup('Wasp Pickpocketer');
  Self.Start.setOnClick(@Self.StartScript);

  Self.AddTab('Script Settings');
  tab := Self.Tabs[High(Self.Tabs)];

  Self.CreateAccountManager(tab);

  with Self.DiaryLevelSelector do
  begin
    Create(tab);
    SetCaption('Ardougne diaries completed:');
    SetLeft(TControl.AdjustToDPI(30));
    SetTop(TControl.AdjustToDPI(180));
    SetStyle(csDropDownList);
    AddItemArray(['None/Easy diary', 'Medium diary', 'Hard diary', 'Elite diary']);
    SetItemIndex(DiaryLevel - 1);
  end;

  with Self.NeckAmountEdit do
  begin
    Create(tab);
    SetCaption('Necklaces amount:');
    SetLeft(Self.DiaryLevelSelector.GetRight() + TControl.AdjustToDPI(30));
    SetTop(Self.DiaryLevelSelector.GetTop());
    SetWidth(TControl.AdjustToDPI(120));
    SetText(ToStr(NeckAmount));
  end;


  with Self.NPCSelector do
  begin
    Create(tab);
    SetCaption('NPC:');
    SetLeft(Self.DiaryLevelSelector.GetLeft());
    SetTop(Self.DiaryLevelSelector.GetBottom() + TControl.AdjustToDPI(10));
    SetStyle(csDropDownList);
    AddItemArray(['Men and women (Lumbridge only)',
                  'Master farmers (Draynor only)',
                  'Guards (Falador only)', 'Elves (Prifddinas only)',
                  'Tzhaars']);
    SetItemIndex(Ord(PickedNPC));
    Combobox.setOnChange(@Self.NPCSelectorOnChange);
  end;

  with Self.SuicideState do
  begin
    Create(tab);
    SetCaption('Suicide mode');
    SetLeft(Self.NPCSelector.GetRight() + TControl.AdjustToDPI(30));
    SetTop(Self.NPCSelector.GetTop() + TControl.AdjustToDPI(15));
    SetEnabled(True);
    Self.NPCSelectorOnChange(@Self.SuicideState);
  end;

  panel := Self.CreateConsumableSettings(tab, FOOD_CONSUMABLE, False);

  panel.SetTop(TControl.AdjustToDPI(300));

  foodAmountEdit := panel.GetChild('cm_food_amount_edit');
  minHealthEdit  := panel.GetChild('cm_food_minimum_edit');

  foodAmountEdit.SetText(ToStr(FoodAmount));
  foodAmountEdit.SetVisible(True);
  minHealthEdit.SetText(ToStr(MinHealth));

  Self.CreateVersionPanel(tab);
  Self.CreateAntibanManager();
  Self.CreateWaspLibSettings();
  Self.CreateAPISettings();

  Self.NPCSelectorOnChange(@Self.NPCSelector.ComboBox);

  inherited;
end;

var
  ThieverConfig: TThieverConfig;
{$ENDIF}

begin
  {$IFDEF SCRIPT_GUI}
  ThieverConfig.Run();
  {$ENDIF}
  Thiever.Run(WLSettings.MaxActions, WLSettings.MaxTime);
end.
