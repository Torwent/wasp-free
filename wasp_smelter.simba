{$UNDEF SCRIPT_ID}{$DEFINE SCRIPT_ID := 'e8df872a-afaf-44b8-9fc5-eef079ada50b'}
{$UNDEF SCRIPT_REVISION}{$DEFINE SCRIPT_REVISION := '18'}
{$IFNDEF SCRIPT_CHAIN}
  {$IFDEF WINDOWS}{$DEFINE FORMS_V2}{$DEFINE SCRIPT_GUI}{$ENDIF}
  {$I SRL-T/osr.simba}
  {$I WaspLib/osr.simba}

begin
  Login.PlayerIndex := 0;
  StatsPayload.SetUsername('');
end;

{$ENDIF}

type
  ERSBarType = (
    BRONZE, IRON, SILVER, STEEL, GOLD, MITHRIL, ADAMANT, RUNITE, MOLTEN_GLASS
  );

  ERSFurnace = (
    EDGEVILLE_FURNACE, FALADOR_FURNACE
  );

var
  CurrentBar = ERSBarType.GOLD;
  CurrentFurnace = ERSFurnace.EDGEVILLE_FURNACE;

type
  ESmelterState = (
    HOVER_BANK,
    OPEN_BANK,
    WITHDRAW_MATERIALS,
    DEPOSIT_BAR,
    DEPOSIT_RANDOM_ITEM,

    OPEN_COLLECT,
    HANDLE_COLLECT,

    OPEN_FURNACE,
    SMELT_BAR,
    WAIT_SMELT,

    CLOSE_INTERFACE,

    LEVEL_UP,
    CLOSE_CONTEXT,

    END_SCRIPT,
    OUT_OF_SUPPLIES
  );

  TSmelter = record(TBaseBankScript)
    State: ESmelterState;

    Furnace: TRSObject;

    Ore1: TRSBankWithdrawItem;
    Ore2: TRSBankWithdrawItem;

    Bar: TRSBankDepositItem;
    Materials: TRSItemArray;
    Smelting: Boolean;

    BarAmount: Int32;
    Multiplier: Int32;

    UseOre2: Boolean;
    UseRing: Boolean;
  end;


procedure TAntiban.Setup(); override;
begin
  Self.Skills := [ERSSkill.Smithing, ERSSkill.TOTAL];
  Self.MinZoom := 0;
  Self.MaxZoom := 40;

  inherited;
end;

procedure TSmelter.SetupFurnaces();
begin
  case CurrentFurnace of
    ERSFurnace.EDGEVILLE_FURNACE:
      begin
        RSW.Setup([RSRegions.VARROCK]);
        Furnace := RSObjects.EdgevilleFurnace;
      end;

    ERSFurnace.FALADOR_FURNACE:
      begin
        RSW.Setup([RSRegions.FALADOR]);
        Furnace := RSObjects.FaladorFurnace;
      end;
  end;
end;

procedure TSmelter.SetupItems();
begin
  Bar.Quantity := BANK_DEPOSIT_ALL;

  case CurrentBar of
    ERSBarType.BRONZE:
      begin
        Bar.Item := 'Bronze bar';
        Ore1 := ['Copper ore', 14, False];
        Ore2 := ['Tin ore', 14, False];
      end;

    ERSBarType.IRON:
      begin
        Bar.Item := 'Iron bar';
        Ore1 := ['Iron ore', BANK_WITHDRAW_ALL, False];
      end;

    ERSBarType.SILVER:
      begin
        Bar.Item := 'Silver bar';
        Ore1 := ['Silver ore', BANK_WITHDRAW_ALL, False];
      end;

    ERSBarType.STEEL:
      begin
        Bar.Item := 'Steel bar';
        Ore1 := ['Iron ore', 9, False];
        Ore2 := ['Coal', 18, False];
      end;

    ERSBarType.GOLD:
      begin
        Bar.Item := 'Gold bar';
        Ore1 := ['Gold ore', BANK_WITHDRAW_ALL, False];
      end;

    ERSBarType.MITHRIL:
      begin
        Bar.Item := 'Mithril bar';
        Ore1 := ['Mithril ore', 5, False];
        Ore2 := ['Coal', 20, False];
      end;

    ERSBarType.ADAMANT:
      begin
        Bar.Item := 'Adamantite bar';
        Ore1 := ['Adamantite ore', 4, False];
        Ore2 := ['Coal', 24, False];
      end;

    ERSBarType.RUNITE:
      begin
        Bar.Item := 'Runite bar';
        Ore1 := ['Runite ore', 3, False];
        Ore2 := ['Coal', 27, False];
      end;

    ERSBarType.MOLTEN_GLASS:
      begin
        Bar.Item := 'Molten glass';
        Ore1 := ['Soda ash', 14, False];
        Ore2 := ['Bucket of sand', 14, False];
        Antiban.Skills := [ERSSkill.CRAFTING, ERSSkill.TOTAL];
      end;
  end;

  Self.Materials += Ore1.Item;
  Self.ActionProfit := (ItemData.GetAverage(Bar.Item)) -
                  (ItemData.GetAverage(Ore1.Item) * Ore1.Quantity);

  if Ore2 <> [] then
  begin
    Self.ActionProfit -= (ItemData.GetAverage(Ore2.Item) * Round(Ore2.Quantity / Ore1.Quantity));
    Materials += Ore2.Item;
  end;
end;


procedure TSmelter.Init(MaxActions: Int32; MaxTime: Int64); override;
begin
  inherited;

  Self.SetupFurnaces();
  Self.SetupItems();

  if WLSettings.RemoteInput.HUDDebug then
    DebugObjectArray += Furnace;
end;


function TSmelter.Withdraw(): Boolean; overload;
var
  invCount: Int32;
  item: TRSBankWithdrawItem;
  Find1, Find2: Boolean;
begin
  Find1 := Inventory.ContainsItem(Self.Ore1.Item);
  Find2 := (Self.Ore2 = []) or Inventory.ContainsItem(Self.Ore2.Item);

  if Find1 then
  begin
    if Find2 then
      Exit;
    item := Self.Ore2;
    item.Quantity := Self.Ore1.Quantity;
  end
  else if Find2 or (Self.Ore1.Quantity > Self.Ore2.Quantity) or
          ((Self.Ore1.Quantity = Self.Ore2.Quantity) and Antiban.BioDice) then
  begin
    item := Self.Ore1;
    if Self.Ore2 <> [] then
      item.Quantity := Self.Ore2.Quantity;
  end
  else
    item := Self.Ore2;

  invCount := Inventory.Count();
  if Result := Bank.WithdrawItem(BankTab, item, True) then
    WaitUntil(Inventory.Count > invCount, 100, 3000);

  if Self.BankEmpty := ((not Result and Bank.IsOpen()) and
                    not Bank.ContainsItem(Self.BankTab, item)) then
    if Self.CollectEmpty and Self.CollectTimer.IsFinished() then
      Self.CollectEmpty := False;
end;

function TSmelter.Deposit(): Boolean;
var
  itemCount: Int32;
begin
  itemCount := Inventory.Count();

  if Bank.DepositAll() then
    Result := WaitUntil(Inventory.Count() = 0, 100, 3000);

  if Result then
  begin
    Self.TotalActions += itemCount;
    Self.TotalProfit := Self.TotalActions * Self.ActionProfit;
  end;
end;


function TSmelter.OpenFurnace(): Boolean;
begin
  if Furnace.WalkSelectOption(['Smelt']) then
  begin
    Minimap.WaitPlayerMoving(300, 10000);
    Result := Make.IsOpen(8000);
  end;
end;


function TSmelter.SmeltBar(): Boolean;
begin
  if not Inventory.ContainsAll(Materials) then
    Exit;

  if Result := Make.Select(ToStr(Bar.Item), MAKE_QUANTITY_ALL, Antiban.BioDice(EBioBehavior.KEYBOARD_CHAT_CHANCE)) then
    Smelting := XPBar.WaitXP(4000);
end;

function TSmelter.IsMakingItem(WaitTime: Int32 = 3200): Boolean;
var
  ItemCount: Int32 := Inventory.CountItem(Self.Bar.Item);
begin
  if Result := WaitUntil(ItemCount < Inventory.CountItem(Self.Bar.Item), 100, WaitTime) then
    WL.Activity.Restart();
end;

function TSmelter.WaitSmelt(): Boolean;
begin
  if Smelting then
    Result := Smelting := (Inventory.ContainsAll(Materials) and Self.IsMakingItem())
  else
    Result := Smelting := Self.IsMakingItem();

  if Result then
    WL.Activity.Restart();
end;


function TSmelter.GetState(): ESmelterState;
begin
  if WL.Activity.IsFinished() then
    Exit(ESmelterState.END_SCRIPT);

  if Chat.LeveledUp() then
  begin
    Smelting := False;
    Exit(ESmelterState.LEVEL_UP);
  end;

  if MainScreen.HasInterface() then
  begin
    Self.HoveringBank := False;
    if Bank.IsOpen() then
    begin
      if Inventory.ContainsItem(Self.Bar.Item) then
        Exit(ESmelterState.DEPOSIT_BAR);

      if Inventory.HasRandomItem(Materials) then
        Exit(ESmelterState.DEPOSIT_RANDOM_ITEM);

      if not Self.BankEmpty and not Inventory.ContainsAll(Materials) then
        Exit(ESmelterState.WITHDRAW_MATERIALS);
    end;

    if not Self.CollectEmpty and CollectBox.IsOpen() then
      Exit(ESmelterState.HANDLE_COLLECT);

    Exit(ESmelterState.CLOSE_INTERFACE);
  end;

  if Inventory.ContainsAll(Materials) then
  begin
    if Self.Smelting then
    begin
      if not Self.HoveringBank then
      begin
        Self.CountItemsLeft(Self.Ore1.Item);

        if Self.ShouldHoverBank() then
          Exit(ESmelterState.HOVER_BANK);
      end;

      Exit(ESmelterState.WAIT_SMELT);
    end;

    if Make.IsOpen() then
      Exit(ESmelterState.SMELT_BAR);

    Exit(ESmelterState.OPEN_FURNACE);
  end;

  if Self.BankEmpty then
  begin
    if Self.CollectEmpty then
      Exit(ESmelterState.OUT_OF_SUPPLIES);

    Exit(ESmelterState.OPEN_COLLECT);
  end;

  if Self.BankEmpty then
    Exit(ESmelterState.OPEN_COLLECT);

  Exit(ESmelterState.OPEN_BANK);
end;

function TSmelter.Terminate(): Boolean; override;
begin
  Result := inherited;
  if Result then
    for 0 to 5 do
      if Result := Bank.WithdrawItem(BankTab, [Bar.Item, BANK_WITHDRAW_ALL, True], True) then
        Break;
end;

procedure TSmelter.Run(MaxActions: Int32; MaxTime: Int64);
begin
  Self.Init(MaxActions, MaxTime);

  repeat
    State := Self.GetState();
    Self.SetAction(ToStr(State));

    case State of
      ESmelterState.HOVER_BANK: Self.HoverBank();
      ESmelterState.OPEN_BANK: Bank.WalkOpen();
      ESmelterState.WITHDRAW_MATERIALS: Self.Withdraw();
      ESmelterState.DEPOSIT_BAR: Self.Deposit();
      ESmelterState.DEPOSIT_RANDOM_ITEM: Bank.DepositAllBut(Self.BankTab, Self.Materials);

      ESmelterState.OPEN_COLLECT: CollectBox.WalkOpen();
      ESmelterState.HANDLE_COLLECT: Self.HandleCollectBox(Self.Materials);

      ESmelterState.OPEN_FURNACE: Self.OpenFurnace();
      ESmelterState.SMELT_BAR: Self.SmeltBar();
      ESmelterState.WAIT_SMELT: Self.WaitSmelt();

      ESmelterState.CLOSE_INTERFACE: MainScreen.CloseInterface();
      ESmelterState.CLOSE_CONTEXT: ChooseOption.Close();
      ESmelterState.LEVEL_UP: Chat.HandleLevelUp();

      ESmelterState.END_SCRIPT, ESmelterState.OUT_OF_SUPPLIES: Break;
    end;

    Self.DoAntiban();
  until Self.ShouldStop();

  if not Self.Terminate() then
    TerminateScript(Self.Name + ' didn''t terminate properly. Stopping execution.');
end;

var
  Smelter: TSmelter;

{$IFNDEF SCRIPT_CHAIN}
{$IFDEF SCRIPT_GUI}
type
  TSmelterConfig = record(TScriptForm)
    BarTypeSelector: TLabeledCombobox;
    FurnaceSelector: TLabeledCombobox;
  end;

procedure TSmelterConfig.StartScript(sender: TObject); override;
begin
  CurrentBar := ERSBarType(Self.BarTypeSelector.getItemIndex());
  CurrentFurnace := ERSFurnace(Self.FurnaceSelector.getItemIndex());
  inherited;
end;

procedure TSmelterConfig.Run(); override;
var
  tab: TTabSheet;
begin
  Self.Setup('Wasp Smelter');
  Self.Start.SetOnClick(@Self.StartScript);

  Self.AddTab('Script Settings');
  tab := Self.Tabs[High(Self.Tabs)];

  Self.CreateAccountManager(tab);

  with Self.BarTypeSelector do
  begin
    Create(tab);
    SetCaption('Bar type:');
    SetLeft(TControl.AdjustToDPI(50));
    SetTop(TControl.AdjustToDPI(200));
    SetStyle(csDropDownList);
    AddItemArray(['Bronze', 'Iron', 'Silver', 'Steel', 'Gold', 'Mithril', 'Adamantite', 'Runite', 'Molten glass']);
    SetItemIndex(Ord(CurrentBar));
  end;

  with Self.FurnaceSelector do
  begin
    Create(tab);
    SetCaption('Furnace:');
    SetLeft(Self.BarTypeSelector.GetRight() + TControl.AdjustToDPI(15));
    SetTop(Self.BarTypeSelector.GetTop());
    SetStyle(csDropDownList);
    AddItemArray(['Edgeville', 'Falador']);
    SetItemIndex(Ord(CurrentFurnace));
  end;

  Self.CreateVersionPanel(tab);

  Self.CreateAntibanManager();
  Self.CreateBankSettings();
  Self.CreateWaspLibSettings();
  Self.CreateAPISettings();

  inherited;
end;

var
  SmelterConfig: TSmelterConfig;
{$ENDIF}
{$ENDIF}

{$IFNDEF SCRIPT_CHAIN}
begin
  {$IFDEF SCRIPT_GUI}
  SmelterConfig.Run();
  {$ENDIF}
  Smelter.Run(WLSettings.MaxActions, WLSettings.MaxTime);
end.
{$ENDIF}
