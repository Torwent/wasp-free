{$UNDEF SCRIPT_ID}{$DEFINE SCRIPT_ID := '0fb23f9b-3bc2-41cd-8670-3fec0eb256f1'}
{$UNDEF SCRIPT_REVISION}{$DEFINE SCRIPT_REVISION := '15'}
{$IFDEF WINDOWS}{$DEFINE SCRIPT_GUI}{$ENDIF}
{$I SRL-T/osr.simba}
{$I WaspLib/osr.simba}

begin
  Login.PlayerIndex := 0;
  StatsPayload.Username := '';
end;

var
  FoodAmount: Int32 := 20;
  NeckAmount: Int32 := 4;

const
  BANK_POLYGON: TPointArray = [[5987, 3297], [6015, 3297], [6015, 3330], [5987, 3330]] ;

//Enum of script states. used in the GetState() function to decide what we got to do next.
type
  EThieverState = (
    OPEN_CHAT,
    CHANGE_CHAT_OFF,
    CHANGE_CHAT_FILTER,
    IS_UPSTAIRS,
    WALK_TO_BANK,

    OPEN_BANK,
    OPEN_COLLECT,
    DEPOSIT_ITEMS,
    WITHDRAW_NECK,
    WITHDRAW_FOOD,
    HANDLE_COLLECT,
    CLOSE_INTERFACE,

    EQUIP_ROGUE,
    CHECK_NECK,
    EQUIP_NECK,
    EAT_FOOD,

    FIND_KNIGHT,
    PICKPOCKET,
    OPEN_POUCH,

    CLOSE_CONTEXT,

    WAIT_STATE,

    END_SCRIPT
  );

//script record, all variables of the script should go here.
  TThiever = record(TBaseBankScript)
    //init variables (to be set while the script is initiates)
    State: EThieverState;

    Knight: TRSNPC;

    CoinPouch: TRSItem;
    Necklace: TRSItem;
    ValuableItem: TRSItem;

    LootArray: TRSItemArray;
    KeepArray: TRSItemArray;
    BankArray: TRSItemArray;
    RogueEquipment: TRSItemArray;

    NeckTimer: TCountDown;

    //run variables (to be used while the script is running)
    HasNeck: Boolean;
    FreshNeck: Boolean;

    CoinPouchLimit: Int32;
    MaxHit: Int32;
    NextHeal: Int32;
  end;


//Extra antiban specifically for this script.
procedure TAntiban.Setup(); override;
begin
  inherited;

  Antiban.Skills := [ERSSkill.THIEVING, ERSSkill.TOTAL];
  Antiban.MinZoom := 15;
  Antiban.MaxZoom := 60;
end;

procedure TThiever.CheckRogueEquipment();
var
  ProfitMultiplier: Double;
begin
  if Inventory.Open() then
  begin
    if Inventory.ContainsItem('Rogue mask') then
    begin
      ProfitMultiplier += 0.2;
      Self.RogueEquipment += 'Rogue mask';
    end;

    if Inventory.ContainsItem('Rogue top') then
    begin
      ProfitMultiplier += 0.2;
      Self.RogueEquipment += 'Rogue top';
    end;

    if Inventory.ContainsItem('Rogue trousers') then
    begin
      ProfitMultiplier += 0.2;
      Self.RogueEquipment += 'Rogue trousers';
    end;

    if Inventory.ContainsItem('Rogue gloves') then
    begin
      ProfitMultiplier += 0.2;
      Self.RogueEquipment += 'Rogue gloves';
    end;

    if Inventory.ContainsItem('Rogue boots') then
    begin
      ProfitMultiplier += 0.2;
      Self.RogueEquipment += 'Rogue boots';
    end;

    //This are not part of rogue equipment but are useful while thieving
    if Inventory.ContainsItem('Thieving cape') then
      Self.RogueEquipment += 'Thieving cape'
    else if Inventory.ContainsItem('Thieving cape(t)') then
      Self.RogueEquipment += 'Thieving cape(t)';

    //Lava staves for shadow veil spell
    if Inventory.ContainsItem('Lava battlestaff') then
      Self.RogueEquipment += 'Lava battlestaff'
    else if Inventory.ContainsItem('Mystic lava staff') then
      Self.RogueEquipment += 'Mystic lava staff'
    else if Inventory.ContainsItem(21200) then
      Self.RogueEquipment += 21200;
  end;

  if Equipment.Open() then
  begin
    if Equipment.ContainsItem('Rogue mask') then
    begin
      ProfitMultiplier += 0.2;
      Self.RogueEquipment += 'Rogue mask';
    end;

    if Equipment.ContainsItem('Rogue top') then
    begin
      ProfitMultiplier += 0.2;
      Self.RogueEquipment += 'Rogue top';
    end;

    if Equipment.ContainsItem('Rogue trousers') then
    begin
      ProfitMultiplier += 0.2;
      Self.RogueEquipment += 'Rogue trousers';
    end;

    if Equipment.ContainsItem('Rogue gloves') then
    begin
      ProfitMultiplier += 0.2;
      Self.RogueEquipment += 'Rogue gloves';
    end;

    if Equipment.ContainsItem('Rogue boots') then
    begin
      ProfitMultiplier += 0.2;
      Self.RogueEquipment += 'Rogue boots';
    end;


    //This are not part of rogue equipment but are useful while thieving
    if Equipment.ContainsItem('Thieving cape') then
      Self.RogueEquipment += 'Thieving cape'
    else if Equipment.ContainsItem('Thieving cape(t)') then
      Self.RogueEquipment += 'Thieving cape(t)';

    //Lava staves for shadow veil spell
    if Equipment.ContainsItem('Lava battlestaff') then
      Self.RogueEquipment += 'Lava battlestaff'
    else if Equipment.ContainsItem('Mystic lava staff') then
      Self.RogueEquipment += 'Mystic lava staff'
    else if Equipment.ContainsItem(21200) then
      Self.RogueEquipment += 21200;
  end;

  Self.ActionProfit += Round(Self.ActionProfit * ProfitMultiplier);
end;

procedure TThiever.SetupKnight();
begin
  Self.Knight.SetupCommon();
  Self.Knight.UpText := ['Knight', 'of', 'Ardougne'];
  Self.Knight.Finder.Colors += CTS2(10620793, 19, 0.22, 0.31);
  Self.Knight.TileArray := [[6006, 3315]];
  Self.Knight.ShapeArray := [[Self.Knight.TileArray, [1, 1, 6]]];
  Self.Knight.DotFilter.Setup(BANK_POLYGON, True);
  Self.Knight.Filter.Walker := True;
end;


procedure TThiever.Init(MaxActions: Int32; MaxTime: Int64); override;
begin
  Self.Name :='Ardougne Knights';

  inherited;

  Self.RSW.Setup('x5723y3038');
  Self.ScriptBank := RSObjects.ArdougneSouthBank;
  Self.SetupKnight();

  FoodHandler.Amount := FoodAmount;

  Self.CoinPouch := 'Coin pouch';
  Self.Necklace := 'Dodgy necklace';
  Self.KeepArray := ['Coin pouch', 'Coins', 'Cosmic rune', 'Dodgy necklace'];
  Self.BankArray := ['Jug', 'Pie dish'];

  Self.MaxHit := 3;
  Self.ActionProfit := 50;

  Self.NeckTimer.Init(140000);

  if not RSClient.IsLoggedIn() then
    Login.LoginPlayer();

  if Antiban.BioDice then
    Options.SetNPCAttackOption(ERSAttackOption.HIDDEN)
  else
    Options.SetNPCAttackOption(ERSAttackOption.ALWAYS_RIGHT_CLICK);

  Self.CheckRogueEquipment();
  Self.HasNeck := Equipment.ContainsItem(Self.Necklace);

  if WLSettings.RemoteInput.ScriptDebug then
  begin
    DebugObjectArray += @Self.ScriptBank;
    DebugNPCArray += @Self.Knight;
  end;
end;


function TThiever.HasRogue(): Boolean;
begin
  Result := Inventory.ContainsAny(Self.RogueEquipment);
end;

function TThiever.EquipRogue(): Boolean;
var
  Item: TRSItem;
begin
  for Item in Self.RogueEquipment do
    if Inventory.ClickItem(Item) then
      Wait(200, 300);

  if Inventory.ClickItem(21200) then
    Wait(200, 300);

  Result := not Self.HasRogue() and not Inventory.ContainsItem('Lava battlestaff');
end;



function TThiever.HasSpace(): Boolean;
begin            //we need at least 2 spaces always.
  Result := (Inventory.CountEmptySlots() >= 2) or
            (Inventory.ContainsItem(Self.CoinPouch) and Inventory.ContainsItem('Coins')
             and not Inventory.ContainsAny(Self.BankArray));
end;


function TThiever.IsLowHP(): Boolean;
var
  CurrentHP: Int32 := Minimap.GetHPLevel();
begin
  if Self.NextHeal < 1 then
    Self.NextHeal := Round(Antiban.GetUniqueNumber(Self.MaxHit * 4, Self.MaxHit + 1, Self.MaxHit * 15)) + Random(-Self.MaxHit, Self.MaxHit);

  if Self.NextHeal >= BaseStats.GetCurrentLevel(ERSSkill.HITPOINTS) then
    Self.NextHeal := Round(BaseStats.GetCurrentLevel(ERSSkill.HITPOINTS) / 2) + Random(0, Self.MaxHit);

  if Self.NextHeal <= Self.MaxHit then
    Self.NextHeal += Random(0, Self.MaxHit);

  Result := (CurrentHP <= Self.NextHeal) or (CurrentHP <= Self.MaxHit);
end;

function TThiever.IsPouchFull(): Boolean;
begin
  if Self.CoinPouchLimit < 1 then
    Self.CoinPouchLimit := SRL.TruncatedGauss(28, 1);

  Result := Inventory.CountItemStack(Self.CoinPouch) >= Self.CoinPouchLimit;
end;


function TThiever.OpenCoinPouch(): Boolean;
var
  PouchCount: Int32;
begin
  PouchCount := Inventory.CountItemStack(Self.CoinPouch);
  if Inventory.ClickItem(Self.CoinPouch) then
  begin
    Wait(600);
    Result := WaitUntil(not Inventory.ContainsItem(Self.CoinPouch), 100, 2000);
  end;

  if Result then
  begin
    Self.CoinPouchLimit := SRL.TruncatedGauss(28, 1);
    Self.TotalActions += PouchCount;
    WL.Activity.Restart();
  end;
end;



function TThiever.CheckFailed(StartingHP: Int32): Boolean;
var
  HasHitSplat: Boolean;
begin
  HasHitSplat := MainScreen.FindHitsplats(MainScreen.GetPlayerBox()) <> [];

  Result := (Minimap.GetHPLevel() < StartingHP) or HasHitSplat;

  if Result then
    Self.OpenCoinPouch();
end;

function TThiever.NecklaceExpired(): Boolean;
begin
  Result := Chat.FindMessage('crumbles', [CHAT_COLOR_LIGHT_RED]);
  Self.HasNeck := not Result;
end;

function TThiever.Dodged(): Boolean;
begin
  Result := Chat.FindMessage('dodgy');
end;



function TThiever.Heal(): Boolean;
begin
  repeat
    if not Inventory.Consume(FOOD_CONSUMABLE) then
      Exit;

    Wait(300, 500);
  until Result := (Minimap.GetHPPercent() >= (50 + Antiban.GetUniqueNumber(20)));

  if Result then
    Self.NextHeal := 0; //This will be reset when TThiever.IsLowHP() is called.
end;

function TThiever.EquipNeck(): Boolean;
begin
  if Result := Inventory.ClickItem(Self.Necklace) then Wait(300, 400);

  Self.FreshNeck := Self.HasNeck := Result;
end;

function TThiever.CheckNeck(): Boolean;
begin
  Result := Self.HasNeck := Equipment.ContainsItem(Self.Necklace);

  Self.NeckTimer.Restart(Random(-30000, 60000));
end;


function TThiever.HoverKnight(): Boolean;
var
  t: TCountDown;
  TPA: TPointArray;
  ATPA: T2DPointArray;
  P: TPoint;
  B: TBox;
begin
  t.Init(1800);

  repeat
    SRL.FindColors(TPA, $00FFFF, MainScreen.Bounds());
    ATPA := TPA.Cluster(3, 1);
    ATPA.FilterDimensions(32, 10, 32, 10);
  until (ATPA <> []) or t.IsFinished();

  if ATPA <> [] then
  begin
    ATPA.SortByMiddle(MainScreen.GetPlayerBox().Middle());
    TPA := ATPA[0];
    P := TPA.Mean();
    P.X += MainScreen.ConvertDistance(7);
    P.Y += MainScreen.ConvertDistance(18);
    B := Box(P, MainScreen.ConvertDistance(12), MainScreen.ConvertDistance(20));
    Mouse.Move(B);
    Result := MainScreen.IsUpText(Self.Knight.UpText);
  end
  else if Length(Minimap.GetDots(ERSMinimapDot.PLAYER)) < 10 then
    Result := Self.Knight.Hover(0);

  if not Result and MainScreen.IsUpText('Climb') then
    Antiban.RandomRotate();
end;

function TThiever.StealKnight(): Boolean;
var
  Slot: Int32 := Inventory.GetSelectedSlot();
  CurrentHP: Int32 := Minimap.GetHPLevel();
begin
  if Slot > -1 then Inventory.ClickSlot(Slot);

  if Result := Self.HoverKnight() then
  begin
    Mouse.Click(MOUSE_LEFT);

    if MainScreen.DidYellowClick() then
      Exit(False);

    if not Self.NecklaceExpired() then
      Self.FreshNeck := False;

    Self.CheckFailed(CurrentHP);
    WL.Activity.Restart();
  end;
end;


function TThiever.Deposit(): Boolean;
begin
  Result := Bank.DepositItemArray(Self.BankTab, Self.BankArray, False);
end;

function TThiever.Withdraw(Item: TRSItem; Quantity: Int32): Boolean;
var
  ItemCount: Int32 := Inventory.CountItem(Item);
  QuantityNeeded: Int32 := Quantity - ItemCount;
  InvCount: Int32;
begin
  InvCount := Inventory.Count();
  if Result := Bank.WithdrawItem(Self.BankTab, [Item, QuantityNeeded, False], False) then
    WaitUntil(Inventory.Count() > InvCount, 100, 3000);

  if not Result and (Item = Self.Necklace) then
    NeckAmount := 0;
end;

procedure TThiever.HandleUpstairs();
const
  Finder: TRSObjectFinder = [[[5395288, 3, 0.95, 0.22]], [], 0, 0, 0, 0, 0, 0, 0];
var
  TPA: TPointArray;
begin
  repeat
    for TPA in MainScreen.FindObject(Finder, MainScreen.GetPlayerBox.Expand(10)) do
    begin
      Mouse.Move(TPA);
      if MainScreen.IsUpText('down') then
      begin
        Mouse.Click(MOUSE_LEFT);
        if MainScreen.DidRedClick() then
        begin
          WaitUntil(Minimap.OnGroundLevel(), 100, 5000);
          Wait(2000);
        end;
      end;
    end;
  until Minimap.OnGroundLevel();
end;

//GetState() is responsible for figuring out what we are doing and what we will be doing next.
function TThiever.GetState(): EThieverState;
begin
  if WL.Activity.IsFinished() then
    Exit(END_SCRIPT);

  if ChooseOption.IsOpen() then
    Exit(CLOSE_CONTEXT);

  if MainScreen.HasInterface() then
  begin
    if Bank.IsOpen() then
    begin
      if Inventory.ContainsAny(Self.BankArray) then
        Exit(DEPOSIT_ITEMS);

      if Inventory.IsFull() then
      begin
        if (Inventory.CountItem(Self.Necklace) < NeckAmount) or
           not Inventory.HasEnoughConsumable(FOOD_CONSUMABLE) then
        Bank.DepositAll();
      end;

      if Inventory.CountItem(Self.Necklace) < NeckAmount then
        Exit(WITHDRAW_NECK);
      if not Inventory.HasEnoughConsumable(FOOD_CONSUMABLE) then
        Exit(WITHDRAW_FOOD);
    end;

    Exit(CLOSE_INTERFACE);
  end;

  if Self.IsPouchFull() then
    Exit(OPEN_POUCH);

  if ChatButtons.GetState(ERSChatButton.PUBLIC_CHAT) <> ERSChatButtonState.DISABLED then
    Exit(CHANGE_CHAT_OFF);

  if ChatButtons.GetState(ERSChatButton.GAME_CHAT) <> ERSChatButtonState.ENABLED then
    Exit(CHANGE_CHAT_FILTER);

  if not ChatButtons.IsActive(ERSChatButton.GAME_CHAT) then
    Exit(OPEN_CHAT);

  if Self.NeckTimer.IsFinished() then
    Exit(CHECK_NECK);

  if Self.HasRogue() then
    Exit(EQUIP_ROGUE);

  if not Self.HasSpace() then
  begin
    if Inventory.ContainsAny(Self.BankArray) then
      Exit(OPEN_BANK);
    if Inventory.FindConsumable(FOOD_CONSUMABLE) then
      Exit(EAT_FOOD);

    Exit(OPEN_BANK);
  end;

  if (NeckAmount > 0) and not Self.FreshNeck and (not Self.HasNeck or Self.NecklaceExpired) then
  begin
    if Inventory.ContainsItem(Self.Necklace) then
      Exit(EQUIP_NECK);

    Exit(OPEN_BANK);
  end;

  if Self.IsLowHP() then
  begin
    if Inventory.FindConsumable(FOOD_CONSUMABLE) then
      Exit(EAT_FOOD);

    Exit(OPEN_BANK);
  end;

  if not Minimap.OnGroundLevel() then
    Exit(IS_UPSTAIRS);

  if not Self.RSW.InPoly(BANK_POLYGON) then
    Exit(WALK_TO_BANK);

  Exit(PICKPOCKET);
end;

//Main loop.
procedure TThiever.Run(MaxActions: Int32; MaxTime: Int64);
begin
  Self.Init(MaxActions, MaxTime);

  repeat
    State := Self.GetState();
    Action := ToStr(State);
    ExtraInfo := ' FreshNeck: ' + ToStr(FreshNeck) +
                 ', HasNeck: ' + ToStr(HasNeck) +
                 ', NextHeal: ' + ToStr(NextHeal);
    Self.ProcessWhileWaiting();

    case Self.State of
      OPEN_BANK: Bank.WalkOpen(Self.ScriptBank);
      DEPOSIT_ITEMS: Self.Deposit();
      WITHDRAW_FOOD: Bank.WithdrawConsumable(FOOD_CONSUMABLE);
      WITHDRAW_NECK: Self.Withdraw(Self.Necklace, NeckAmount);
      CLOSE_INTERFACE: MainScreen.CloseInterface();

      OPEN_CHAT: ChatButtons.Open(ERSChatButton.GAME_CHAT);
      CHANGE_CHAT_OFF: ChatButtons.ChangeState(ERSChatButton.PUBLIC_CHAT, ERSChatButtonState.DISABLED);
      CHANGE_CHAT_FILTER: ChatButtons.ChangeState(ERSChatButton.GAME_CHAT, ERSChatButtonState.ENABLED);

      PICKPOCKET: Self.StealKnight();
      OPEN_POUCH: Self.OpenCoinPouch();

      EAT_FOOD: Self.Heal();
      EQUIP_NECK: Self.EquipNeck();
      CHECK_NECK: Self.CheckNeck();
      CLOSE_CONTEXT: ChooseOption.Close();
      IS_UPSTAIRS: Self.HandleUpstairs();
      WALK_TO_BANK: Self.RSW.WebWalk(Self.Knight.TileArray, 30, 0.2);
      EQUIP_ROGUE: Self.EquipRogue();

      END_SCRIPT: Break;
    end;

    Self.DoAntiban();       //Checks for due antiban
  until Self.ShouldStop();  //Stops if MaxAtions or MaxTime have been reached.
end;

var
  Thiever: TThiever;

function TRSInventory.HasEnoughConsumable(ConsumableType: ERSConsumable): Boolean; override
var
  consumableHandler: PConsumableHandler := GetHandler(ConsumableType);
begin
  Result := Self.CountPoints(ConsumableType) >= consumableHandler^.Amount;
end;

{$IFDEF SCRIPT_GUI}
type
  TThieverConfig = record(TScriptForm)
    NeckAmountEdit: TLabeledEdit;
  end;

procedure TThieverConfig.StartScript(Sender: TObject);
begin
  Self.Init(Sender);

  FoodAmount := StrToInt(Self.WLPanel.FoodPanel.ConsumableAmount.GetText());
  NeckAmount := StrToInt(Self.NeckAmountEdit.getText());
end;

procedure TThieverConfig.Setup(UseFood, UsePrayer, UseBank: Boolean); override;
begin
  inherited;

  with Self.NeckAmountEdit do
  begin
    Init(Self.SSPanel.Panel);
    SetCaption('Necklaces amount:');
    SetLeft(150);
    SetTop(50);
    SetWidth(110);
    SetText(ToStr(NeckAmount));
  end;

  Self.WLPanel.FoodPanel.ConsumableAmount.SetText(ToStr(FoodAmount));
  Self.WLPanel.FoodPanel.ConsumableAmount.SetVisible(True);
  Self.WLPanel.StartButton.setOnClick(@Self.StartScript);
end;

procedure TThieverConfig.Run(); override;
begin
  Self.Setup(True, False, False);

  inherited;
end;

var
  ThieverConfig: TThieverConfig;
{$ENDIF}

begin
  {$IFDEF SCRIPT_GUI}
  Sync(@ThieverConfig.Run);
  {$ENDIF}
  Thiever.Run(WLSettings.MaxActions, WLSettings.MaxTime);
end.
