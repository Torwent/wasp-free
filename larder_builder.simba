{$UNDEF SCRIPT_ID}{$DEFINE SCRIPT_ID := 'bee77a9c-8ff3-4b11-b0fc-2fbbaa788d15'}
{$UNDEF SCRIPT_REVISION}{$DEFINE SCRIPT_REVISION := '10'}
{$IFDEF WINDOWS}{$DEFINE SCRIPT_GUI}{$ENDIF}
{$I SRL-T/osr.simba}
{$I WaspLib/osr.simba}

begin
  Login.PlayerIndex := 0;
  StatsPayload.Username := '';
end;

type
  ERSNPC = (PHIALS, SERVANT);
  ERSLarder = (REGULAR_LARDER, OAK_LARDER);

var
  CurrentNPC: ERSNPC := PHIALS;
  CurrentLarder: ERSLarder := OAK_LARDER;

type
  EState = (
    LEVEL_UP,
    WAIT_STATE,

    EXIT_HOUSE,
    ENTER_HOUSE,

    BUILD_LARDER,
    HANDLE_FURNITURE_UI,
    DESTROY_LARDER,
    HANDLE_LARDER_CHAT,
    HANDLE_PIN,

    GET_PLANKS,
    HANDLE_PHIALS_CHAT,
    CALL_SERVANT,
    TALK_SERVANT,
    HANDLE_SERVANT,

    END_SCRIPT
  );

  TBuilder = record(TBaseWalkerScript)
    State: EState;
    POH: TRSWalker;

    BuiltLarder: TRSObject;
    LarderSpace: TRSObject;
    ServantNPC: TRSNPC;

    LarderIsBuilt: Boolean;
    UsePhials: Boolean;
    WaitingServant: Boolean;

    Plank: TRSItem;
    NotedPlanksSlot: Int32;
    MinInvCount: Int32;
  end;


procedure TBuilder.SetupNPC();
begin
  case CurrentNPC of
    PHIALS:
      UsePhials := True;

    SERVANT:
      begin
        UsePhials := False;

        ServantNPC.Setup(['Talk-to']);
        ServantNPC.Filter.Finder := False;
        ServantNPC.Filter.MinimapDot := True;
      end;
  end;
end;

procedure TBuilder.SetupPlank();
begin
  case CurrentLarder of
    REGULAR_LARDER:
      begin
        Plank := 'Plank';
        MinInvCount := 12;
      end;

    OAK_LARDER:
      begin
        Plank := 'Oak plank';
        MinInvCount := 11;
      end;
  end;
end;

procedure TBuilder.SetupObjects();
begin
  BuiltLarder.Setup(1.5, 7, [[3604, 86]]);
  BuiltLarder.Setup(['Larder']);
  BuiltLarder.Filter.Finder := False;
  BuiltLarder.Filter.UpText := False;
  BuiltLarder.Finder.ColorClusters += [CTS2(6916239, 10, 0.33, 1.18), CTS2(4154749, 9, 0.13, 0.65), 150];

  LarderSpace.Setup(1.5, 7, [[3604, 86]]);
  LarderSpace.Setup(['2 more']);
  LarderSpace.Filter.Finder := False;
  LarderSpace.Filter.UpText := False;
  LarderSpace.Finder.ColorClusters += [CTS2(10529472, 16, 0.25, 0.95), CTS2(8884636, 16, 0.16, 0.18), 80];
end;

procedure TAntiban.Setup(); override;
begin
  inherited;

  Antiban.Skills := [ERSSkill.CONSTRUCTION, ERSSkill.TOTAL];
  Antiban.MinZoom := 0;
  Antiban.MaxZoom := 35;
end;

procedure TBuilder.Init(MaxActions: Int32; MaxTime: Int64); override;
begin
  inherited;
  RSW.Setup([RSRegions.RIMMINGTON, RSRegions.TEMP_POH]);

  Self.SetupNPC();
  Self.SetupObjects();
  Self.SetupPlank();

  ActionProfit -= (ItemData.GetAverage(Plank) * 8 + 40);

  if not RSClient.IsLoggedIn() then
    Login.LoginPlayer();

  if CurrentNPC = PHIALS then
    case CurrentLarder of
      REGULAR_LARDER: Inventory.FindItem('noted Plank', NotedPlanksSlot);
      OAK_LARDER: Inventory.FindItem('noted Oak plank', NotedPlanksSlot);
    end;

  if WLSettings.RemoteInput.ScriptDebug then
  begin
    DebugObjectArray += BuiltLarder;
    DebugObjectArray += LarderSpace;
    DebugObjectArray += RSObjects.POHPortal;
    DebugNPCArray += RSNPCs.Phials;
    DebugNPCArray += ServantNPC;
  end;
end;


function TBuilder.EnterHouse(): Boolean;
begin
  if RSObjects.POHPortal.WalkSelectOption(['Build', 'mode']) then
  begin
    Minimap.WaitMoving();
    Result := MainScreen.WaitLoadingPOH(10000);
  end;
end;

function TBuilder.BuildLarder(): Boolean;
begin
  if LarderSpace.WalkSelectOption(['Build La']) then
  begin
    Minimap.WaitMoving();
    Result := WaitUntil(MainScreen.IsClassicOpen(), 200, 5000);
  end
  else if ChooseOption.HasOption('Remove L') then
  begin
    ChooseOption.Close();
    LarderIsBuilt := True;
  end;
end;

function TBuilder.DestroyLarder(): Boolean;
begin
  if BuiltLarder.WalkSelectOption(['Remove L']) then
  begin
    Minimap.WaitMoving;
    Result := WaitUntil(Chat.GetChatTitle = 'Really remove it?', 200, 3000);
  end
  else if ChooseOption.HasOption('Build La') then
  begin
    ChooseOption.Close;
    LarderIsBuilt := False;
  end;
end;


function TBuilder.CallServant(): Boolean;
begin
  if ServantNPC.Click then
  begin
    Minimap.WaitMoving;
    Result := WaitUntil(Chat.GetChatTitle = 'Repeat last task?',
                        SRL.TruncatedGauss(50, 1500), 5000);
  end
  else
    Result := Options.CallServant();
end;

function TBuilder.PhialsGetPlanks(): Boolean;
begin
  if not RSNPCs.Phials.Find() and not RSW.AtTile(RSNPCs.Phials.Coordinates, 8) then
    RSW.WebWalk(RSNPCs.Phials.Coordinates, 15, 0.2);

  if (Inventory.SetSelectedSlot(Self.NotedPlanksSlot) and RSNPCs.Phials.Click()) then
  begin
    Minimap.WaitFlag();
    Result := WaitUntil(Chat.GetChatTitle() = 'Select an Option', 100, 15000);
  end;
end;

function TBuilder.TalkServant(): Boolean;
begin
  if ServantNPC.Click then
  begin
    Minimap.WaitMoving;
    Result := WaitUntil(Inventory.IsFull, 100, 5000);
  end;

  WaitingServant := not Result;
end;


function TBuilder.HandleServant(): Boolean;
begin
  Result := Chat.ClickOption('Fetch', BioHash);
  if Result then
  begin
    WaitingServant := True;
    WaitUntil(Chat.GetChatTitle() <> 'Repeat last task?', 200, 3000)
  end;
end;

function TBuilder.HandleLarderChat(): Boolean;
begin
  Keyboard.PressKey(VK_1);
  Result := WaitUntil(Chat.GetChatTitle <> 'Really remove it?', 200, 3000);
  LarderIsBuilt := not Result;
  Wait(100, 150);
end;

function TBuilder.HandleFurniture(): Boolean;
begin
  case CurrentLarder of
    REGULAR_LARDER: Keyboard.PressKey(VK_1);
    OAK_LARDER: Keyboard.PressKey(VK_2);
  end;

  Result := LarderIsBuilt := WaitUntil(not MainScreen.IsClassicOpen(), 100, 3000);
  if Result then
  begin
    Self.TotalActions += 1;
    WL.Activity.Restart();
    Wait(250, 300);
  end else
    MainScreen.CloseInterface();
end;


function TBuilder.GetState(): EState;
var
  s: String;
begin
  if WL.Activity.IsFinished() then
    Exit(END_SCRIPT);

  if Chat.LeveledUp() then
    Exit(EState.LEVEL_UP);

  s := Chat.GetChatTitle();

  if s.Contains('remove') then
    Exit(EState.HANDLE_LARDER_CHAT);

  if s.Contains('Select') then
    Exit(EState.HANDLE_PHIALS_CHAT);

  if s.Contains('Repeat') then
    Exit(EState.HANDLE_SERVANT);

  if MainScreen.IsClassicOpen() then
  begin
    if BankPin.IsOpen() then
      Exit(EState.HANDLE_PIN);
    Exit(EState.HANDLE_FURNITURE_UI);
  end;

  if not Minimap.InPOH() then
  begin
    if not UsePhials or Inventory.IsFull() then
      Exit(EState.ENTER_HOUSE);
    Exit(EState.GET_PLANKS);
  end;

  if Self.LarderIsBuilt then
    Exit(EState.DESTROY_LARDER);

  if Inventory.Count() > Self.MinInvCount then
  begin
    WaitingServant := False;
    Exit(EState.BUILD_LARDER);
  end;

  if Self.UsePhials then
    Exit(EState.EXIT_HOUSE);

  if WaitingServant then
  begin
    if ServantNPC.Find() then
      Exit(EState.TALK_SERVANT);
    Exit(EState.WAIT_STATE);
  end;

  Exit(EState.CALL_SERVANT);
end;

procedure TBuilder.Run(MaxActions: Int32; MaxTime: Int64);
begin
  Self.Init(MaxActions, MaxTime);

  repeat
    State := Self.GetState();
    Self.SetAction(ToStr(State));

    case State of
      EState.LEVEL_UP: Chat.HandleLevelUp(True);

      EState.HANDLE_LARDER_CHAT: Self.HandleLarderChat();
      EState.HANDLE_PHIALS_CHAT: Keyboard.PressKey(VK_3);
      EState.HANDLE_FURNITURE_UI: Self.HandleFurniture();
      EState.HANDLE_SERVANT: Self.HandleServant();
      EState.HANDLE_PIN: if BankPin.Enter(Login.GetPlayerPin()) then MainScreen.WaitLoadingPOH(10000);

      EState.CALL_SERVANT: Self.CallServant();
      EState.TALK_SERVANT: Self.TalkServant();
      EState.EXIT_HOUSE: Options.LeaveHouse();
      EState.GET_PLANKS: Self.PhialsGetPlanks();

      EState.ENTER_HOUSE: Self.EnterHouse();
      EState.BUILD_LARDER: Self.BuildLarder();
      EState.DESTROY_LARDER: Self.DestroyLarder();

      EState.WAIT_STATE: Wait(7000, 8000);

      END_SCRIPT: Break;
    end;

    Self.DoAntiban();
  until Self.ShouldStop();
end;

var
  Builder: TBuilder;

function TRSWalkerObject._SelectHelper(action: TStringArray): Boolean; override;
begin
  if Self.CurrentUpText <> [] then
    Self.CurrentUpText := [];

  if Self.RedClicked then
  begin
    Self.RedClicked := False;
    Exit(True);
  end;

  if ChooseOption.IsOpen() and ChooseOption.Select(action) then
    Exit(True);

  if MainScreen.IsUpText(action) then
  begin
    Mouse.Click(MOUSE_LEFT);
    Exit(MainScreen.DidRedClick());
  end;

  Result := ChooseOption.Select(action, MOUSE_LEFT, True, (False));
end;

//TEMP OPTION FIXES
function TRSOptions.IsHouseOptionsOpen(): Boolean; override;
begin
  Result := SRL.CountColor(CTS2(4630611, 23, 0.04, 0.79), Self.Bounds()) > 115;
end;

function TRSOptions.CallServant(): Boolean; override;
var
  s: String;
  b: TBox;
begin
  if not Self.OpenHouseOptions() then
    Exit;

  b := Self.Bounds();

  b.X1 += 9;
  b.Y1 += 185;
  b.X2 -= 9;
  b.Y2 -= 50;

  Mouse.Click(b, MOUSE_LEFT);
  Result := WaitUntil(((s := Chat.GetChatTitle) = 'Demon butler') or
                       (s = 'Repeat last task?'), SRL.TruncatedGauss(50, 1500), 5000);
end;

function TRSOptions.LeaveHouse(): Boolean; override;
var
  b: TBox;
begin
  if not Self.OpenHouseOptions() then
    Exit;

  b := Self.Bounds();

  b.X1 += 9;
  b.Y1 += 214;
  b.X2 -= 9;
  b.Y2 -= 21;

  Mouse.Click(b, MOUSE_LEFT);
  Result := WaitUntil(not Self.IsHouseOptionsOpen(), RandomLeft(50, 1500), 5000);
end;

procedure TRSMinimap.SetCompassAngle(Degrees: Double; Accuracy: Double = 5); override;
begin
  if MainScreen.IsUpText('>') then
    ChooseOption.Select('Cancel');

  inherited;
end;

{$IFDEF SCRIPT_GUI}
type
  TBuilderConfig = record(TScriptForm)
    NPCSelector: TLabeledCombobox;
    LarderSelector: TLabeledCombobox;
    BuilderInfo: TLabel;
    ImagePanel: TImage;
    Pic: TPicture;
  end;

procedure TBuilderConfig.StartScript(Sender: TObject);
begin
  Self.Init(Sender);

  CurrentNPC := ERSNPC(Self.NPCSelector.Combobox.getItemIndex());
  CurrentLarder := ERSLarder(Self.LarderSelector.Combobox.getItemIndex());
end;

procedure TBuilderConfig.Setup(); override;
begin
  inherited;

  UpdateFormSize(Self.Form, 0, Self.Form.GetHeight() + 250);

  with Self.NPCSelector do
  begin
    Init(Self.SSPanel.Panel);
    SetCaption('Method:');
    SetLeft(5);
    SetTop(35);
    SetWidth(200);
    Combobox.setStyle(csDropDownList);
    AddItem('Phials');
    AddItem('Demon butler');
    Combobox.setItemIndex(Ord(CurrentNPC));
  end;

  with Self.LarderSelector do
  begin
    Init(Self.SSPanel.Panel);
    SetCaption('Larder:');
    SetLeft(Self.NPCSelector.GetLeft() + Self.NPCSelector.GetWidth() + 10);
    SetTop(Self.NPCSelector.GetTop());
    SetWidth(200);
    Combobox.setStyle(csDropDownList);
    AddItem('Regular');
    AddItem('Oak');
    Combobox.setItemIndex(Ord(CurrentLarder));
  end;

  with Self.BuilderInfo do
  begin
    Init(Self.SSPanel.Panel);
    setParent(Self.SSPanel.Panel);

    SetCaption('For Phials method make sure you have noted planks with you.' +
               LineEnding +
               'For Demon butler make sure you previously asked him for 24 planks and' +
               LineEnding +
               'you need to have money in the servant''s money bag.' +
               LineEnding +
               'You need a decoration that has green grass and the following layout:');

    SetLeft(5);
    SetTop(Self.NPCSelector.GetTop() + Self.NPCSelector.GetHeight() + 2);
  end;

  Self.Pic.Init();
  Self.Pic.LoadFromFile(IncludePath+'\WaspLib\docs\images\POH_layout.png');

  with Self.ImagePanel do
  begin
    Init(Self.SSPanel.Panel);
    SetParent(Self.SSPanel.Panel);
    SetTop(Self.BuilderInfo.GetTop() + 65);
    SetLeft(Self.BuilderInfo.GetLeft() + 80);
    setWidth(195);
    SetHeight(194);
    setPicture(Self.Pic);
  end;


  Self.WLPanel.StartButton.setOnClick(@Self.StartScript);
end;

procedure TBuilderConfig.Run(); override;
begin
  Self.Setup();

  inherited;
end;

var
  BuilderConfig: TBuilderConfig;
{$ENDIF}

begin
  {$IFDEF SCRIPT_GUI}
  Sync(@BuilderConfig.Run);
  {$ENDIF}
  Builder.Run(WLSettings.MaxActions, WLSettings.MaxTime);
end.
