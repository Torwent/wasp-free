{$IFDEF WINDOWS}{$DEFINE SCRIPT_GUI}{$ENDIF}
{$I SRL/osr.simba}
{$I WaspLib/osr.simba}

begin
  Login.PlayerIndex     := 0;
  AntibanEnabled        := True; //Enables Most Antiban
  BreaksEnabled         := True; //Enables Short Breaks
  SleepEnabled          := True; //Enables Sleep Breaks
  RemoteInputEnabled    := True; //Enables Remote Input
  ProgressReportEnabled := True; //Enables Graphical Progress Report
  ScriptDebugEnabled    := False; //Enables Script Debugging
end;

type
  ERSNMZPotion = (
    OVERLOAD_POTION,
    SUPER_RANGING_POTION,
    SUPER_MAGIC_POTION
  );

var
  CurrentPotion := ERSNMZPotion.OVERLOAD_POTION;

type
  ENMZState = (
    //Outside Dream.
    CHECK_COFFER,
    OPEN_COFFER,
    UNLOCK_COFFER,
    OPEN_NMZREWARD,
    BUY_POTS,

    WITHDRAW_BOOST,
    WITHDRAW_ABSORPTION,
    ANSWER_BOOST,
    ANSWER_ABSORPTION,
    DEPOSIT_BOOST,
    DEPOSIT_ABSORPTION,
    HANDLE_DEPOSIT,

    TALK_DOMINIC,
    CHOOSE_DREAM,
    HANDLE_DOMINIC,
    PAY_DOMINIC,

    OPEN_NMZSETUP,
    HANDLE_NMZSETUP,
    CLOSE_INTERFACE,

    //Inside Dream.
    DRINK_BOOST,
    DRINK_ANTIPOISON,
    DRINK_ABSORPTION,

    WALK_MIDDLE,
    ATTACK_NPC,
    WAIT_STATE1,
    WAIT_STATE2,
    WAIT_STATE3,

    USE_POWER,
    USE_SPEC,
    REEQUIP_GEAR,

    LOWER_HEALTH,
    FLICK_PRAYER,

    END_SCRIPT
  );

  TNMZFighter = record(TBaseScript)
    State: ENMZState;
    RSWReal: TRSWalker;
    RSWDream: TRSWalker;

    InsideDream: Boolean;
    DreamSetup: Boolean;
    Boosted: Boolean;
    PrayFlick: Boolean;

    DreamMiddle: TPoint;

    Potion: String;
    BoostPotion: TRSItemArray;
    AbsPotion: TRSItemArray;

    BoostBarrel: TRSObject;
    AbsBarrel: TRSObject;

    BoostAmount: Int32;
    AbsAmount: Int32;
    MinAbsPoints: Int32;

    DamageItem: TRSItem;

    PowerSurgeTimer: TCountDown;
    ZapperTimer: TCountDown;
    BoostTimer: TCountDown;
    PrayFlickTimer: TCountDown;
    WalkMiddleTimer: TCountDown;

    CofferLocked: Boolean;
    CofferChecked: Boolean;
  end;

procedure TAntiban.Setup; override;
begin
  inherited;

  Antiban.Skills := [ERSSkill.TOTAL];
  Antiban.MinZoom := 15;
  Antiban.MaxZoom := 40;
end;

procedure TNMZFighter.SetupPotions;
var
  i: Int32;
begin
  case CurrentPotion of
    OVERLOAD_POTION:      Potion := 'Overload';
    SUPER_RANGING_POTION: Potion := 'Super ranging';
    SUPER_MAGIC_POTION:   Potion := 'Super magic potion';
  end;

  SetLength(BoostPotion, 4);
  for i := 1 to 4 do
    BoostPotion[i-1] := Potion + ' (' + ToStr(i) + ')';

  if Potion.Contains('Overload') then
    BoostTimer.Init(300000)
  else
    BoostTimer.Init(420000);

  BoostTimer.Timeout := GetTickCount;
  BoostAmount := 8;

  SetLength(AbsPotion, 4);
  for i := 1 to 4 do
    AbsPotion[i-1] := 'Absorption' + ' (' + ToStr(i) + ')';

  AbsAmount := 15;
end;

procedure TNMZFighter.SetupBarrels;
begin
  RSObjects.NMZChest.WalkerIndex := 1;
  RSObjects.NMZPotion.WalkerIndex := 1;
  RSObjects.NMZCoffer.WalkerIndex := 1;
  RSObjects.NMZOverloadBarrel.WalkerIndex := 1;
  RSObjects.NMZSuperMagicBarrel.WalkerIndex := 1;
  RSObjects.NMZSuperRangingBarrel.WalkerIndex := 1;
  RSObjects.NMZAbsorptionBarrel.WalkerIndex := 1;
  RSNPCs.DominicOnion.WalkerIndex := 1;

  if Potion.Contains('Overload') then
    BoostBarrel := RSObjects.NMZOverloadBarrel
  else if Potion.Contains('magic') then
    BoostBarrel := RSObjects.NMZSuperMagicBarrel
  else if Potion.Contains('ranging') then
    BoostBarrel := RSObjects.NMZSuperRangingBarrel;

  AbsBarrel := RSObjects.NMZAbsorptionBarrel;
end;

procedure TNMZFighter.SetupTimers;
begin
  DreamMiddle := [5456, 453];

  if PrayFlick then
    PrayFlickTimer.Setup(60000);
  WalkMiddleTimer.Setup(190000 - Round(20000 * BioHash));
end;


procedure TNMZFighter.SetupPowerUps;
begin
  PowerSurgeTimer.Init(45000);
  PowerSurgeTimer.Timeout := GetTickCount;

  ZapperTimer.Init(65000);  //5 extra seconds for margin.
  ZapperTimer.Timeout := GetTickCount;
end;

procedure TNMZFighter.SetupDamageItem;
begin
  if not Inventory.Open() then Exit;

  if Inventory.ContainsItem('Locator orb') then
    DamageItem := 'Locator orb'
  else if Inventory.ContainsItem('Dwarven rock cake') then  //this will use 7509 ID which is hot cake
    DamageItem := 'Dwarven rock cake'
  else if Inventory.ContainsItem(7510) then  //7510 is cold rock cake ID
    DamageItem := 7510;

  if ToStr(DamageItem) = '' then
    TerminateScript('You need either a Locator orb or a Dwarven rock cake to run this script.');

  DebugLn('Damage item is: ' + ToStr(DamageItem));
end;

procedure TNMZFighter.Init(MaxActions: Int32; MaxTime: Int64); override;
begin
  Name := 'Nightmare Zone';

  inherited;

  RSWDream.Setup('x5284y280');
  RSWReal.Setup('x5631y3844');

  Self.SetupPotions;
  Self.SetupBarrels;
  Self.SetupPowerUps;

  RSNPCS.AnyNPC.UpText := ['Attack'];
  MinAbsPoints := 250;

  if not RSClient.IsLoggedIn then
    Login.LoginPlayer;

  Options.SetNPCAttackOption(ERSAttackOption.ALWAYS_LEFT_CLICK);

  CombatHandler.Setup(True, @RSMonsters.AnyMonster, 25000 - Round(15000 * BioHash));
  Combat.SetAutoRetaliate(True);

  Self.SetupDamageItem;

  PrayFlick := Stats.GetCurrentLevel(ERSSkill.PRAYER) > 21;
  Self.SetupTimers;

  if ScriptDebugEnabled then
  begin
    DebugObjectArray += @BoostBarrel;
    DebugObjectArray += @AbsBarrel;
    DebugObjectArray += @RSObjects.NMZChest;
    DebugObjectArray += @RSObjects.NMZPotion;

    DebugObjectArray += @RSObjects.NMZZapper;
    DebugObjectArray += @RSObjects.NMZPowerSurge;
    DebugObjectArray += @RSObjects.NMZRecurrentDamage;

    DebugNPCArray += @RSNPCs.DominicOnion;
  end;
end;


function TNMZFighter.InDream: Boolean;
begin
  Result := InsideDream := MainScreen.InMultiCombat;

  if Result then
  begin
    BoostTimer.Resume;
    if PrayFlick then
      PrayFlickTimer.Resume;
    CombatHandler.InCombatTimer.Resume;
    WalkMiddleTimer.Resume;
    PowerSurgeTimer.Resume;
    ZapperTimer.Resume;
    WL.Activity.Restart;
  end
  else
  begin
    Boosted := False;

    BoostTimer.Timeout := GetTickCount;
    if PrayFlick then
      PrayFlickTimer.Timeout := GetTickCount;
    CombatHandler.InCombatTimer.Timeout := GetTickCount;
    WalkMiddleTimer.Timeout := GetTickCount;
    PowerSurgeTimer.Timeout := GetTickCount;
    ZapperTimer.Timeout := GetTickCount;
  end;
end;


//Outside Dream.
function TNMZFighter.OpenChest: Boolean;
begin
  if RSObjects.NMZChest.WalkClick then
  begin
    Minimap.WaitMoving;
    Result := WaitUntil(MainScreen.IsClassicOpen, 100, 5000);
  end;
end;

function TNMZFighter.HandleChest: Boolean;
begin
  Result := True;
end;


function TNMZFighter.CountDoses(PotionArray: TRSItemArray): Int32;
var
  i: Int32;
begin
  for i := 0 to High(PotionArray) do
    Result += Inventory.CountItem(PotionArray[i]) * (i + 1);
end;

function TNMZFighter.PotAmountNeeded(Potion: TRSItemArray): Int32;
begin
  if Potion = BoostPotion then
    Result := BoostAmount * 4 - Self.CountDoses(BoostPotion)
  else
    Result := Random(80, 999);
end;


function TNMZFighter.HasEnoughBoost: Boolean;
begin
  Result := Inventory.CountItem(BoostPotion[3]) = BoostAmount;
end;

function TNMZFighter.HasEnoughAbs: Boolean;
begin
  Result := Inventory.IsFull or (Inventory.CountItem(AbsPotion[3]) >= AbsAmount);
end;


function TNMZFighter.WithdrawPotion(Barrel: TRSObject): Boolean;
var
  Str: String;
begin
  case Barrel of
    BoostBarrel: Str := 'Take ' + Potion;
    AbsBarrel:   Str := 'Take Absorption';
  end;

  if Barrel.WalkSelectOption([Str]) then
  begin
    Minimap.WaitMoving;
    Result := Chat.FindQuery('How many doses', 5000, 100);
  end;
end;

function TNMZFighter.DepositPotion(Barrel: TRSObject): Boolean;
begin
  if Barrel.WalkSelectOption(['Store']) then
  begin
    Minimap.WaitMoving;
    Result := WaitUntil('Store' in Chat.GetChatTitle, 5000, 100);
  end;
end;

function TNMZFighter.HandleQuery(Amount: Int32): Boolean;
var
  InvCount: Int32 := Inventory.Count;
begin
  Chat.AnswerQuery('How many', ToStr(Amount), 2000, 100);
  Result := WaitUntil(InvCount <> Inventory.Count, 100, 3000);
end;

function TNMZFighter.TalkWithDominic: Boolean;
begin
  if RSNPCs.DominicOnion.WalkSelectOption(['Dream']) then
  begin
    Minimap.WaitMoving;
    Result := WaitUntil('dream would' in Chat.GetChatTitle, 100, 3000);
  end;
end;

function TNMZFighter.HandlePayment: Boolean;
begin
  if Chat.ClickOption('Yes') then
    Result := DreamSetup := WaitUntil(not ('Agree' in Chat.GetChatTitle), 100, 3000);
end;

function TNMZFighter.OpenNMZSetup: Boolean;
begin
  if RSObjects.NMZPotion.WalkClick then
  begin
    Minimap.WaitMoving;
    Result := WaitUntil(MainScreen.IsClassicOpen, 100, 5000);
  end;
end;

function TNMZFighter.HandleNMZSetup: Boolean;
var
  TPA: TPointArray;
begin
  if SRL.FindColors(TPA, CTS0(65280, 0), InterfaceArea.Bounds) then
  begin
    Mouse.Move(TPA.Bounds);
    MainScreen.IsUpText('Continue');
    Mouse.Click(MOUSE_LEFT);
    Result := WaitUntil(Self.InDream, 200, 10000);
  end;
end;


function TNMZFighter.CheckCoffer: Boolean;
begin
  if RSObjects.NMZCoffer.WalkHover then
  begin
    Result := CofferLocked := MainScreen.IsUpText('Unlock');
    CofferChecked := True;
  end;
end;

function TNMZFighter.OpenCoffer: Boolean;
begin
  if RSObjects.NMZCoffer.WalkClick then
  begin
    Minimap.WaitMoving;
    Result := CofferLocked := BankPin.IsOpen(10000);
  end;
end;


//Inside Dream.
function TNMZFighter.HasBoostPot: Boolean;
var
  PotDose: TRSItem;
  Slots: TIntegerArray;
begin
  if not Inventory.Open then Exit;

  for PotDose in BoostPotion do
    Inventory.FindItem(PotDose, Slots);

  Result := Slots <> [];
end;

function TNMZFighter.NeedBoost: Boolean;
var
  PotStr: String := ToStr(BoostPotion[0]);
begin
  if not Self.HasBoostPot then
    Exit;

  if BoostTimer.IsFinished then
    Boosted := False;

  if PotStr.Contains('Overload') then
    Result := (Minimap.GetHPLevel > 50)
  else
    Result := not Boosted;
end;

function TNMZFighter.GetAbsorptionPoints: Int32;
var
  B: TBox;
begin
  case RSClient.Mode of
    ERSClientMode.FIXED: B := [10, 57, 63, 76];
    ERSClientMode.RESIZABLE_CLASSIC,
    ERSClientMode.RESIZABLE_MODERN: B := [10, 53, 63, 72];
  end;

  Result := OCR.RecognizeNumber(B, TOCRShadowRule.Create(20), RS_FONT_QUILL);
end;

function TNMZFighter.NeedAbsorption: Boolean;
begin
  Result := (Self.GetAbsorptionPoints < MinAbsPoints) and Inventory.ContainsAny(AbsPotion);
end;

function TNMZFighter.NeedLowerHP(HPLevel: Int32 = -1): Boolean;
var
  Str: String;
begin
  if not Self.InDream then
    Exit;

  if HPLevel = -1 then
    HPLevel := Minimap.GetHPLevel; //if we don't pass a value in, set it.

  if HPLevel = 1 then
    Exit(False);

  Str := ToStr(BoostPotion[0]);
  if (Str.Contains('Overload') and Self.HasBoostPot) then
    Result := (Boosted or (HPLevel < 51))
  else
    Result := True;
end;


function TNMZFighter.LowerHealth: Boolean;
var
  Slot: Int32;
  SlotOption: String;
  CurrentHP, NewHP: Int32;
begin
  if not Inventory.Open then
    Exit;

  if ToStr(DamageItem) <> 'Locator orb' then
    SlotOption := 'Guzzle';

  if Inventory.FindItem(DamageItem, Slot) then
  begin
    CurrentHP := Minimap.GetHPLevel;
    while Self.NeedLowerHP(CurrentHP) do
    begin
      Inventory.ClickSlot(Slot, SlotOption);
      Wait(150);

      if not WaitUntil(CurrentHP > (NewHP := Minimap.GetHPLevel), 100, 1500) then
        Antiban.SmallRandomMouse;

      CurrentHP := NewHP;
    end;
  end;

  Result := CurrentHP = 1;
end;

function TNMZFighter.DrinkPotion(PotionArray: TRSItemArray): Boolean;
var
  DoseCount: Int32 := Self.CountDoses(PotionArray);
  i, Slot, CurrentHP: Int32;
  PotStr: String := ToStr(PotionArray[0]);
begin
  if not Inventory.Open then Exit;

  for i := 0 to High(PotionArray) do
  begin
    if Inventory.FindItem(PotionArray[i], Slot) then Break
    else if i = High(PotionArray) then Exit;
  end;

  if PotStr.Contains('Overload') then
    CurrentHP := Minimap.GetHPLevel;

  if Inventory.ClickSlot(Slot) then
    Result := WaitUntil(
                Self.CountDoses(PotionArray) < DoseCount, 100, 2000);

  if (PotionArray = BoostPotion) then
  begin
    if PotStr.Contains('Overload') then
      Result := WaitUntil(Minimap.GetHPLevel < CurrentHP, 100, 4000)
    else
      Wait(1200, 1600);
  end;

  if Result then
  begin
    if (PotionArray = BoostPotion) then
    begin
      if PotStr.Contains('Overload') then
        if not WaitUntil(Minimap.GetHPLevel <= (CurrentHP - 45), 100, 12000) then
          Exit(False);

      Boosted := True;
      BoostTimer.Restart(Random(1000, 15000));
    end

    else MinAbsPoints := Random(300, 940);
  end;
end;

function TNMZFighter.FlickPrayer: Boolean;
begin
  if Minimap.IsPrayerEnabled then
    Result := Minimap.DisablePrayer
  else if Minimap.EnablePrayer then
  begin
    Wait(600, 800);
    Result := Minimap.DisablePrayer;
  end;

  if Result and PrayFlick then
    PrayFlickTimer.Restart(Random(-20000, 20000));
end;


function TNMZFighter.PowerUpAvailable: Boolean;
begin
  Result := RSObjects.NMZRecurrentDamage.Find or
            RSObjects.NMZPowerSurge.Find or
            (ZapperTimer.IsFinished and RSObjects.NMZZapper.Find);
end;

function TNMZFighter.GetPowerUp: Boolean;
begin
  if RSObjects.NMZRecurrentDamage.Click then
  begin
    Minimap.WaitMoving;
    Result := WaitUntil(not Self.PowerUpAvailable, 100, 6000);
  end
  else if RSObjects.NMZPowerSurge.Click then
  begin
    Minimap.WaitMoving;
    if Result := WaitUntil(not Self.PowerUpAvailable, 100, 6000) then
      PowerSurgeTimer.Restart;
  end
  else if Result := RSObjects.NMZZapper.Click then
  begin
    Minimap.WaitMoving;
    ZapperTimer.Restart;
  end;
end;


function TNMZFighter.GetState: ENMZState;
var
  Query: String; //to cache the query and title.
  Title: String; //not doing multiple checks improves performance.
begin
  if Self.InDream then
  begin
    DreamSetup := False;

    if CombatHandler.NeedAntiPoison then
      Exit(DRINK_ANTIPOISON);

    if Self.NeedAbsorption then
      Exit(DRINK_ABSORPTION);

    if Self.NeedBoost then
      Exit(DRINK_BOOST);

    if Self.NeedLowerHP then
      Exit(LOWER_HEALTH);

    if PrayFlick and PrayFlickTimer.IsFinished then
      Exit(FLICK_PRAYER);

    if not MainScreen.WaitInCombat(600) then
      Exit(ATTACK_NPC);

    if CombatHandler.CanSpec then
      Exit(USE_SPEC);

    if Minimap.IsSpecEnabled then
      Exit(WAIT_STATE1);

    if CombatHandler.NeedRegear then
      Exit(REEQUIP_GEAR);

    if RSWDream.AtTile(DreamMiddle, 30) or not WalkMiddleTimer.IsFinished then
    begin
      if Self.PowerUpAvailable then
        Exit(USE_POWER);
      if not MainScreen.InCombat then
        Exit(ATTACK_NPC);

      Exit(WAIT_STATE2);
    end;

    if WalkMiddleTimer.IsFinished then
      Exit(WALK_MIDDLE);

    Exit(WAIT_STATE3);
  end;


  //if not Self.InDream
  if WL.Activity.IsFinished then
    Exit(END_SCRIPT);

  Query := Chat.GetQuery;
  Title := Chat.GetChatTitle;

  if CombatHandler.NeedRegear then
    Exit(REEQUIP_GEAR);

  if not CofferChecked then
    Exit(CHECK_COFFER);

  if CofferLocked then
    Exit(OPEN_COFFER);

  if MainScreen.IsClassicOpen then
  begin
    if BankPin.IsOpen then
      Exit(UNLOCK_COFFER);

    Exit(HANDLE_NMZSETUP);
  end;

  if 'How many doses' in Query then
  begin
    if not Self.HasEnoughBoost then
    begin
      if Potion.Lower in Query then
        Exit(ANSWER_BOOST);

      Exit(WITHDRAW_BOOST);
    end;

    if not Self.HasEnoughAbs then
    begin
      if 'absorption' in Query then
        Exit(ANSWER_ABSORPTION);

      Exit(WITHDRAW_ABSORPTION);
    end;

    if DreamSetup then
      Exit(OPEN_NMZSETUP);

    Exit(TALK_DOMINIC);
  end;


  if not Self.HasEnoughBoost then
  begin
    if Inventory.CountItem(BoostPotion[3]) > BoostAmount then
    begin
      if Potion.Lower in Title then
        Exit(HANDLE_DEPOSIT);

      Exit(DEPOSIT_BOOST);
    end;

    if Inventory.CountItem(BoostPotion[3]) < BoostAmount then
      Exit(WITHDRAW_BOOST);
  end;

  if not Self.HasEnoughAbs then
    Exit(WITHDRAW_ABSORPTION);

  if DreamSetup then
  begin
    if 'When you purchase' in Chat.GetChat then
    begin
      DreamSetup := False;
      Exit(TALK_DOMINIC);
    end;

    Exit(OPEN_NMZSETUP);
  end;

  if 'dream would' in Title then
    Exit(CHOOSE_DREAM);

  if ('Dominic' in Title) and ('deduct the money' in Chat.GetChat) then
    Exit(HANDLE_DOMINIC);

  if 'Agree' in Title then
    Exit(PAY_DOMINIC);

  Exit(TALK_DOMINIC);
end;

procedure TNMZFighter.Run(MaxActions: Int32; MaxTime: Int64);
begin
  ClearDebug;
  Self.Init(MaxActions, MaxTime);

  repeat
    State := Self.GetState;
    Action := ToStr(State);
    ExtraInfo := ' InDream: ' + ToStr(InDream) +
                 ' DreamSetup: ' + ToStr(DreamSetup) +
                 ' BoostTimer: ' + ToStr(BoostTimer.TimeRemaining) +
                   STR_NEW_LINE +
                 ' Boostsfinished: ' + ToStr(BoostTimer.IsFinished) +
                 ' NeedBoost: ' + ToSTR(Self.NeedBoost) + ' Boosted: ' + ToStr(Boosted);
    Self.ProcessWhileWaiting;

    case State of
      END_SCRIPT: Break;

      //Outside Dream.
      CHECK_COFFER: Self.CheckCoffer;
      OPEN_COFFER: Self.OpenCoffer;
      UNLOCK_COFFER: BankPin.Enter(Login.GetPlayerPin);

      OPEN_NMZREWARD: Self.OpenChest;
      BUY_POTS: Self.HandleChest;

      WITHDRAW_BOOST: Self.WithdrawPotion(BoostBarrel);
      WITHDRAW_ABSORPTION: Self.WithdrawPotion(AbsBarrel);

      ANSWER_BOOST: Self.HandleQuery(Self.PotAmountNeeded(BoostPotion));
      ANSWER_ABSORPTION: Self.HandleQuery(Self.PotAmountNeeded(AbsPotion));

      DEPOSIT_BOOST: Self.DepositPotion(BoostBarrel);
      DEPOSIT_ABSORPTION: Self.DepositPotion(AbsBarrel);
      HANDLE_DEPOSIT: Chat.ClickOption('Yes');

      TALK_DOMINIC: Self.TalkWithDominic;
      CHOOSE_DREAM: Chat.ClickOption('Previous', False);
      HANDLE_DOMINIC: Chat.ClickContinue;
      PAY_DOMINIC: Self.HandlePayment;

      OPEN_NMZSETUP: Self.OpenNMZSetup;
      HANDLE_NMZSETUP: Self.HandleNMZSetup;
      CLOSE_INTERFACE: MainScreen.CloseInterface;

      //Inside Dream.
      DRINK_BOOST: Self.DrinkPotion(BoostPotion);
      DRINK_ABSORPTION: Self.DrinkPotion(AbsPotion);
      DRINK_ANTIPOISON: Inventory.Consume(ERSConsumable.POISON_CONSUMABLE);

      WALK_MIDDLE: if RSWDream.WebWalk(DreamMiddle, 5, 0.2) then WalkMiddleTimer.Restart;
      ATTACK_NPC: CombatHandler.AttackMonster;
      WAIT_STATE1, WAIT_STATE2, WAIT_STATE3: XPBar.WaitXP(1200);

      USE_POWER: Self.GetPowerUp;
      USE_SPEC: CombatHandler.DoSpec;
      REEQUIP_GEAR: CombatHandler.ReGear;

      LOWER_HEALTH: Self.LowerHealth;
      FLICK_PRAYER: Self.FlickPrayer;
    end;

    if InsideDream then
    begin
      if PowerSurgeTimer.IsFinished then
        Self.DoAntiban(False, False);
    end
    else
      Self.DoAntiban;
  until Self.ShouldStop;
end;

var
  NMZFighter: TNMZFighter;

function TRSBankPin.Enter(Pin: String): Boolean; override;

  function GetDigitToEnter: Char;
  begin
    if OCR.LocateText(Self.Bounds, 'FIRST',  RS_FONT_BOLD_12, TOCRColorRule.Create([$FFFFFF]), 1) then Exit(Pin[1]);
    if OCR.LocateText(Self.Bounds, 'SECOND', RS_FONT_BOLD_12, TOCRColorRule.Create([$FFFFFF]), 1) then Exit(Pin[2]);
    if OCR.LocateText(Self.Bounds, 'THIRD',  RS_FONT_BOLD_12, TOCRColorRule.Create([$FFFFFF]), 1) then Exit(Pin[3]);
    if OCR.LocateText(Self.Bounds, 'FOURTH', RS_FONT_BOLD_12, TOCRColorRule.Create([$FFFFFF]), 1) then Exit(Pin[4]);
  end;

var
  I: Int32;
  Digit: Char;
begin
  for 1 to 3 do // 3 Attempts
  begin
    for I := 1 to 4 do
    begin
      Digit := GetDigitToEnter;
      if Digit = '' then
        Break;

      if not Self.ClickDigit(Digit) then
        Break;

      Wait(1000, 2000, wdLeft);
    end;

    if WaitUntil(not BankPin.IsOpen, 100, 3000) then
    begin
      NMZFighter.CofferLocked := False;
      Exit(True);
    end;
  end;

  Result := WaitUntil(not BankPin.IsOpen, 100, 3000); // wait 3 extra seconds before failing.
  NMZFighter.CofferLocked := not Result;
end;

function TRSLogin.LoginPlayer: Boolean; override;
begin
  Result := inherited;

  NMZFighter.CofferChecked := False;
end;

//override to move the camera less.
function TRSObject._HoverHelper(Attempts: Int32): Boolean; override;
var
  Attempt: Int32;
  ATPA: T2DPointArray;
  Points: TPointArray;
begin
  for Attempt := 0 to Attempts do
  begin
    if Filter.UpText and MainScreen.IsUpText(UpText) then
      Exit(True);

    if Self.Find(ATPA) then
    begin
      if Attempt < 2 then
        Points := ATPA[0] // 0 for the closest one.
      else
        Points := ATPA[Random(0, High(ATPA))];

      if Points <> [] then
      begin
        Mouse.Move(Points[Random(0, High(Points))]);
        if not Filter.UpText then
          Exit(True);
      end;
    end
    else Exit(False);
  end;
end;

function TCombatHandler.NeedRegear: Boolean; override;
begin
  if not ManageGear or not GearIsSetup then
    Exit;

  if not NMZFighter.PowerSurgeTimer.IsFinished or (Minimap.GetSpecLevel >= WeaponSpec) then
    Exit;

  Result := ((Weapon <> 'Null') and Inventory.ContainsItem(Weapon)) or
            ((Shield <> 'Null') and Inventory.ContainsItem(Shield));
end;

function TCombatHandler.DoSpec: Boolean; override;
begin
  if ManageGear and not GearIsSetup then
  begin
    Self._SetupGear;
    ManageGear := GearIsSetup;
    if not GearIsSetup then
      Exit;
  end;

  if (SpecWeapon <> Weapon) and Inventory.ClickItem(SpecWeapon) then
    if not WaitUntil(Inventory.ContainsItem(Weapon), 100, 4000) then
      Exit;

  InCombatTimer.Timeout := GetTickCount;
  Result := DoingSpec := Minimap.EnableSpec(WeaponSpec);

  case SpecWeapon of
    'Granite maul', '4153', '12848':
      begin
        Result := DoingSpec := True;
        Wait(50, 100);
      end;
    else
      Wait(300, 400);
  end;

  if Result and NMZFighter.PowerSurgeTimer.IsFinished and (Minimap.GetSpecLevel < WeaponSpec) then
    Self.Regear;
end;

type
  TNMZConfig = record(TScriptConfig)
    PotionSelector: TLabeledCombobox;
    NMZInfo: TLabel;
  end;

procedure TNMZConfig.StartScript(Sender: TObject);
begin
  Self.Init(Sender);

  CurrentPotion := ERSNMZPotion(PotionSelector.Combobox.getItemIndex)
end;

procedure TNMZConfig.Setup; override;
begin
  inherited;

  with PotionSelector do
  begin
    Init(ScriptSettingsPanel);
    SetCaption('NMZ Potion:');
    SetLeft(ScriptSettingsLabel.getLeft + 5);
    SetTop(ScriptSettingsLabel.getTop + ScriptSettingsLabel.getHeight);
    SetWidth(200);
    Combobox.setStyle(csDropDownList);
    AddItem('Overload');
    AddItem('Super ranging');
    AddItem('Super magic');
    Combobox.setItemIndex(Ord(CurrentPotion));
  end;

  with NMZInfo do
  begin
    Init(ScriptSettingsPanel);
    setParent(ScriptSettingsPanel);

    SetCaption('Should work with most setups.' +
                STR_NEW_LINE +
               'Start with Main Weapon + Shield equipped and Spec Weapon in the inventory.');

    SetLeft(ScriptSettingsLabel.getLeft + 5);
    SetTop(PotionSelector.Combobox.GetTop + PotionSelector.Combobox.getHeight + 2);
  end;

  StartButton.setOnClick(@Self.StartScript);
end;

procedure TNMZConfig.Run; override;
begin
  Self.Setup;

  inherited;
end;

var
  NMZConfig: TNMZConfig;

begin
  {$IFDEF SCRIPT_GUI}
  Sync(@NMZConfig.Run);
  {$ENDIF}
  NMZFighter.Run(MaxActions, MaxTime);
end.
