{$DEFINE SCRIPT_ID := 'bee77a9c-8ff3-4b11-b0fc-2fbbaa788d15'}
{$DEFINE SCRIPT_REVISION := '32'}
{$IFDEF WINDOWS}{$DEFINE FORMS_V2}{$DEFINE SCRIPT_GUI}{$ENDIF}
{$I SRL-T/osr.simba}
{$I WaspLib/osr.simba}

begin
  Login.PlayerIndex := 0;
  StatsPayload.SetUsername('');
end;

type
  ERSNPC = (PHIALS, SERVANT);
  ERSLarder = (REGULAR_LARDER, OAK_LARDER);

var
  CurrentNPC: ERSNPC := PHIALS;
  CurrentLarder: ERSLarder := OAK_LARDER;

type
  ELarderState = (
    LEVEL_UP,
    WAIT_STATE,

    EXIT_HOUSE,
    ENTER_HOUSE,

    BUILD_LARDER,
    HANDLE_FURNITURE_UI,
    DESTROY_LARDER,
    HANDLE_LARDER_CHAT,
    HANDLE_PIN,

    GET_PLANKS,
    HANDLE_PHIALS_CHAT,
    CALL_SERVANT,
    TALK_SERVANT,
    HANDLE_SERVANT,

    END_SCRIPT
  );

  TBuilder = record(TBaseWalkerScript)
    State: ELarderState;
    POH: TRSWalker;

    BuiltLarder: TRSObject;
    LarderSpace: TRSObject;
    ServantNPC: TRSNPC;

    LarderIsBuilt: Boolean;
    UsePhials: Boolean;
    WaitingServant: Boolean;

    Plank: TRSItem;
    NotedPlanksSlot: Int32;
    MinInvCount: Int32;
  end;


procedure TBuilder.SetupNPC();
begin
  case CurrentNPC of
    PHIALS: Self.UsePhials := True;

    SERVANT:
      begin
        ServantNPC.Filter.Skip := False;
        ServantNPC.Filter.Finder := False;
        ServantNPC.Filter.Walker := False;
        ServantNPC.Filter.MinimapDot := True;

        ServantNPC.ShapeArray += [[1, 1, 7], 0];
        ServantNPC.Setup(['Talk-to']);
      end;
  end;
end;

procedure TBuilder.SetupPlank();
begin
  case CurrentLarder of
    REGULAR_LARDER:
      begin
        Plank := 'Plank';
        MinInvCount := 12;
      end;

    OAK_LARDER:
      begin
        Plank := 'Oak plank';
        MinInvCount := 11;
      end;
  end;
end;

procedure TBuilder.SetupObjects();
begin
  BuiltLarder.Setup(1.5, 7, [[3604, 86]]);
  BuiltLarder.Setup(['Larder']);
  BuiltLarder.Filter.Finder := False;
  BuiltLarder.Filter.UpText := False;
  BuiltLarder.Finder.ColorClusters += [CTS2(6916239, 10, 0.33, 1.18), CTS2(4154749, 9, 0.13, 0.65), 150];

  LarderSpace.Setup(1.5, 7, [[3604, 86]]);
  LarderSpace.Setup(['2 more']);
  LarderSpace.Filter.Finder := False;
  LarderSpace.Filter.UpText := False;
  LarderSpace.Finder.ColorClusters += [CTS2(10529472, 16, 0.25, 0.95), CTS2(8884636, 16, 0.16, 0.18), 80];
end;

procedure TAntiban.Setup(); override;
begin
  Self.Skills := [ERSSkill.CONSTRUCTION, ERSSkill.TOTAL];
  Self.MinZoom := 0;
  Self.MaxZoom := 35;

  inherited;
end;

procedure TBuilder.Init(maxActions: Int32; maxTime: Int64); override;
begin
  inherited;
  RSW.Setup([RSRegions.RIMMINGTON, RSRegions.TEMP_POH]);

  Self.SetupNPC();
  Self.SetupObjects();
  Self.SetupPlank();

  ActionProfit -= (ItemData.GetAverage(Plank) * 8 + 40);

  if CurrentNPC = PHIALS then
    case CurrentLarder of
      REGULAR_LARDER: Inventory.FindItem('noted Plank', NotedPlanksSlot);
      OAK_LARDER: Inventory.FindItem('noted Oak plank', NotedPlanksSlot);
    end;

  if WLSettings.RemoteInput.HUDDebug then
  begin
    DebugObjectArray += BuiltLarder;
    DebugObjectArray += LarderSpace;
    DebugObjectArray += RSObjects.POHPortal;
    DebugMMDotArray += RSNPCs.Phials;
    DebugMMDotArray += ServantNPC;
  end;
end;


function TBuilder.EnterHouse(): Boolean;
begin
  if RSObjects.POHPortal.WalkSelectOption(['Build', 'mode']) then
  begin
    Minimap.WaitMoving();
    Result := MainScreen.WaitLoadingPOH(10000);
  end;
end;

function TBuilder.BuildLarder(): Boolean;
begin
  if LarderSpace.WalkSelectOption(['Build La']) then
  begin
    Minimap.WaitMoving();
    Result := WaitUntil(MainScreen.IsClassicOpen(), 200, 5000);
  end
  else if ChooseOption.HasOption('Remove L') then
  begin
    ChooseOption.Close();
    LarderIsBuilt := True;
  end;
end;

function TBuilder.DestroyLarder(): Boolean;
begin
  if BuiltLarder.WalkSelectOption(['Remove L']) then
  begin
    Minimap.WaitMoving;
    Result := WaitUntil(Chat.GetChatTitle = 'Really remove it?', 200, 3000);
  end
  else if ChooseOption.HasOption('Build La') then
  begin
    ChooseOption.Close;
    LarderIsBuilt := False;
  end;
end;


function TBuilder.CallServant(): Boolean;
begin
  if ServantNPC.Click then
  begin
    Minimap.WaitMoving;
    Result := WaitUntil(Chat.GetChatTitle = 'Repeat last task?',
                        SRL.TruncatedGauss(50, 1500), 5000);
  end
  else
    Result := Options.CallServant();
end;

function TBuilder.PhialsGetPlanks(): Boolean;
begin
  if not RSNPCs.Phials.IsVisible() and not RSW.AtTile(RSNPCs.Phials.Coordinates, 8) then
    RSW.WebWalk(RSNPCs.Phials.Coordinates, 15, 0.2);

  if Inventory.SetSelectedSlot(Self.NotedPlanksSlot) and
     RSNPCs.Phials.SelectOption(['Use']) then
  begin
    Minimap.WaitFlag();
    Result := WaitUntil(Chat.GetChatTitle() = 'Select an Option', 100, 15000);
  end;
end;

function TBuilder.TalkServant(): Boolean;
begin
  if ServantNPC.Click then
  begin
    Minimap.WaitMoving;
    Result := WaitUntil(Inventory.IsFull, 100, 5000);
  end;

  WaitingServant := not Result;
end;


function TBuilder.HandleServant(): Boolean;
begin
  Result := Chat.ClickOption('Fetch', BioHash);
  if Result then
  begin
    WaitingServant := True;
    WaitUntil(Chat.GetChatTitle() <> 'Repeat last task?', 200, 3000)
  end;
end;

function TBuilder.HandleLarderChat(): Boolean;
begin
  Keyboard.PressKey(VK_1);
  Result := WaitUntil(Chat.GetChatTitle <> 'Really remove it?', 200, 3000);
  LarderIsBuilt := not Result;
  Wait(100, 150);
end;

function TBuilder.HandleFurniture(): Boolean;
begin
  case CurrentLarder of
    REGULAR_LARDER: Keyboard.PressKey(VK_1);
    OAK_LARDER: Keyboard.PressKey(VK_2);
  end;

  Result := LarderIsBuilt := WaitUntil(not MainScreen.IsClassicOpen(), 100, 3000);

  if Result then
  begin
    Self.TotalActions += 1;
    Self.TotalProfit := Self.TotalActions * Self.ActionProfit;
    WL.Activity.Restart();
    Wait(600, 800);
  end else
    MainScreen.CloseInterface();
end;


function TBuilder.GetState(): ELarderState;
var
  s: String;
begin
  if WL.Activity.IsFinished() then
    Exit(END_SCRIPT);

  if Chat.LeveledUp() then
    Exit(ELarderState.LEVEL_UP);

  s := Chat.GetChatTitle();

  if s.Contains('remove') then
    Exit(ELarderState.HANDLE_LARDER_CHAT);

  if s.Contains('Select') then
    Exit(ELarderState.HANDLE_PHIALS_CHAT);

  if s.Contains('Repeat') then
    Exit(ELarderState.HANDLE_SERVANT);

  if MainScreen.IsClassicOpen() then
  begin
    if BankPin.IsOpen() then
      Exit(ELarderState.HANDLE_PIN);
    Exit(ELarderState.HANDLE_FURNITURE_UI);
  end;

  if not Minimap.InPOH() then
  begin
    if not UsePhials or Inventory.IsFull() then
      Exit(ELarderState.ENTER_HOUSE);
    Exit(ELarderState.GET_PLANKS);
  end;

  if Self.LarderIsBuilt then
    Exit(ELarderState.DESTROY_LARDER);

  if Inventory.Count() > Self.MinInvCount then
  begin
    WaitingServant := False;
    Exit(ELarderState.BUILD_LARDER);
  end;

  if Self.UsePhials then
    Exit(ELarderState.EXIT_HOUSE);

  if WaitingServant then
  begin
    if ServantNPC.IsVisible() then
      Exit(ELarderState.TALK_SERVANT);
    Exit(ELarderState.WAIT_STATE);
  end;

  Exit(ELarderState.CALL_SERVANT);
end;

procedure TBuilder.Run(maxActions: Int32; maxTime: Int64);
begin
  Self.Init(maxActions, maxTime);

  repeat
    Self.State := Self.GetState();
    Self.SetAction(ToStr(Self.State));

    case Self.State of
      ELarderState.LEVEL_UP: Chat.HandleLevelUp();

      ELarderState.HANDLE_LARDER_CHAT: Self.HandleLarderChat();
      ELarderState.HANDLE_PHIALS_CHAT: Keyboard.PressKey(VK_3);
      ELarderState.HANDLE_FURNITURE_UI: Self.HandleFurniture();
      ELarderState.HANDLE_SERVANT: Self.HandleServant();
      ELarderState.HANDLE_PIN: BankPin.Enter(Login.GetPlayerPin());

      ELarderState.CALL_SERVANT: Self.CallServant();
      ELarderState.TALK_SERVANT: Self.TalkServant();
      ELarderState.EXIT_HOUSE: Options.LeaveHouse();
      ELarderState.GET_PLANKS: Self.PhialsGetPlanks();

      ELarderState.ENTER_HOUSE: Self.EnterHouse();
      ELarderState.BUILD_LARDER: Self.BuildLarder();
      ELarderState.DESTROY_LARDER: Self.DestroyLarder();

      ELarderState.WAIT_STATE: Wait(7000, 8000);

      END_SCRIPT: Break;
    end;

    Self.DoAntiban();
  until Self.ShouldStop();
end;

var
  Builder: TBuilder;

function TRSWalker.GetCleanMinimap(): TMufasaBitmap; override;
var
  Bitmap: TMufasaBitmap;
begin
  Bitmap := GetMufasaBitmap(BitmapFromClient(Minimap.Bounds));
  Bitmap.ReplaceColor(0, 1); // We need zero to be unique

  Self.CleanMinimap(Bitmap);
  Bitmap.ReplaceColor(1, $32A072);

  Result.Init();

  Bitmap.RotateBitmapEx(Minimap.GetCompassAngle(False), False, True, Result);
  Bitmap.Free();
end;

function TRSWalkerObject._SelectHelper(action: TStringArray): Boolean; override;
begin
  if Self.CurrentUpText <> [] then
    Self.CurrentUpText := [];

  if Self.RedClicked then
  begin
    Self.RedClicked := False;
    Exit(True);
  end;

  if ChooseOption.IsOpen() and ChooseOption.Select(action) then
    Exit(True);

  if MainScreen.IsUpText(action) then
  begin
    Mouse.Click(MOUSE_LEFT);
    Exit(MainScreen.DidRedClick());
  end;

  Result := ChooseOption.Select(action, MOUSE_LEFT, True, (False));
end;

//TEMP OPTION FIXES
function TRSOptions.IsHouseOptionsOpen(): Boolean; override;
begin
  Result := SRL.CountColor(CTS2(4630611, 23, 0.04, 0.79), Self.Bounds()) > 115;
end;

function TRSOptions.CallServant(): Boolean; override;
var
  s: String;
  b: TBox;
begin
  if not Self.OpenHouseOptions() then
    Exit;

  b := Self.Bounds();

  b.X1 += 9;
  b.Y1 += 185;
  b.X2 -= 9;
  b.Y2 -= 50;

  Mouse.Click(b, MOUSE_LEFT);
  Result := WaitUntil(((s := Chat.GetChatTitle) = 'Demon butler') or
                       (s = 'Repeat last task?'), SRL.TruncatedGauss(50, 1500), 5000);
end;

function TRSOptions.LeaveHouse(): Boolean; override;
var
  b: TBox;
begin
  if not Self.OpenHouseOptions() then
    Exit;

  b := Self.Bounds();

  b.X1 += 9;
  b.Y1 += 214;
  b.X2 -= 9;
  b.Y2 -= 21;

  Mouse.Click(b, MOUSE_LEFT);
  Result := WaitUntil(not Self.IsHouseOptionsOpen(), RandomLeft(50, 1500), 5000);
end;

function TRSBankPin.Enter(pin: String): Boolean; override;

  function GetDigitToEnter(): Char;
  begin
    if OCR.LocateText(Self.Bounds, 'FIRST',  RS_FONT_BOLD_12, TOCRColorRule.Create([$FFFFFF]), 1) then Exit(pin[1]);
    if OCR.LocateText(Self.Bounds, 'SECOND', RS_FONT_BOLD_12, TOCRColorRule.Create([$FFFFFF]), 1) then Exit(pin[2]);
    if OCR.LocateText(Self.Bounds, 'THIRD',  RS_FONT_BOLD_12, TOCRColorRule.Create([$FFFFFF]), 1) then Exit(pin[3]);
    if OCR.LocateText(Self.Bounds, 'FOURTH', RS_FONT_BOLD_12, TOCRColorRule.Create([$FFFFFF]), 1) then Exit(pin[4]);
  end;

var
  i: Int32;
  digit: Char;
begin
  for 1 to 3 do // 3 Attempts
  begin
    for i := 1 to 4 do
    begin
      digit := GetDigitToEnter();
      if digit = '' then
        Break;

      if not Self.ClickDigit(digit) then
        Break;

      Wait(1000, 2000, wdLeft);
    end;

    if 'try again' in Chat.GetChat() then
    begin
      Chat.ClickContinue(True);
      Continue;
    end;

    if MainScreen.WaitLoadingPOH(5000) or Minimap.InPOH() then
      Exit(True);
  end;

  Result := MainScreen.WaitLoadingPOH(5000) or Minimap.InPOH(); // wait 3 extra seconds before failing.
end;


procedure TRSMinimap.SetCompassAngle(Degrees: Double; Accuracy: Double = 5); override;
begin
  if MainScreen.IsUpText('>') then
    ChooseOption.Select('Cancel');

  inherited;
end;

procedure TSRL.Debug(Bitmap: TMufasaBitmap); override;
begin
  inherited;

  Builder.RSW.Draw(Bitmap);
end;

{$IFDEF SCRIPT_GUI}
type
  TBuilderConfig = record(TScriptForm)
    NPCSelector: TLabeledCombobox;
    LarderSelector: TLabeledCombobox;
    BuilderInfo: TLabel;
    ImagePanel: TImage;
  end;

procedure TBuilderConfig.StartScript(sender: TObject); override;
begin
  CurrentNPC := ERSNPC(Self.NPCSelector.GetItemIndex());
  CurrentLarder := ERSLarder(Self.LarderSelector.GetItemIndex());
  inherited;
end;

procedure TBuilderConfig.NPCSelectorOnChange(sender: TObject);
var
  combobox: TComboBox;
begin
  combobox := sender;
  case combobox.getItemIndex() of
    0: Self.BuilderInfo.SetCaption(
       'Make sure you have noted planks with you.' +

       LineEnding + LineEnding +
       'Your house layout must look exactly like the image to the right!' +
       LineEnding +
       'Looking close enough won''t do, it has to look exactly the same.' +
       LineEnding +
       'If you have more rooms, move them 3 squares away so they are never,' +
       LineEnding +
       'ever visible on the minimap.'
       );

    1: Self.BuilderInfo.SetCaption(
       'Make sure you have planks in your bank and you have previously' +
       LineEnding +
       'asked the buttler to fetch you 24 planks.' +
       LineEnding +
       'You also need money in your servant''s money bag and you need to' +
       LineEnding +
       'replenish it manually once in a while.' +

       LineEnding + LineEnding +
       'Your house layout must look exactly like the image to the right!' +
       LineEnding +
       'Looking close enough won''t do, it has to look exactly the same.' +
       LineEnding +
       'If you have more rooms, move them 3 squares away so they are never,' +
       LineEnding +
       'ever visible on the minimap.'
       );
  end;
end;

procedure TBuilderConfig.Run(); override;
var
  tab: TTabSheet;
begin
  Self.Setup('Wasp Builder');
  Self.Start.SetOnClick(@Self.StartScript);

  Self.AddTab('Script Settings');
  tab := Self.Tabs[High(Self.Tabs)];

  Self.CreateAccountManager(tab);

  with Self.NPCSelector do
  begin
    Create(tab);
    SetCaption('Method:');
    SetLeft(TControl.AdjustToDPI(50));
    SetTop(TControl.AdjustToDPI(150));
    SetStyle(csDropDownList);
    AddItemArray(['Phials', 'Demon butler']);
    SetItemIndex(Ord(CurrentNPC));
    Combobox.SetOnChange(@Self.NPCSelectorOnChange);
  end;

  with Self.LarderSelector do
  begin
    Create(tab);
    SetCaption('Larder:');
    SetTop(Self.NPCSelector.GetTop());
    SetLeft(Self.NPCSelector.GetRight() + TControl.AdjustToDPI(15));
    SetStyle(csDropDownList);
    AddItemArray(['Regular', 'Oak']);
    SetItemIndex(Ord(CurrentLarder));
  end;

  with Self.BuilderInfo do
  begin
    Create(tab);
    SetLeft(Self.NPCSelector.GetLeft());
    SetTop(Self.NPCSelector.GetBottom() + TControl.AdjustToDPI(15));
  end;

  with Self.ImagePanel do
  begin
    Create(tab);
    SetTop(Self.LarderSelector.GetTop() + TControl.AdjustToDPI(15));
    SetLeft(Self.LarderSelector.GetRight() + TControl.AdjustToDPI(80));
    LoadFromFile(IncludePath+'\WaspLib\docs\images\POH_layout.png');
    SetWidth(TControl.AdjustToDPI(195));
    SetHeight(TControl.AdjustToDPI(194));
  end;

  Self.CreateVersionPanel(tab);
  Self.CreateAntibanManager();
  Self.CreateWaspLibSettings();
  Self.CreateAPISettings();
  Self.NPCSelectorOnChange(Self.NPCSelector.ComboBox);

  inherited;
end;

var
  BuilderConfig: TBuilderConfig;
{$ENDIF}

begin
  {$IFDEF SCRIPT_GUI}
  BuilderConfig.Run();
  {$ENDIF}
  Builder.Run(WLSettings.MaxActions, WLSettings.MaxTime);
end.
