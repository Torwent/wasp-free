{$DEFINE SRL_USE_REMOTEINPUT}
{$IFDEF WINDOWS}{$DEFINE SCRIPT_GUI}{$ENDIF}
{$I SRL/osr.simba}
{$I WaspLib/osr.simba}

begin
  Login.PlayerIndex     := 0;
  AntibanEnabled        := True; //Enables Most Antiban
  BreaksEnabled         := True; //Enables Short Breaks
  SleepEnabled          := True; //Enables Sleep Breaks
  RemoteInputEnabled    := True; //Enables Remote Input
  ProgressReportEnabled := True; //Enables Graphical Progress Report
  ScriptDebugEnabled    := False; //Enables Script Debugging
end;

type
  EState = (
    WAIT_STATE,
    FILL_TOAD,
    FILL_BELLOWS,
    PLACE_TOAD,
    CLICK_CHOMPY
  );

  TChompyHunter = record(TBaseWalkerScript)
    State: EState;
    Toad: PRSNPC;
    OgreBellows: TRSItemArray;
    FillingBellows: Boolean;
  end;

procedure TAntiban.Setup; override;
begin
  inherited;

  Antiban.Skills := [ERSSkill.TOTAL];
  Antiban.MinZoom := 15;
  Antiban.MaxZoom := 40;
end;

procedure TChompyHunter.Init(MaxActions: Int32; MaxTime: Int64); override;
begin
  Name := 'Chompy Hunter';

  inherited;

  RSW.Setup('x4595y3834');

  CombatHandler.Setup(@RSNPCs.Chompy, 2000);
  Toad := @RSNPCs.SwampToad;

  OgreBellows := ['Ogre bellows (3)', 'Ogre bellows (2)', 'Ogre bellows (1)'];

  if not RSClient.IsLoggedIn then Login.LoginPlayer;

  if ScriptDebugEnabled then
  begin
    DebugNPCArray += CombatHandler.EnemyNPC;
    DebugNPCArray += Toad;
  end;
end;

function TChompyHunter.PlaceToad: Boolean;
begin
  if Inventory.ClickItem('Bloated toad') then
  begin
    Wait(1400, 1800);
    Result := WaitUntil(not Inventory.FindItem('Bloated toad'), 100, 5000);
  end;
end;


function TChompyHunter.FillToad: Boolean;
var
  InvCount := Inventory.Count;
begin
  if Toad^.Click(True, 10) then
  begin
    Minimap.WaitMoving;
    Result := WaitUntil(InvCount < Inventory.Count, 100, 5000);
  end;

  if Result then
    Self.PlaceToad;
end;


function TChompyHunter.ClickedChompy(FeatherCount: Int32): Boolean;
begin
  Result := MainScreen.InCombat or (FeatherCount < Inventory.CountItemStack('Feather'));
end;

function TChompyHunter.ClickChompy: Boolean;
var
  FeatherCount: Int32;
begin
  if CombatHandler.EnemyNPC^.Click then
  begin
    FeatherCount := Inventory.CountItemStack('Feather');
    Minimap.WaitMoving;
    Result := WaitUntil(Self.ClickedChompy(FeatherCount) or MainScreen.InCombat, 100, 5000);
  end;

  if Result then TotalActions += 1;
end;



function TChompyHunter.GetState: EState;
begin
  if FillingBellows then
  begin
    if FillingBellows := Inventory.FindItem('Ogre bellows') then
      Exit(WAIT_STATE);
    Exit;
  end

  else if Inventory.FindItem('Bloated toad') then
    Exit(PLACE_TOAD)

  else if MainScreen.InCombat then
    Exit(WAIT_STATE)

  else if CombatHandler.EnemyNPC^.Find then
    Exit(CLICK_CHOMPY)

  else if Inventory.FindItems(OgreBellows) then
    Exit(FILL_TOAD);

  Exit(FILL_BELLOWS);
end;

procedure TChompyHunter.Run(MaxActions: Int32; MaxTime: Int64);
begin
  ClearDebug;
  Self.Init(MaxActions, MaxTime);

  while not Self.ShouldStop do
  begin
    State := Self.GetState;
    Action := ToStr(State);
    Self.ProcessWhileWaiting;

    if ScriptDebugEnabled then SRL.Debug(RSClient.Image);

    case State of
      WAIT_STATE: Wait(800, 1200);
      FILL_TOAD: Self.FillToad;
      FILL_BELLOWS: RSObjects.SwampBubbles.WalkClick;
      PLACE_TOAD: Self.PlaceToad;
      CLICK_CHOMPY: Self.ClickChompy;
    end;

    Self.DoAntiban;
    if not RSClient.IsLoggedIn then Login.LoginPlayer;
  end;
end;

var ChompyHunter: TChompyHunter;

type
  TChompyHunterConfig = record(TScriptConfig)
    ChompyHunterInfo: TLabel;
  end;

procedure TChompyHunterConfig.StartScript(Sender: TObject);
begin
  Self.Init(Sender);
end;

procedure TChompyHunterConfig.Setup; override;
begin
  inherited;

  with ChompyHunterInfo do
  begin
    Init(ScriptSettingsPanel);
    setParent(ScriptSettingsPanel);

    SetCaption('This script is in BETA!' +
               STR_NEW_LINE +
               'Have ogre bellows in your inventory and an ogre bow/arrows equipped.' +
               STR_NEW_LINE +
               'It''s probably a good idea to BABY SIT!');

    SetLeft(ScriptSettingsLabel.getLeft + 5);
    SetTop(ScriptSettingsLabel.getTop + ScriptSettingsLabel.getHeight + 5);
  end;

  StartButton.setOnClick(@Self.StartScript);
end;

procedure TChompyHunterConfig.Run; override;
begin
  Self.Setup;

  inherited;
end;

var
  ChompyHunterConfig: TChompyHunterConfig;

{$IFNDEF SCRIPT_CHAIN}
begin
  {$IFDEF SCRIPT_GUI}
  Sync(@ChompyHunterConfig.Run);
  {$ENDIF}
  ChompyHunter.Run(MaxActions, MaxTime);
end.
{$ENDIF}
