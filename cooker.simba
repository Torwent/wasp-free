{$IFNDEF SCRIPT_CHAIN}
  {$DEFINE SRL_USE_REMOTEINPUT}
  {$IFDEF WINDOWS}{$DEFINE SCRIPT_GUI}{$ENDIF}
  {$I SRL/osr.simba}
  {$I WaspLib/osr.simba}

begin
  Login.PlayerIndex     := 0;
  AntibanEnabled        := True; //Enables Most Antiban
  BreaksEnabled         := True; //Enables Short Breaks
  SleepEnabled          := True; //Enables Sleep Breaks
  RemoteInputEnabled    := True; //Enables Remote Input
  ProgressReportEnabled := True; //Enables Graphical Progress Report
  ScriptDebugEnabled    := False; //Enables Script Debugging
end;
{$ENDIF}

type
  ERSLocation = (HOSIDIUS, ROGUES_DEN);

const
  RawFood: TRSItemArray = [
    'Custom',
    'Meat',
    'Shrimps',
    'Chicken',
    'Rabbit',
    'Anchovies',
    'Sardine',
    'Herring',
    'Mackerel',
    'Trout',
    'Cod',
    'Pike',
    'Salmon',
    'Tuna',
    'Karambwan',
    'Rainbow fish',
    'Lobster',
    'Bass',
    'Swordfish',
    'Monkfish',
    'Shark',
    'Sea turtle',
    'Anglerfish',
    'Dark crab',
    'Manta ray'
  ];

var
  CurrentFood: TRSItem   := 'Anglerfish';
  CurrentLocation := ERSLocation.HOSIDIUS;

type
  ECookerState = (
    OPEN_BANK,
    WITHDRAW_ITEMS,
    DEPOSIT_ITEMS,

    OPEN_COLLECT,
    HANDLE_COLLECT,
    CLOSE_INTERFACE,

    DO_COOKING,
    HANDLE_MAKE,
    WAIT_COOKING,

    LEVEL_UP,
    CLOSE_CONTEXT,

    END_SCRIPT
  );

  TCooker = record(TBaseBankScript)
    State: ECookerState;

    Food: TRSItem;
    Ingredient: TRSBankWithdrawItem;
    Products: array of TRSBankDepositItem;

    CookingObj: TRSObject;

    Cooking: Boolean;
    UsingFire: Boolean;
  end;

function TRSMake.Select(Index: Int32; Quantity: Int32; UseKeyboard: Boolean = True): Boolean; override;
var
  Buttons: TRSButtonArray;
begin
  Buttons := Self.GetItemButtons();
  if Buttons <> [] then
    Result := Self.SelectHelper(Buttons[Index], Quantity, UseKeyboard);
end;


procedure TCooker.SetupItems;
var
  CookedPrefix: TStringArray;
  BurntFish: TStringArray;
begin
  Food := CurrentFood;

  if ToStr(Food).Lower = 'shark' then
    ItemFinder.Similarity := 0.9999;

  CookedPrefix := ['chicken', 'rabbit', 'meat', 'crab meat',
                   'karambwan', 'chompy', 'fishcake', 'jubbly', 'sweetcorn'];

  BurntFish := ['herring', 'mackerel', 'sardine', 'anchovies',
                'giant carp', 'trout', 'cod', 'pike', 'salmon', 'tuna', 'bass'];

  Ingredient := [('Raw ' + ToStr(Food).Lower()), BANK_WITHDRAW_ALL, False];

  if (CookedPrefix.Find(LowerCase(Food)) > -1) then
    Products := [
      ['Cooked ' + ToStr(Food).Lower, BANK_DEPOSIT_ALL],
      ['Burnt ' + ToStr(Food).Lower, BANK_DEPOSIT_ALL]]
  else if (BurntFish.Find(LowerCase(Food)) > -1) then
    Products := [
      [Food, BANK_DEPOSIT_ALL],
      ['Burnt fish', BANK_DEPOSIT_ALL]]
  else
    Products := [
      [Food, BANK_DEPOSIT_ALL],
      ['Burnt ' + ToStr(Food).Lower, BANK_DEPOSIT_ALL]];

  ActionProfit := (
    ItemData.GetAverage(Products[0].Item) -
    ItemData.GetAverage(Ingredient.Item)
  );
end;

procedure TCooker.SetupLocation;
begin
  case CurrentLocation of
    ERSLocation.HOSIDIUS:
      begin
        RSW.Setup('x1916y1841');
        ScriptBank := RSObjects.HosidiusKitchenBank;
        CookingObj := RSObjects.HosidiusKitchenOven;
      end;

    ERSLocation.ROGUES_DEN:
      begin
        RSW.Setup('x9921y310');
        RSNPCs.EmeraldBenedict.Filter.Finder := False;
        ScriptBanker := RSNPCs.EmeraldBenedict;

        RSObjects.EternalFire.Filter.Finder := False;
        RSObjects.EternalFire.Filter.UpText := False;
        CookingObj := RSObjects.EternalFire;
      end;
  end;

end;

procedure TAntiban.Setup; override;
begin
  inherited;
  Antiban.Skills := [ERSSkill.COOKING, ERSSkill.TOTAL];
  Antiban.MinZoom := 15;
  Antiban.MaxZoom := 60;
end;

procedure TCooker.Init(MaxActions: Int32; MaxTime: Int64); override;
begin
  Name := 'AIO Cooker';

  inherited;

  Self.SetupItems;
  Self.SetupLocation;

  if not RSClient.IsLoggedIn then Login.LoginPlayer;

  if ScriptDebugEnabled then
  begin
    case CurrentLocation of
      ERSLocation.HOSIDIUS:   DebugObjectArray += @ScriptBank;
      ERSLocation.ROGUES_DEN: DebugNPCArray += @ScriptBanker;
    end;
    DebugObjectArray += @CookingObj;
  end;
end;


function TCooker.HasProducts: Boolean; overload;
var
  Product: TRSBankDepositItem;
  Slots: TIntegerArray;
begin
  for Product in Products do
    if not Inventory.FindItem(Product.Item, Slots) then
      Exit(False);

  Result := True;
end;

function TCooker.OpenBank: Boolean;
begin
  case CurrentLocation of
    ERSLocation.HOSIDIUS:   Result := Bank.WalkOpen(ScriptBank);
    ERSLocation.ROGUES_DEN: Result := Bank.WalkOpen(ScriptBanker);
  end;
end;

function TCooker.Withdraw: Boolean;
var
  InvCount: Int32;
begin
  if BankTab = -1 then
    BankTab := Bank.FindItemTab(Ingredient.Item);

  InvCount := Inventory.Count;
  if Result := Bank.WithdrawItem(BankTab, Ingredient, True) then
    WaitUntil(Inventory.Count > InvCount, 100, 3000);

  if BankEmpty := (not Result and not Bank.FindItem(BankTab, Ingredient)) then
    if CollectEmpty and CollectTimer.IsFinished then
      CollectEmpty := False;
end;

function TCooker.Deposit: Boolean;
var
  InvCount: Int32 := Inventory.Count;
  Product: TRSBankDepositItem;
begin
  TotalActions += InvCount;

  for Product in Products do
  begin
    if not Bank.DepositItem(BankTab, Product, True) then
      Exit(False);
    WaitUntil((InvCount > Inventory.Count), 50, 2000);
    InvCount := Inventory.Count;
  end;

  Result := True;
end;


function TCooker.HandleCollectBox: Boolean;
var
  Slots: TIntegerArray;
begin
  if CollectBox.FindItem(Ingredient.Item, Slots) then
  begin
    if Result := CollectEmpty := CollectBox.CollectToBank then
      WaitUntil(not CollectBox.FindItem(Ingredient.Item, Slots), 100, 2000);
    BankEmpty := not CollectEmpty;
  end
  else
    CollectEmpty := True;

  CollectTimer.Restart;
end;


function TCooker.DoCooking: Boolean;
begin
  if CurrentLocation = ERSLocation.ROGUES_DEN then
    if Inventory.GetSelectedSlot = -1 then
      Inventory.SetSelectedItem(Ingredient.Item);

  if CookingObj.WalkSelectOption(['Cook', 'Fire']) then
  begin
    Minimap.WaitPlayerMoving(300, 3000);
    Result := Make.IsOpen(4000);
  end;
end;

function TCooker.HandleMake: Boolean;
var
  i: Int32 := 0;
begin
  if not Make.IsOpen then //not sure if I can remove this.
    Exit;                 //it's probably not needed.

  if 'arambwan' in Food then
    i := 1;

  Result := Cooking := Make.Select(i, MAKE_QUANTITY_ALL, Antiban.BioDice);
  
  if Result then
    WaitUntil(not Make.IsOpen, 100, 4000);  
end;

function TCooker.IsCooking(WaitTime: Int32 = 2500): Boolean;
var
  ItemCount: Int32 := Inventory.CountItem(Ingredient.Item);
begin
  if ItemCount = 0 then Exit;

  if Result := WaitUntil(ItemCount > Inventory.CountItem(Ingredient.Item), 100, WaitTime) then
    WL.Activity.Restart;
end;

function TCooker.WaitCooking: Boolean;
begin
  Result := Cooking := Self.IsCooking
end;


function TCooker.GetState: ECookerState;
begin
  if WL.Activity.IsFinished then
    Exit(ECookerState.END_SCRIPT);

  if ChooseOption.IsOpen then
    Exit(ECookerState.CLOSE_CONTEXT);

  if XPBar.EarnedXP then
    Exit(ECookerState.WAIT_COOKING);

  if Chat.LeveledUp then
  begin
    Cooking := False;
    Exit(ECookerState.LEVEL_UP);
  end;

  if Make.IsOpen then
    Exit(ECookerState.HANDLE_MAKE);

  if MainScreen.HasInterface then
  begin
    if Bank.IsOpen then
    begin
      if Self.HasProducts then
        Exit(ECookerState.DEPOSIT_ITEMS);

      if BankEmpty or Inventory.FindItem(Ingredient.Item) then
        Exit(ECookerState.CLOSE_INTERFACE);

      Exit(ECookerState.WITHDRAW_ITEMS);
    end;

    if CollectBox.IsOpen then
    begin
      if CollectEmpty or Inventory.FindItem(Ingredient.Item) then
        Exit(ECookerState.CLOSE_INTERFACE);

      Exit(ECookerState.HANDLE_COLLECT);
    end;

    Exit(ECookerState.CLOSE_INTERFACE);
  end;

  if Inventory.FindItem(Ingredient.Item) then
  begin
    if Cooking then
      Exit(ECookerState.WAIT_COOKING);

    Exit(ECookerState.DO_COOKING);
  end;

  if BankEmpty and CollectEmpty then
    Exit(ECookerState.END_SCRIPT);

  if BankEmpty then
    Exit(ECookerState.OPEN_COLLECT);

  Exit(ECookerState.OPEN_BANK);
end;

function TCooker.Terminate: Boolean; override;
begin
  if inherited then
    for 0 to 5 do
      if Result := Bank.WithdrawItem(BankTab, [Products[0].Item, BANK_WITHDRAW_ALL, True], True) then
        Break;
end;

procedure TCooker.Run(MaxActions: Int32; MaxTime: Int64);
begin
  ClearDebug;
  Self.Init(MaxActions, MaxTime);

  while not Self.ShouldStop do
  begin
    State := Self.GetState;
    Action := ToStr(State);
    Self.ProcessWhileWaiting;

    case State of
      ECookerState.OPEN_BANK: Self.OpenBank;
      ECookerState.WITHDRAW_ITEMS: Self.Withdraw;
      ECookerState.DEPOSIT_ITEMS: Self.Deposit;

      ECookerState.OPEN_COLLECT: CollectBox.WalkOpen(ScriptBank);
      ECookerState.HANDLE_COLLECT: Self.HandleCollectBox;
      ECookerState.CLOSE_INTERFACE: MainScreen.CloseInterface;

      ECookerState.LEVEL_UP: Chat.HandleLevelUp;
      ECookerState.DO_COOKING: Self.DoCooking;
      ECookerState.HANDLE_MAKE: Self.HandleMake;
      ECookerState.WAIT_COOKING: Self.WaitCooking;
      ECookerState.CLOSE_CONTEXT: ChooseOption.Close;

      ECookerState.END_SCRIPT: Break;
    end;

    Self.DoAntiban;
  end;

  if not Self.Terminate then
    TerminateScript('aio_cooker didn''t terminate properly. Stopping execution.');
end;

var
  Cooker: TCooker;

type
  TCookerConfig = record(TScriptConfig)
    LocSelector: TLabeledCombobox;
    CustomFoodName: TLabeledEdit;
    FoodSelector: TLabeledCombobox;
  end;

procedure TCookerConfig.StartScript(Sender: TObject);
begin
  Self.Init(Sender);

  CurrentLocation := ERSLocation(LocSelector.Combobox.getItemIndex);
  if FoodSelector.GetText = 'Custom' then
    CurrentFood := CustomFoodName.GetText
  else
    CurrentFood := FoodSelector.GetText;
end;


procedure TCookerConfig.FoodSelectorOnChange(Sender: TObject);
begin
  CustomFoodName.SetVisible(FoodSelector.GetText = 'Custom');
end;

procedure TCookerConfig.Setup; override;
var
  Str: String;
begin
  inherited;

  Self.UpdateFormSize(Form.getWidth, Form.getHeight + 30);

  with LocSelector do
  begin
    Init(ScriptSettingsPanel);
    SetCaption('Location:');
    SetLeft(ScriptSettingsLabel.getLeft + 5);
    SetTop(ScriptSettingsLabel.getTop + ScriptSettingsLabel.getHeight + 5);
    SetWidth(200);
    SetStyle(csDropDownList);
    AddItem('Hosidius kitchen');
    AddItem('Rogues den');
    SetItemIndex(Ord(CurrentLocation));
  end;

  with FoodSelector do
  begin
    Init(ScriptSettingsPanel);
    SetCaption('Food type:');
    SetLeft(LocSelector.GetLeft + LocSelector.GetWidth + 20);
    SetTop(LocSelector.GetTop);
    SetWidth(200);
    SetStyle(csDropDownList);

    for Str in RawFood do
      AddItem(Str);

    SetItemIndex(0);
    Combobox.setOnChange(@Self.FoodSelectorOnChange);
  end;

  with CustomFoodName do
  begin
    Init(ScriptSettingsPanel);
    SetCaption('Food name (must be the exact name):');
    SetLeft(FoodSelector.getLeft);
    SetTop(LocSelector.getTop + LocSelector.GetHeight - 15);
    SetWidth(200);
    SetText(CurrentFood);
  end;

  Self.FoodSelectorOnChange(FoodSelector.Combobox);

  StartButton.setOnClick(@Self.StartScript);
end;

procedure TCookerConfig.Run; override;
begin
  Self.Setup;

  inherited;
end;

var
  CookerConfig: TCookerConfig;

{$IFNDEF SCRIPT_CHAIN}
begin
  {$IFDEF SCRIPT_GUI}
  Sync(@CookerConfig.Run);
  {$ENDIF}
  Cooker.Run(MaxActions, MaxTime);
end.
{$ENDIF}
