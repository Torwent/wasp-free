{$DEFINE SCRIPT_ID := 'a398a083-63fd-4f13-936b-3963354fb4c1'}
{$DEFINE SCRIPT_REVISION := '34'}
{$DEFINE SCRIPT_GUI}
{$I SRL-T/osr.simba}
{$I WaspLib/osr.simba}
{$I WaspLib/optional/handlers/combathandler.simba}
{$SCOPEDENUMS ON}

type
  EKrakenState = (
    WAIT_STATE,

    //Outside
    ENTER_COVE,
    ENTER_CREVICE,
    HANDLE_CHAT,
    HANDLE_PIN,
    HANDLE_WARNING,

    //Inside
    WALK_BRIDGE,
    CHECK_LOOT,
    WAKE_KRAKEN,
    ATTACK_KRAKEN,
    SELECT_SPELL,
    CAST_BLOOD,
    BOOST,
    EAT_FOOD,
    DRINK_PRAYER,
    ENABLE_QPRAY,
    ALCH_DROP,
    EQUIP_WEAPON,
    EQUIP_BRACELET,
    END_SCRIPT
  );

  TKrakenKiller = record(TBaseScript)
    State: EKrakenState;

    RSW: TRSWalker;
    Bridge: TPoint;
    BossRoomTPA, SleepyKraken, KrakenTPA: TPointArray;

    KrakenFinder: TRSObjectFinder;

    AnotherPLayer, IsAttacking, CheckedLoot, UseImbuedHeart,
    HasBloodSpells: Boolean;
    BloodSpell: ERSSpell;

    Gear: TRSItemArray;

    ImbuedHeart: TCountDown;
  end;


procedure TAntiban.SetupBreaks(); override;
begin
  if Self.Breaks <> [] then
    Exit;

  Self.AddBreak(30 * ONE_MINUTE, 2 * ONE_MINUTE, 0.33, 0.01);
  Self.AddBreak(45 * ONE_MINUTE, 4 * ONE_MINUTE, 0.33, 0.15);

  Self.OnBreaking := @OnBreakingTask;
end;

procedure TAntiban.Setup(); override;
begin
  Self.Skills := [ERSSkill.TOTAL, ERSSkill.MAGIC, ERSSkill.HITPOINTS];
  Self.MinZoom := 0;
  Self.MaxZoom := 40;

  inherited;
end;

function TKrakenKiller.CanCastBloodSpells(): Boolean;
const
  BLOOD_SPELLS: array of ERSSpell = [
    ERSSpell.BLOOD_BARRAGE, ERSSpell.BLOOD_BLITZ,
    ERSSPell.BLOOD_BURST, ERSSpell.BLOOD_RUSH
  ];
var
  i: Int32;
begin
  if not Magic.IsSpellBook(ERSSpellBook.ANCIENT) or not Magic.Open() then
    Exit;

  for i := 0 to 3 do
  begin
    if Magic.ContainsSpell(BLOOD_SPELLS[i]) then
    begin
      Self.BloodSpell := BLOOD_SPELLS[i];
      Exit(True);
    end;
  end;
end;

procedure TKrakenKiller.Init(maxActions: UInt32; maxTime: UInt64); override;
begin
  inherited;

  Self.RSW.SetupRegions([RSRegions.KRAKEN_COVE, RSRegions.PISCATORIS]);
  Self.Bridge := [10391, 1145];
  Self.BossRoomTPA := [
    [10369, 1085], [10420, 1086], [10440, 1107],
    [10440, 1149], [10415, 1155], [10411, 1187],
    [10373, 1187], [10367, 1156], [10345, 1148],
    [10345, 1105]
  ];

  FoodHandler.MinInvPoints := 10;
  FoodHandler.MinInvPoints := 90;

  Self.KrakenFinder.ClusterDistance := 3;
  Self.KrakenFinder.Erode := 2;
  Self.KrakenFinder.Grow := 4;
  Self.KrakenFinder.Colors += CTS2(14407349, 13, 0.13, 1.43);
  Self.KrakenFinder.MinShortSide := 70;
  
  Self.UseImbuedHeart := True;
  Self.ImbuedHeart.Setup(7 * ONE_MINUTE);

  Options.SetNPCAttackOption(ERSAttackOption.ALWAYS_LEFT_CLICK);
  Combat.SetAutoRetaliate(False);
  CombatHandler.Setup(False, RSMonsters.Kraken, 5000, 4000);

  Self.HasBloodSpells :=  Self.CanCastBloodSpells();

  Self.Gear := Equipment.DiscoverAll();

  if not Self.Gear.Contains('Sanguinesti staff') then
  begin
    CombatHandler.LootHandler.Loot.UpText += 'Shark';
    CombatHandler.LootHandler.Drops += ['Shark', '', False, False, 5];
  end;
end;


function TKrakenKiller.InsideBossRoom(): Boolean;
begin
  Result := MainScreen.InMultiCombat();
  if not Result then
    Self.CheckedLoot := True;
end;

function TKrakenKiller.EnterCove(): Boolean;
begin
  if RSObjects.KrakenCoveEntrance.WalkClick() then
  begin
    Minimap.WaitMoving();
    Result := WaitUntil(not Minimap.OnGroundLevel(), 300, 5000);
  end;
end;

function TKrakenKiller.EnterCrevice(): Boolean;
begin
  if RSObjects.KrakenCrevice.WalkSelectOption(['Private']) then
  begin
    Minimap.WaitMoving;
    Result := WaitUntil(('Pay' in Chat.GetChatTitle()) or BankPin.IsOpen(), 300, 5000);
  end;
end;

function TKrakenKiller.HandlePayment(): Boolean;
begin
  Chat.ClickOption('Yes', BioHash);
  Result := WaitUntil(Self.InsideBossRoom() or Chat.HasContinue(), 300, 8000);
end;


function TKrakenKiller.FindSleepyKraken(): Boolean;
var
  ATPA: T2DPointArray;
begin
  ATPA := MainScreen.FindObject(Self.KrakenFinder);
  Result := ATPA <> [];
  if Result then
    Self.SleepyKraken := ATPA[0];
end;

function TKrakenKiller.FindRagingKraken(): Boolean;
var
  MMDots: TPointArray;
  cuboid: TCuboidEx;
  KrakenDot: TPoint;
begin
  MMDots := Minimap.GetDots(ERSMinimapDot.NPC);
  Result := (Length(MMDots) >= 5) and not Self.FindSleepyKraken();

  if not Result then
    Exit;

  Self.CheckedLoot := False;

  MMDots.Sort(MMDots.Mean());
  KrakenDot := MMDots[0];

  cuboid := Minimap.GetCuboidMS(KrakenDot, [3, 3, 8], [-3, -3]);
  Self.KrakenTPA := cuboid.ShapeFill();
end;


function TKrakenKiller.FinishedSlayerTask(): Boolean;
begin
  Result := ('Lieve' in Chat.GetChat) or ('Lieve' in Chat.GetMessage(7));
  if Result then
    TerminateScript('FINISHED SLAYER TASK');
end;


function TKrakenKiller.WakeKraken(): Boolean;
begin
  if MainScreen.IsUpText('>') then
    ChooseOption.Select('Cancel');

  if not Inventory.SetSelectedItem('Fishing explosive') then
    Exit;

  Mouse.Move(Self.SleepyKraken);
  if MainScreen.IsUpText('Whirl') then
  begin
    Mouse.Click(MOUSE_LEFT);
    if MainScreen.DidRedClick then
      Result := IsAttacking := WaitUntil(Self.FindRagingKraken() or Self.FinishedSlayerTask(), 100, 7000);
  end;

  if Result then
    WL.Activity.Restart();
end;

function TKrakenKiller.AttackKraken(): Boolean;
begin
  Mouse.Move(KrakenTPA.Mean());
  if MainScreen.IsUpText('Attack K') then
  begin
    Mouse.Click(MOUSE_LEFT);
    Result := MainScreen.DidRedClick();
    Self.IsAttacking := Result;
  end;
end;

function TKrakenKiller.CastBlood(): Boolean;
begin
  Mouse.Move(KrakenTPA.Mean());

  if MainScreen.IsUpText('> K') then
  begin
    XPBar.EarnedXP();
    Mouse.Click(MOUSE_LEFT);
    Result := MainScreen.DidRedClick() and XPBar.WaitXP(1200);
    if Result then
      Self.IsAttacking := False;
  end;
end;



function TKrakenKiller.BoostMagic(): Boolean;
var
  Slot: Int32;
begin
  Result := UseImbuedHeart := Inventory.FindItem('Imbued heart', Slot);

  if not Result then
    Exit;

  Inventory.ClickSlot(Slot);
  ImbuedHeart.Restart(Random(3000, 35000));
  IsAttacking := False
end;

function TKrakenKiller.CheckLoot(): Boolean;
var
  rect: TRectangle;
begin
  if MainScreen.IsUpText('>') then
    ChooseOption.Select('Cancel');

  while Inventory.IsFull() and Inventory.ContainsConsumable(ERSConsumable.FOOD) do
    Inventory.Consume(ERSConsumable.FOOD);

  Result := CheckedLoot := True;
  rect := Minimap.GetTileMS(Minimap.Center(), [1,1,0]);
  while CombatHandler.LootHandler.CheckLoot(rect) and Inventory.HasSpace(1) do
    Wait(400, 600);
end;


function TKrakenKiller.EquipWeapon(): Boolean;
var
  item: TRSItem;
begin
  for item in Self.Gear do
    if Inventory.ClickItem(item) then
      Exit(True);
end;

function TKrakenKiller.GetState(): EKrakenState;
var
  health: Int32;
  pos: TPoint;
begin
  if WL.Activity.IsFinished() and not MainScreen.InCombat() then
    Exit(EKrakenState.END_SCRIPT);

  pos := Self.RSW.GetMyPos();
  if not Self.InsideBossRoom() then
  begin
    if BankPin.IsOpen() then
      Exit(EKrakenState.HANDLE_PIN);

    if 'Pay' in Chat.GetChatTitle ()then
      Exit(EKrakenState.HANDLE_CHAT);

    if Chat.HasContinue() then
      Exit(EKrakenState.HANDLE_WARNING);

    case Self.RSW.RegionIndex of
      0: Exit(EKrakenState.ENTER_CREVICE);
      1: Exit(EKrakenState.ENTER_COVE);
    end;
  end;

  if (Minimap.GetPrayerLevel() <= 30) and Inventory.ContainsItem('Prayer potion(1..4)') then
    Exit(EKrakenState.DRINK_PRAYER);

  if CombatHandler.NeedBracelet() then
    Exit(EKrakenState.EQUIP_BRACELET);

  if Self.UseImbuedHeart and ImbuedHeart.IsFinished() then
    Exit(EKrakenState.BOOST);

  if not Minimap.IsPrayerEnabled() and (Minimap.GetPrayerLevel() > 0) then
    Exit(EKrakenState.ENABLE_QPRAY);

  if not pos.WithinDistance(Self.Bridge, 5) then
    Exit(EKrakenState.WALK_BRIDGE);

  if Inventory.ContainsAny(Self.Gear) then
    Exit(EKrakenState.EQUIP_WEAPON);

  health := Minimap.GetHPLevel();

  if Self.FindRagingKraken() then
  begin
    if health <= 40 then
    begin
      if Self.HasBloodSpells then
      begin
        if not MainScreen.IsUpText('Blood') then
        begin
          if (health <= 25) and Inventory.ContainsConsumable(ERSConsumable.FOOD) then
            Exit(EKrakenState.EAT_FOOD);

          Exit(EKrakenState.SELECT_SPELL);
        end;

        Exit(EKrakenState.CAST_BLOOD);
      end;

      if Inventory.ContainsConsumable(ERSConsumable.FOOD) then
        Exit(EKrakenState.EAT_FOOD);
      if health <= 25 then
        TerminateScript('OUT OF FOOD AND HEALTH WHILE FIGHTING');
    end;

    if Self.IsAttacking then
      Exit(EKrakenState.WAIT_STATE);
    Exit(EKrakenState.ATTACK_KRAKEN);
  end;

  if health <= 75 then
  begin                         Self.CheckedLoot
    if Inventory.ContainsConsumable(ERSConsumable.FOOD) then
      Exit(EKrakenState.EAT_FOOD);
    if health <= 40 then
      TerminateScript('OUT OF FOOD AND HEALTH');
  end;

  if not Self.CheckedLoot then
    Exit(EKrakenState.CHECK_LOOT);

  if not Self.HasBloodSpells and Inventory.ContainsAny(RSAlchHandler.ItemArray) then
    Exit(EKrakenState.ALCH_DROP);

  if Self.FindSleepyKraken() then
    Exit(EKrakenState.WAKE_KRAKEN);
end;

procedure TKrakenKiller.Run(maxActions: UInt32; maxTime: UInt64);
begin
  Self.Init(maxActions, maxTime);

  repeat
    Self.State := Self.GetState();
    Self.SetAction(ToStr(Self.State));

    case Self.State of
      //Outside
      EKrakenState.ENTER_COVE: Self.EnterCove();
      EKrakenState.ENTER_CREVICE: Self.EnterCrevice();
      EKrakenState.HANDLE_PIN: BankPin.Enter(Login.GetPlayerPin());
      EKrakenState.HANDLE_CHAT: Self.HandlePayment();
      EKrakenState.HANDLE_WARNING: Chat.ClickContinue(BioHash);

      //Inside
      EKrakenState.WALK_BRIDGE: RSW.WebWalk(Bridge, 5, 0.2);
      EKrakenState.WAIT_STATE: WaitUntil(not Self.FindRagingKraken, 100, 3000);
      EKrakenState.CHECK_LOOT: Self.CheckLoot();
      EKrakenState.WAKE_KRAKEN: Self.WakeKraken();
      EKrakenState.SELECT_SPELL: Magic.CastSpell(Self.BloodSpell);
      EKrakenState.CAST_BLOOD: Self.CastBlood();
      EKrakenState.ATTACK_KRAKEN: Self.AttackKraken();
      EKrakenState.BOOST: Self.BoostMagic();
      EKrakenState.EAT_FOOD: Inventory.Consume(ERSConsumable.FOOD);
      EKrakenState.DRINK_PRAYER: Inventory.Consume(ERSConsumable.PRAYER);
      EKrakenState.ENABLE_QPRAY: Minimap.EnablePrayer();
      EKrakenState.ALCH_DROP: RSAlchHandler.CastAlchemy(True, True);
      EKrakenState.EQUIP_WEAPON: Self.EquipWeapon();
      EKrakenState.EQUIP_BRACELET: CombatHandler.EquipBracelet();
      EKrakenState.END_SCRIPT: Break;
    end;

    Self.DoAntiban(not Self.FindRagingKraken(), False);
  until Self.ShouldStop();
end;


var
  KrakenKiller: TKrakenKiller;

function TRSInventory.Consume(ConsumableType: ERSConsumable; out Slots: TIntegerArray): Boolean; override;
begin
  Result := inherited(ConsumableType);

  if Result then
    KrakenKiller.IsAttacking := False;
end;

function TConsumableHandler.FindInInventory(): TRSConsumableArray; override;
var
  consumable: TRSConsumable;
  item: TRSItem;
begin
  Self.DebugLn('Setting up consumable of type: ' + ToStr(Self.ConsumableType));

  for item in CONSUMABLE_ARRAYS[Self.ConsumableType] do
  begin
    if item = 'Sanfew serum(1..4)' then
      Continue;
    if Self.ConsumableArray.Contains(item) then //avoid resetting leftover items.
      Continue;

    if Inventory.ContainsItem(item) then
    begin
      Self.DebugLn('Consumable found: ' + ToStr(item));
      consumable := Self.Setup(item);

      Result += consumable;
    end;
  end;
end;
{$IFDEF SCRIPT_GUI}
type
  TKrakenConfig = record(TScriptForm)
    InfoText: TLabel;
  end;

procedure TKrakenConfig.Run(); override;
var
  tab: TTabSheet;
begin
  Self.Setup('Wasp Kraken');
  Self.Start.SetOnClick(@Self.StartScript);

  Self.AddTab('Script Settings');
  tab := Self.Tabs[High(Self.Tabs)];

  Self.CreateAccountManager(tab);

  with Self.InfoText do
  begin
    Create(tab);
    SetCaption(
      'Keep in mind this script doesn''t bank so Sang Staff is highly recommended.'
      + LINEENDING +
      'If you have issues staying alive consider using magic and defense boosting prayers.'
      + LINEENDING +
      'Overhead prayers have no effect on kraken.'
      + LINEENDING + LINEENDING +
      'Also keep in mind this won''t stop when you have your inventory full of loot.'
      + LINEENDING +
      'I recommend you check it every 40 mins or so to see if it needs banking.'
      + LINEENDING + LINEENDING +
      'FISHING EXPLOSIVES ARE REQUIRED!'
    );
    SetLeft(TControl.AdjustToDPI(50));
    SetTop(TControl.AdjustToDPI(200));
  end;

  Self.CreateAntibanManager();
  Self.CreateWaspLibSettings();
  Self.CreateAPISettings();

  inherited;
end;

var
  KrakenConfig: TKrakenConfig;
{$ENDIF}

begin
  {$IFDEF SCRIPT_GUI}
  KrakenConfig.Run();
  {$ENDIF}
  KrakenKiller.Run(WLSettings.MaxActions, WLSettings.MaxTime);
end.
