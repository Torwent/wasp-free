{$IFDEF WINDOWS}{$DEFINE SCRIPT_GUI}{$ENDIF}
{$I SRL/osr.simba}
{$I WaspLib/osr.simba}

begin
  Login.PlayerIndex     := 0;
  AntibanEnabled        := True; //Enables Most Antiban
  BreaksEnabled         := True; //Enables Short Breaks
  SleepEnabled          := True; //Enables Sleep Breaks
  RemoteInputEnabled    := True; //Enables Remote Input
  ProgressReportEnabled := True; //Enables Graphical Progress Report
  ScriptDebugEnabled    := False; //Enables Script Debugging
end;

type
  ENPC = (
    MAN, MASTER_FARMER,
    GUARD, KNIGHT_OF_ARDOUGNE, ELF, TZHAAR_CITY
  );

var
  PickedNPC: ENPC := ENPC.ELF;       //Use one of the above NPCs.
  SuicideMode: Boolean := True;      //Pickpocket until death. Useful if you respawn nearby.
  FoodAmount: Int32 := 5;
  NeckAmount: Int32 := 2;
  NextHeal: Int32;

type
  EThieverState = (
    OPEN_BANK,
    DEPOSIT_LOOT,
    WITHDRAW_FOOD,
    WITHDRAW_NECKLACES,
    CLOSE_INTERFACE,

    OPEN_CHAT,
    CHANGE_CHAT_FILTER,

    WALK_TO_NPC,
    EQUIP_ROGUE,

    CAST_SHADOW_VEIL,
    PICKPOCKET,
    OPEN_POUCH,

    EAT_FOOD,
    EQUIP_NECKLACE,
    CHECK_NECKLACE,
    CLOSE_CONTEXT,

    DROP_ITEMS,

    ENABLE_REDEMPTION,
    LOOT_GRAVE,

    END_SCRIPT
  );

  TThiever = record(TBaseBankScript)
    //init variables (to be set while the script is initiates)
    State: EThieverState;

    UseShadowVeil: Boolean;
    UseRedemption: Boolean;

    CurrentNPC: PRSNPC;

    CoinPouch: TRSItem;
    Necklace: TRSItem;
    ValuableItem: TRSItem;

    LootArray: TRSItemArray;
    StackableArray: TRSItemArray;
    DropArray: TRSItemArray;
    RogueEquipment: TRSItemArray;

    GearAmount: Int32;
    ValuableItemValue: Int32;

    NeckTimer: TCountDown;
    ShadowVeilTimer: TCountDown;
    LasPickPosition: TPoint;

    //run variables (to be used while the script is running)
    RenderDisabled: Boolean;
    HasNeck: Boolean;
    FreshNeck: Boolean;
    RedemptionEnabled: Boolean;

    CoinPouchLimit: Int32;
    NPCMaxHit: Int32;
  end;

function TRSChooseOption.Select(Text: TStringArray; MouseAction: Int32 = MOUSE_LEFT; CaseSensitive: Boolean = True; CloseIfNotFound: Boolean = True): Boolean; override;
var
  Choices: TRSChooseOption_OptionArray;
  I, J: Int32;
begin
  if not Self.Open then
    Exit;

  Wait(0, 1000 - Round(1000*BioHash), wdLeft);

  Choices := Self.GetOptions;

  for I := 0 to High(Choices) do
    for J := 0 to High(Text) do
      if (CaseSensitive and (Text[J] in Choices[I].Text)) or
         ((not CaseSensitive) and (Lowercase(Text[J]) in Lowercase(Choices[I].Text))) then
      begin
        Self.Select(Choices[I], MouseAction);
        Exit(True);
      end;

  if CloseIfNotFound then
  begin
    WaitEx(350 - Round(350*BioHash), 50);
    Self.Close;
  end;

end;


procedure TAntiban.Setup; override;
begin
  inherited;

  Antiban.Skills := [ERSSkill.THIEVING, ERSSkill.TOTAL];
  Antiban.MinZoom := 30;
  Antiban.MaxZoom := 50;
end;

procedure TThiever.SetupNPC;
begin
  FoodManager.Amount := FoodAmount;

  case PickedNPC of
    MAN:
      begin
        RSW.Setup('x7617y3214');
        CurrentNPC := @RSNPCs.Man;

        LootArray += CoinPouch;
        LootArray += 'Coins';

        StackableArray := LootArray;

        NPCMaxHit := 1;

        if not SuicideMode then
          SuicideMode := True;

        ActionProfit := 3;
      end;

    MASTER_FARMER:
      begin
        RSW.Setup('x7617y3214');
        CurrentNPC := @RSNPCs.MasterFarmer;
        LootArray := [
          'Potato seed', 'Onion seed', 'Cabbage seed', 'Tomato seed',
          'Sweetcorn seed', 'Strawberry seed', 'Watermelon seed',
          'Snape grass seed',

          'Barley seed', 'Hammerstone seed', 'Asgarnian seed', 'Jute seed',
          'Yanillian seed', 'Krandorian seed', 'Wildblood seed',

          'Marigold seed', 'Nasturtium seed', 'Rosemary seed', 'Woad seed',
          'Limpwurt seed',

          'Redberry seed', 'Cadavaberry seed', 'Dwellberry seed',
          'Jangerberry seed', 'Whiteberry seed', 'Poison ivy seed',

          'Ranarr seed',

          'Mushroom spore', 'Belladonna seed', 'Cactus seed', 'Seaweed spore',
          'Potato cactus seed'
        ];

        DropArray := [
          'Potato seed', 'Onion seed', 'Cabbage seed', 'Tomato seed',
          'Sweetcorn seed', 'Strawberry seed', 'Watermelon seed',

          'Barley seed', 'Hammerstone seed', 'Asgarnian seed', 'Jute seed',
          'Yanillian seed', 'Krandorian seed', 'Wildblood seed',

          'Marigold seed', 'Nasturtium seed', 'Rosemary seed', 'Woad seed',
          'Limpwurt seed',

          'Redberry seed', 'Cadavaberry seed', 'Dwellberry seed',
          'Jangerberry seed', 'Whiteberry seed', 'Poison ivy seed',

          'Mushroom spore', 'Belladonna seed', 'Cactus seed',
          'Potato cactus seed'
        ];

        NPCMaxHit := 3;

        ScriptBank := RSObjects.DraynorBank;

        if SuicideMode then
          SuicideMode := False;
      end;

    GUARD:
      begin
        RSW.Setup('x7051y2796');
        CurrentNPC := @RSNPCs.Guard;

        LootArray += CoinPouch;
        LootArray += 'Coins';

        StackableArray := LootArray;

        NPCMaxHit := 2;
        ActionProfit := 30;

        ScriptBank := RSObjects.FaladorWestBank;
      end;

    KNIGHT_OF_ARDOUGNE:
      begin
        Login.Players[Login.PlayerIndex].Worlds:= []; 
        RSW.Setup('x5723y3038');
        CurrentNPC := @RSNPCs.KnightOfArdougne;
        ScriptBank := RSObjects.ArdougneSouthBank;

        CurrentNPC^.Filter.MinimapDot := False;

        LootArray += CoinPouch;
        LootArray += 'Coins';

        StackableArray := LootArray;

        NPCMaxHit := 3;

        ActionProfit := 100;

        SuicideMode := False;
      end;

    ELF:
      begin
        RSW.Setup('x3583y1905');
        CurrentNPC := @RSNPCs.Elf;
        ScriptBank := RSObjects.PrifddinasBank;

        LootArray := [
          CoinPouch, 'Coins', 'Death rune', 'Jug of wine', 'Nature rune',
          'Fire orb', 'Diamond', 'Gold ore', 'Crystal shards',
          'Enhanced crystal teleport seed'
        ];

        ActionProfit := 560;
        ValuableItem := 'Enhanced crystal teleport seed';
        ValuableItemValue := ItemData.GetAverage(ValuableItem);


        DropArray := ['Gold ore'];

        StackableArray := [CoinPouch, 'Coins', 'Death rune', 'Nature rune',
                           'Crystal shards'];

        NPCMaxHit := 5;
      end;

    TZHAAR_CITY:
      begin
        RSW.Setup('x4333y310');
        CurrentNPC := @RSNPCs.TzhaarHur;
        LootArray := [
          'Tokkul', 'Uncut sapphire', 'Uncut emerald',
          'Uncut ruby', 'Uncut diamond'
        ];

        StackableArray := LootArray;

        NPCMaxHit := 4;

        ScriptBank := RSObjects.TzhaarBank;

        if SuicideMode then
          SuicideMode := False;
      end;
  end;

  DropArray += 'Jug';
  DropArray += 'Pie dish';

  StackableArray += Necklace; //not stackable but this array is used to not bank items
  StackableArray += 'Cosmic rune';

  CurrentNPC^.Filter.Walker := False;
end;

procedure TThiever.CheckRogueEquipment;
var
  ProfitMultiplier: Double;
  Item: TRSItem;
begin
  if Inventory.Open then
  begin
    if Inventory.FindItem('Rogue mask') then
    begin
      ProfitMultiplier += 0.2;
      RogueEquipment += 'Rogue mask';
    end;

    if Inventory.FindItem('Rogue top') then
    begin
      ProfitMultiplier += 0.2;
      RogueEquipment += 'Rogue top';
    end;

    if Inventory.FindItem('Rogue trousers') then
    begin
      ProfitMultiplier += 0.2;
      RogueEquipment += 'Rogue trousers';
    end;

    if Inventory.FindItem('Rogue gloves') then
    begin
      ProfitMultiplier += 0.2;
      RogueEquipment += 'Rogue gloves';
    end;

    if Inventory.FindItem('Rogue boots') then
    begin
      ProfitMultiplier += 0.2;
      RogueEquipment += 'Rogue boots';
    end;

    //This are not part of rogue equipment but are useful while thieving
    if Inventory.FindItem('Thieving cape') then
      RogueEquipment += 'Thieving cape'
    else if Inventory.FindItem('Thieving cape(t)') then
      RogueEquipment += 'Thieving cape(t)';

    //Lava staves for shadow veil spell
    if Inventory.FindItem('Lava battlestaff') then
      RogueEquipment += 'Lava battlestaff'
    else if Inventory.FindItem('Mystic lava staff') then
      RogueEquipment += 'Mystic lava staff'
    else if Inventory.FindItem(21200) then
      RogueEquipment += 21200;
  end;

  if Equipment.Open then
  begin
    if Equipment.FindItem('Rogue mask') then
    begin
      ProfitMultiplier += 0.2;
      RogueEquipment += 'Rogue mask';
    end;

    if Equipment.FindItem('Rogue top') then
    begin
      ProfitMultiplier += 0.2;
      RogueEquipment += 'Rogue top';
    end;

    if Equipment.FindItem('Rogue trousers') then
    begin
      ProfitMultiplier += 0.2;
      RogueEquipment += 'Rogue trousers';
    end;

    if Equipment.FindItem('Rogue gloves') then
    begin
      ProfitMultiplier += 0.2;
      RogueEquipment += 'Rogue gloves';
    end;

    if Equipment.FindItem('Rogue boots') then
    begin
      ProfitMultiplier += 0.2;
      RogueEquipment += 'Rogue boots';
    end;


    //This are not part of rogue equipment but are useful while thieving
    if Equipment.FindItem('Thieving cape') then
      RogueEquipment += 'Thieving cape'
    else if Equipment.FindItem('Thieving cape(t)') then
      RogueEquipment += 'Thieving cape(t)';

    //Lava staves for shadow veil spell
    if Equipment.FindItem('Lava battlestaff') then
      RogueEquipment += 'Lava battlestaff'
    else if Equipment.FindItem('Mystic lava staff') then
      RogueEquipment += 'Mystic lava staff'
    else if Equipment.FindItem(21200) then
      RogueEquipment += 21200;

  end;

  GearAmount := Equipment.CountGear;
  if not Equipment.IsSlotUsed(ERSEquipmentButton.AMULET) then
    GearAmount += 1;

  ActionProfit += Round(ActionProfit * ProfitMultiplier);

  for Item in RogueEquipment do
    StackableArray += Item; //Adding RogueEquipment to the non banking item list
end;

procedure TThiever.Init(MaxActions: Int32; MaxTime: Int64); override;
begin
  Name    := 'AIO Pickpocketer';

  inherited;

  UseRedemption := SuicideMode;

  CoinPouch := 'Coin pouch';
  Necklace := 'Dodgy necklace';

  Self.SetupNPC;

  //NeckTimer to recheck necklace in case the script messes up.
  NeckTimer.Init(140000);

  if not RSClient.IsLoggedIn then
    Login.LoginPlayer;

  if Antiban.BioDice then
    Options.SetNPCAttackOption(ERSAttackOption.HIDDEN)
  else
    Options.SetNPCAttackOption(ERSAttackOption.ALWAYS_RIGHT_CLICK);

  Self.CheckRogueEquipment;
  HasNeck := Equipment.FindItem(Necklace);
  if Magic.CanActivate(ERSSpell.SHADOW_VEIL) then
  begin
    UseShadowVeil := True;
    Magic.CastSpell(ERSSpell.SHADOW_VEIL);
    ShadowVeilTimer.Init(1000 * Round(0.6 * Stats.GetCurrentLevel(ERSSkill.MAGIC)));
  end;

  if not SuicideMode and ((FoodManager.Amount + NeckAmount + 2) >= 26) then
  begin
    if UseShadowVeil then
      FoodManager.Amount := 26 - NeckAmount - 1
    else
      FoodManager.Amount := 26 - NeckAmount;
  end
  else if ((FoodManager.Amount + NeckAmount + 2 + Length(StackableArray)) >= (28 - Length(StackableArray))) then
    FoodManager.Amount := 28 - NeckAmount - 2 - Length(StackableArray);

  if ScriptDebugEnabled then
  begin
    DebugObjectArray += @ScriptBank;
    DebugNPCArray += CurrentNPC;
  end;
end;



function TRSInventory.HoverItems(Items: TRSItemArray): Boolean;
var
  Item: TRSItem;
  Slot: Int32;
begin
  for Item in Items do
    if Result := Self.FindItem(Item, Slot) then Break;

  if Result then Mouse.Move(Self.GetSlotBox(Slot));
end;

function TRSInventory.ClickItems(Items: TRSItemArray; Option: String = ''): Boolean;
begin
  if Self.HoverItems(Items) then
  begin
    if (Option <> '') then Result := ChooseOption.Select(Option)
    else
    begin
      Mouse.Click(MOUSE_LEFT);
      Result := True;
    end;
  end;
end;

function TRSInventory.CountItems(Items: TRSItemArray): Int32;
var
  Item: TRSItem;
begin
  for Item in Items do Result += Self.CountItem(Item);
end;



function TThiever.HasRogue: Boolean;
begin
  Result := Inventory.FindItems(RogueEquipment);
end;

function TThiever.EquipRogue: Boolean;
var
  Item: TRSItem;
begin
  for Item in RogueEquipment do
    if Inventory.ClickItem(Item) then
      Wait(200, 300);

  if Inventory.ClickItem(21200) then
    Wait(200, 300);

  Result := not Self.HasRogue and not Inventory.FindItem('Lava battlestaff');
end;


function TThiever.HasDepositItems: Boolean;
var
  Slots: TIntegerArray;
  UsedSlots: TIntegerArray := Inventory.GetUsedSlots;
begin
  Inventory.FindItems(StackableArray, Slots);

  Result := Length(Slots) <> Length(UsedSlots);
end;

function TThiever.HasSpace: Boolean;
begin
  if SuicideMode then //Makes sure we always have space to loot
  begin               //everything from the gravestone on suicide mode.
    if (Length(LootArray) > 2) then
      Result := (Inventory.CountEmptySlots > GearAmount)
    else
      Result := (Inventory.CountEmptySlots > GearAmount) or
                (Inventory.FindItem(CoinPouch) and
                 Inventory.FindItem('Coins')
                 and not Inventory.FindItems(DropArray));
  end
  else
  begin
    if (Length(LootArray) > 2) then
      Result := (Inventory.CountEmptySlots >= 2) //if has things oher than coins
    else                                         //we need at least 2 spaces always.
      Result := (Inventory.CountEmptySlots >= 2) or
                (Inventory.FindItem(CoinPouch) and
                 Inventory.FindItem('Coins')
                 and not Inventory.FindItems(DropArray));
  end;
end;


function TThiever.IsLowHP: Boolean;
var
  CurrentHP: Int32 := Minimap.GetHPLevel;
begin
  if NextHeal < 1 then
    NextHeal := Round(Antiban.GetUniqueNumber(NPCMaxHit * 8));

  NextHeal += Random(-3, 3);

  Result := (CurrentHP <= NextHeal) or (CurrentHP <= NPCMaxHit);
end;

function TThiever.IsPouchFull: Boolean;
begin
  if CoinPouchLimit < 1 then
    CoinPouchLimit := SRL.TruncatedGauss(28, 1);
  Result := Inventory.CountItemStack(CoinPouch) >= CoinPouchLimit;
end;


function TThiever.CheckFailed(StartingHP: Int32): Boolean;
var
  HasHitSplat: Boolean;
  CurrentHP: Int32 := Minimap.GetHPLevel;
begin
  HasHitSplat := not RenderDisabled and
                (MainScreen.FindHitsplats(MainScreen.GetPlayerBox) <> []);

  Result := (CurrentHP < StartingHP) or HasHitSplat;

  if Result then
  begin
    if HasHitSplat then
    begin
      Options.RenderSelf;
      RenderDisabled := True;
    end
    else
      Self.OpenCoinPouch;
  end;
end;

function TThiever.NecklaceExpired: Boolean;
begin
  Result := Chat.FindMessage('crumbles', [CHAT_COLOR_LIGHT_RED]);
  HasNeck := not Result;
end;

function TThiever.Dodged: Boolean;
begin
  Result := Chat.FindMessage('dodgy');
end;


function TThiever.OpenCoinPouch: Boolean;
var
  PouchCount: Int32;
begin
  PouchCount := Inventory.CountItemStack(CoinPouch);
  if Inventory.ClickItem(CoinPouch) then
  begin
    Wait(300);
    Result := WaitUntil(not Inventory.FindItem(CoinPouch), 100, 1200);
  end;

  if Result then
  begin
    CoinPouchLimit := SRL.TruncatedGauss(28, 1);
    TotalActions += PouchCount;
    WL.Activity.Restart;
  end;

  if not Result then
    Self.OpenCoinPouch;
end;

function TThiever.Heal: Boolean;
begin
  repeat
    if not Inventory.Consume(HEALTH_CONSUMABLE) then
      Exit;

    Wait(300, 500);
  until Result := (Minimap.GetHPPercent >= (50 + Antiban.GetUniqueNumber(20)));

  if Result then
    NextHeal := Round(Antiban.GetUniqueNumber(NPCMaxHit * 7));
end;

function TThiever.EquipNeck: Boolean;
begin
  if Result := Inventory.ClickItem(Necklace) then Wait(300, 400);

  FreshNeck := HasNeck := Result;
end;

function TThiever.CheckNeck: Boolean;
begin
  Result := HasNeck := Equipment.FindItem(Necklace);

  NeckTimer.Restart(Random(-30000, 60000));
end;


function TThiever.CastShadowVeil: Boolean;
begin
  if not UseShadowVeil or not Magic.Open then
    Exit;

  if not Magic.CanActivate(ERSSpell.SHADOW_VEIL) then
  begin
    Result := UseShadowVeil := False;
    Exit(False);
  end;

  Result := Magic.CastSpell(ERSSpell.SHADOW_VEIL);
  ShadowVeilTimer.Restart(Random(-5000, 5000));
end;

function TThiever.PickNPC: Boolean;
var
  Slot: Int32 := Inventory.GetSelectedSlot;
  CurrentHP: Int32 := Minimap.GetHPLevel;
begin
  if Slot > -1 then Inventory.ClickSlot(Slot);

  if Result := CurrentNPC^.SelectOption(['Pick', 'pocket']) then
  begin
    Minimap.WaitPlayerMoving(300, 10000);

    RSObjects.Gravestone.TileArray.Insert(RSW.GetMyPos, 0);
    if Length(RSObjects.Gravestone.TileArray) > 4 then
      RSObjects.Gravestone.TileArray.Pop;

    if not Self.NecklaceExpired then
      FreshNeck := False;

    Self.CheckFailed(CurrentHP);
    WL.Activity.Restart;
  end;
end;

function TThiever.Deposit: Boolean;
var
  ItemCount: Int32;
begin
  if ToStr(ValuableItem) <> '' then
    ItemCount := Inventory.CountItem(ValuableItem);
  Result := Bank.DepositAllBut(BankTab, StackableArray);
  if ItemCount > 0 then
  begin
    if Inventory.CountItem(ValuableItem) = 0 then
      TotalProfit += (ItemCount * ValuableItemValue);
  end;
end;

function TThiever.Withdraw(Item: TRSItem; Quantity: Int32): Boolean;
var
  ItemCount: Int32 := Inventory.CountItem(Item);
  QuantityNeeded: Int32 := Quantity - ItemCount;
  InvCount: Int32;
begin
  InvCount := Inventory.Count;
  if Result := Bank.WithdrawItem(BankTab, [Item, QuantityNeeded, False], False) then
    WaitUntil(Inventory.Count > InvCount, 100, 3000);

  if not Result and (Item = Necklace) and (PickedNPC = GUARD) then
    NeckAmount := 0;
end;

function TThiever.WithdrawAny(Items: TRSItemArray; Quantity: Int32): Boolean;
var
  Item: TRSItem;
  ItemCount: Int32 := Inventory.CountItems(Items);
  QuantityNeeded: Int32 := Quantity - ItemCount;
  InvCount: Int32;
begin
  for Item in Items do
    if Bank.FindItem(Item) then Exit(Self.Withdraw(Item, Quantity));

  for Item in Items do
  begin
    if BankTab = -1 then
      BankTab := Bank.FindItemTab(Item);

    InvCount := Inventory.Count;
    if Result := Bank.WithdrawItem(BankTab, [Item, QuantityNeeded, False], False) then
    begin
      WaitUntil(Inventory.Count > InvCount, 100, 3000);
      Exit;
    end;
  end;

  FoodManager.Amount := 0;
end;

(*
function TThiever.LootGrave: Boolean;
begin
  HasNeck := False;
  FreshNeck := False;
  RedemptionEnabled := False;

  if RSObjects.Gravestone.WalkSelectOption(['Loot']) then
  begin
    Minimap.WaitPlayerMoving(300, 10000);
    Result := WaitUntil(not MainScreen.FindGrave, 100, 10000);
  end;

  if Result then RSObjects.Gravestone.TileArray := [];
end;
 *)
function TThiever.LootGrave: Boolean;
var
  TPA, GraveTPA: TPointArray;
  P: TPoint;
  UpTextFound: Boolean;
begin
  HasNeck := False;
  FreshNeck := False;
  RedemptionEnabled := False;

  if not Minimap.WaitArrow(TPA, 3000) then
    Exit;

  while not MainScreen.WaitArrow(GraveTPA, 3000) do
  begin
    Mouse.Click(TPA.Grow(6).Random, MOUSE_LEFT);
    Minimap.WaitMoving;
  end;

  if not MainScreen.WaitArrow(GraveTPA, 3000) then
    Exit;

  repeat
    GraveTPA.SortByY(False);
    P := GraveTPA[0];
    P.Y += 5;
    Mouse.Move(P);
    UpTextFound := MainScreen.IsUpText('Grave');
    if not UpTextFound then
      MainScreen.WaitArrow(GraveTPA, 3000);
  until UpTextFound;

  Result := ChooseOption.Select('Loot');

  if Result then RSObjects.Gravestone.TileArray := [];
end;

function TThiever.EnableRedemption: Boolean;
begin
  Result := Prayer.ActivatePrayer(ERSPrayer.REDEMPTION);
  UseRedemption := RedemptionEnabled := Result;
end;

function TThiever.GetState: EThieverState;
begin
  if WL.Activity.IsFinished then
    Exit(END_SCRIPT);

  if ChooseOption.IsOpen then
    Exit(CLOSE_CONTEXT);

  if MainScreen.FindGrave then
    Exit(LOOT_GRAVE);

  if MainScreen.HasInterface then
  begin
    if Bank.IsOpen then
    begin
      if Self.HasDepositItems then
        Exit(DEPOSIT_LOOT);
      if Inventory.CountItem(Necklace) < NeckAmount then
        Exit(WITHDRAW_NECKLACES);
      if not Inventory.HasEnoughConsumable(HEALTH_CONSUMABLE) then
        Exit(WITHDRAW_FOOD);
    end;

    Exit(CLOSE_INTERFACE);
  end;

  if Self.IsPouchFull then
    Exit(OPEN_POUCH);

  if not Self.HasSpace then
  begin
    if Inventory.FindItems(DropArray) then
      Exit(DROP_ITEMS);
    if Self.HasRogue then
      Exit(EQUIP_ROGUE);
    if Inventory.FindConsumable(HEALTH_CONSUMABLE) then
      Exit(EAT_FOOD);

    Exit(OPEN_BANK);
  end;

  if ChatButtons.GetState(ERSChatButton.GAME_CHAT) <> ERSChatButtonState.ENABLED then
    Exit(CHANGE_CHAT_FILTER);

  if not ChatButtons.IsActive(ERSChatButton.GAME_CHAT) then
    Exit(OPEN_CHAT);

  if NeckTimer.IsFinished then
    Exit(CHECK_NECKLACE);

  if not FreshNeck and (not HasNeck or Self.NecklaceExpired) and (NeckAmount > 0) then
  begin
    if Inventory.FindItem(Necklace) then
      Exit(EQUIP_NECKLACE);

    Exit(OPEN_BANK);
  end;

  if Self.HasRogue then
    Exit(EQUIP_ROGUE);

  if Self.IsLowHP then
  begin
    if not RedemptionEnabled and Inventory.FindConsumable(HEALTH_CONSUMABLE) then
      Exit(EAT_FOOD);
    if not SuicideMode then
      Exit(OPEN_BANK);
    if not RSW.AtTile(CurrentNPC^.TileArray, 40) then
      Exit(WALK_TO_NPC);
    if UseRedemption and not RedemptionEnabled then
      Exit(ENABLE_REDEMPTION);
    if UseShadowVeil and ShadowVeilTimer.IsFinished then
      Exit(CAST_SHADOW_VEIL);

    Exit(PICKPOCKET);
  end;

  if not RSW.AtTile(CurrentNPC^.TileArray, 40) then
    Exit(WALK_TO_NPC);

  if UseShadowVeil and ShadowVeilTimer.IsFinished then
    Exit(CAST_SHADOW_VEIL);

  Exit(PICKPOCKET);
end;

procedure TThiever.Run(MaxActions: Int32; MaxTime: Int64);
begin
  ClearDebug;
  Self.Init(MaxActions, MaxTime);

  while not Self.ShouldStop do
  begin
    State := Self.GetState;
    Action := ToStr(State);
    ExtraInfo := ' FreshNeck: ' + ToStr(FreshNeck) +
                 ',   HasNeck: ' + ToStr(HasNeck) +
                 STR_NEW_LINE +
                  ' UseRedemption: ' + ToStr(UseRedemption) +
                  ',   RedemptionEnabled: ' + ToStr(RedemptionEnabled) +
                  ',   NextHeal: ' + ToStr(NextHeal) +
                  STR_NEW_LINE +
                  ' SuicideMode: ' + ToStr(SuicideMode) +
                  ',   UseShadowVeil: ' + ToStr(UseShadowVeil);
    Self.ProcessWhileWaiting;

    case State of
      OPEN_BANK: Bank.WalkOpen(ScriptBank);
      DEPOSIT_LOOT: Self.Deposit;
      WITHDRAW_FOOD: Bank.WithdrawConsumable(HEALTH_CONSUMABLE);
      WITHDRAW_NECKLACES: Self.Withdraw(Necklace, NeckAmount);
      CLOSE_INTERFACE: MainScreen.CloseInterface;

      OPEN_CHAT: ChatButtons.Open(ERSChatButton.GAME_CHAT);
      CHANGE_CHAT_FILTER: ChatButtons.ChangeState(ERSChatButton.GAME_CHAT, ERSChatButtonState.ENABLED);

      CAST_SHADOW_VEIL: Self.CastShadowVeil;
      PICKPOCKET: Self.PickNPC;
      OPEN_POUCH: Self.OpenCoinPouch;

      EAT_FOOD: Self.Heal;
      EQUIP_NECKLACE: Self.EquipNeck;
      CHECK_NECKLACE: Self.CheckNeck;
      CLOSE_CONTEXT: ChooseOption.Close;
      DROP_ITEMS: Inventory.ShiftDrop(DropArray, Inventory.RandomPattern);
      WALK_TO_NPC: RSW.WebWalk(CurrentNPC^.TileArray, 30, 0.2);
      EQUIP_ROGUE: Self.EquipRogue;
      LOOT_GRAVE: Self.LootGrave;
      ENABLE_REDEMPTION: Self.EnableRedemption;

      END_SCRIPT: Break;
    end;

    Self.DoAntiban;
  end;
end;

var
  Thiever: TThiever;

procedure TConsumableManager.Setup(ConsumableType: ERSConsumable); override;
var
  Item: TRSItem;
begin
  inherited;

  if ConsumableType = HEALTH_CONSUMABLE then
    for Item in Self.ItemArray do
      if Thiever.StackableArray.Find(Item) < 0 then
        Thiever.StackableArray += Item;
end;

procedure TConsumableManager.SetupNew(ConsumableType: ERSConsumable); override;
var
  Item: TRSItem;
begin
  inherited;

  if ConsumableType = HEALTH_CONSUMABLE then
    for Item in Self.ItemArray do
      if Thiever.StackableArray.Find(Item) < 0 then
        Thiever.StackableArray += Item;
end;


function TRSInventory.HasEnoughConsumable(ConsumableType: ERSConsumable): Boolean; override;
begin
  Result := Self.CountPoints(ConsumableType) >= FoodManager.Amount;
end;

function TRSBank.WithdrawConsumableAmount(ConsumableType: ERSConsumable; Idx: Int32): Int32; override;
var
  HandlerManager: ^TConsumableManager := GetHandler(ConsumableType);
begin
  if ConsumableType = HEALTH_CONSUMABLE then
    Exit(FoodManager.Amount - Inventory.CountConsumable(HEALTH_CONSUMABLE));

  case HandlerManager^.Handlers[Idx].Amount of
    BANK_WITHDRAW_ALL: Result := BANK_WITHDRAW_ALL;
    BANK_WITHDRAW_ALL_BUT_ONE: Result := BANK_WITHDRAW_ALL_BUT_ONE;
    else
      Result := Ceil((HandlerManager^.MinInvPoints - Inventory.CountPoints(ConsumableType)) / HandlerManager^.Handlers[Idx].GetPoints);
  end;
end;

type
  TThieverConfig = record(TScriptConfig)
    NPCSelector: TLabeledCombobox;
    FoodAmountEdit: TLabeledEdit;
    NeckAmountEdit: TLabeledEdit;
    MinHealthEdit: TLabeledEdit;
    SuicideState: TLabeledCheckbox;
  end;

procedure TThieverConfig.StartScript(Sender: TObject);
begin
  Self.Init(Sender);

  PickedNPC := ENPC(NPCSelector.Combobox.getItemIndex);
  SuicideMode := SuicideState.Checkbox.getState = cbChecked;

  FoodAmount := StrToInt(FoodAmountEdit.GetText);
  NeckAmount := StrToInt(NeckAmountEdit.GetText);
  NextHeal := StrToInt(MinHealthEdit.GetText);
end;

procedure TThieverConfig.NPCSelectorOnChange(Sender: TObject);
begin
  Writeln(Sender, ' has changed');

  case ENPC(NPCSelector.Combobox.getItemIndex) of
    MAN:
      begin
        SuicideState.Checkbox.setState(cbUnchecked);
        SuicideState.Checkbox.setEnabled(False);
      end;

    MASTER_FARMER:
      begin
        SuicideState.Checkbox.setState(cbUnchecked);
        SuicideState.Checkbox.setEnabled(False);
      end;

    KNIGHT_OF_ARDOUGNE:
      begin
        SuicideState.Checkbox.setState(cbUnchecked);
        SuicideState.Checkbox.setEnabled(False);
      end;

    TZHAAR_CITY:
      begin
        SuicideState.Checkbox.setState(cbUnchecked);
        SuicideState.Checkbox.setEnabled(False);
      end;

    else
    begin
      SuicideState.Checkbox.setState(cbChecked);
      SuicideState.Checkbox.setEnabled(True);
    end;
  end;

end;

procedure TThieverConfig.Setup; override;
begin
  inherited;

  Self.UpdateFormSize(Form.getWidth, Form.getHeight + 70);

  with NPCSelector do
  begin
    Init(ScriptSettingsPanel);
    SetCaption('Agility course:');
    SetLeft(ScriptSettingsLabel.getLeft + 5);
    SetTop(ScriptSettingsLabel.getTop + ScriptSettingsLabel.getHeight + 5);
    SetWidth(200);
    Combobox.setStyle(csDropDownList);
    Combobox.getItems.Add('Men and women (Lumbridge only)');
    Combobox.getItems.Add('Master farmers (Draynor only)');
    Combobox.getItems.Add('Guards (Falador only)');
    Combobox.getItems.Add('Knights of Ardougne');
    Combobox.getItems.Add('Elves (Prifddinas only)');
    Combobox.getItems.Add('Tzhaars');
    Combobox.setItemIndex(Ord(PickedNPC));
    Combobox.setOnChange(@Self.NPCSelectorOnChange);
  end;

  with FoodAmountEdit do
  begin
    Init(ScriptSettingsPanel);
    SetCaption('Food amount:');
    SetLeft(NPCSelector.getLeft);
    SetTop(NPCSelector.getTop + NPCSelector.GetHeight + 10);
    SetWidth(80);
    SetText(ToStr(FoodAmount));
  end;

  with NeckAmountEdit do
  begin
    Init(ScriptSettingsPanel);
    SetCaption('Necklaces amount:');
    SetLeft(FoodAmountEdit.getLeft + FoodAmountEdit.GetWidth + 30);
    SetTop(FoodAmountEdit.getTop);
    SetWidth(80);
    SetText(ToStr(NeckAmount));
  end;

  with MinHealthEdit do
  begin
    Init(ScriptSettingsPanel);
    SetCaption('Minimum health:');
    SetLeft(NeckAmountEdit.getLeft + NeckAmountEdit.GetWidth + 35);
    SetTop(FoodAmountEdit.getTop);
    SetWidth(80);
    SetText(ToStr(Round(Antiban.GetUniqueNumber(10 * 8))));
  end;

  with SuicideState do
  begin
    Init(ScriptSettingsPanel);
    SetCaption('Suicide mode:');
    SetLeft(NPCSelector.getLeft + NPCSelector.GetWidth + 25);
    SetTop(NPCSelector.getHeight);
    SetWidth(50);
    SetHeight(10);
    SetEnabled(True);
    Self.NPCSelectorOnChange(@SuicideState);
  end;

  StartButton.setOnClick(@Self.StartScript);
end;

procedure TThieverConfig.Run; override;
begin
  Self.Setup;

  inherited;
end;

var
  ThieverConfig: TThieverConfig;

begin
  {$IFDEF SCRIPT_GUI}
  Sync(@ThieverConfig.Run);
  {$ENDIF}
  Thiever.Run(MaxActions, MaxTime);
end.
