{$DEFINE SCRIPT_ID := 'bee77a9c-8ff3-4b11-b0fc-2fbbaa788d15'}
{$DEFINE SCRIPT_REVISION := '41'}
{$DEFINE SCRIPT_GUI}
{$I SRL-T/osr.simba}
{$I WaspLib/osr.simba}

begin
  Login.PlayerIndex := 0;
end;

type
  ERSNPC = (PHIALS, SERVANT);
  ERSFurniture = (REGULAR_LARDER, OAK_LARDER, MYTH_CAPE);

var
  CurrentNPC: ERSNPC := PHIALS;
  CurrentFurniture: ERSFurniture := ERSFurniture.MYTH_CAPE;

type
  EBuilderState = (
    LEVEL_UP,
    WAIT_STATE,

    EXIT_HOUSE,
    ENTER_HOUSE,

    BUILD_FURNITURE,
    HANDLE_FURNITURE_UI,
    DESTROY_FURNITURE,
    HANDLE_BUILD_CHAT,
    HANDLE_PIN,

    GET_PLANKS,
    HANDLE_PHIALS_CHAT,
    FILL_SACK,
    CALL_SERVANT,
    TALK_SERVANT,
    HANDLE_SERVANT,

    END_SCRIPT
  );

  TBuilder = record(TBaseWalkerScript)
    State: EBuilderState;
    POH: TRSWalker;

    BuiltFurniture, FurnitureSpace: TRSObject;
    ServantNPC: TRSNPC;

    UsePlankSack, FurnitureIsBuilt, UsePhials, WaitingServant: Boolean;

    Plank: TRSItem;
    PlankSackCount, NotedPlanksSlot, MinPlankCount: Int32;
    LastPoint: TPoint;
  end;


procedure TBuilder.SetupNPC();
begin
  case CurrentNPC of
    PHIALS: Self.UsePhials := True;

    SERVANT:
      begin
        ServantNPC.Filter.Skip := False;
        ServantNPC.Filter.Finder := False;
        ServantNPC.Filter.Walker := False;
        ServantNPC.Filter.MinimapDot := True;

        ServantNPC.ShapeArray += [[1, 1, 7], 0];
        ServantNPC.Setup(['Talk-to']);
      end;
  end;
end;

procedure TBuilder.SetupPlank();
begin
  case CurrentFurniture of
    REGULAR_LARDER:
      begin
        Self.Plank := 'Plank';
        Self.MinPlankCount := 8;
      end;

    OAK_LARDER:
      begin
        Self.Plank := 'Oak plank';
        Self.MinPlankCount := 8;
      end;

    MYTH_CAPE:
      begin
        Self.Plank := 'Teak plank';
        Self.MinPlankCount := 3;
      end;
  end;
end;

procedure TBuilder.SetupObjects();
begin
  if CurrentFurniture = ERSFurniture.MYTH_CAPE then
  begin
    BuiltFurniture.Setup(0.3, 4, [[3603, 139]]);
    BuiltFurniture.Setup(['Teleport']);
    BuiltFurniture.Filter.Finder := False;
    BuiltFurniture.Filter.UpText := False;

    FurnitureSpace.Setup(0.3, 4, [[3603, 139]]);
    FurnitureSpace.Setup(['2 more']);
    FurnitureSpace.Filter.Finder := False;
    FurnitureSpace.Filter.UpText := False;

    Exit;
  end;

  BuiltFurniture.Setup(1.5, 7, [[3600, 115]]);
  BuiltFurniture.Setup(['Larder']);
  BuiltFurniture.Filter.Finder := False;
  BuiltFurniture.Filter.UpText := False;

  FurnitureSpace.Setup(1.5, 7, [[3600, 115]]);
  FurnitureSpace.Setup(['2 more']);
  FurnitureSpace.Filter.Finder := False;
  FurnitureSpace.Filter.UpText := False;
end;

procedure TAntiban.Setup(); override;
begin
  Self.Skills := [ERSSkill.CONSTRUCTION, ERSSkill.TOTAL];
  Self.MinZoom := 0;
  Self.MaxZoom := 35;

  inherited;
end;

procedure TBuilder.Init(maxActions: Int32; maxTime: Int64); override;
begin
  inherited;

  RSW.Setup([RSRegions.RIMMINGTON, RSRegions.TEMP_POH]);
  Self.LastPoint := [0, 0];
  Self.SetupNPC();
  Self.SetupObjects();
  Self.SetupPlank();

  Self.UsePlankSack := Inventory.ContainsItem('Plank sack');

  Self.ActionProfit -= (ItemData.GetAverage(Plank) * 8 + 40);

  if CurrentNPC = PHIALS then
    case CurrentFurniture of
      ERSFurniture.REGULAR_LARDER: Inventory.FindItem('noted Plank', NotedPlanksSlot);
      ERSFurniture.OAK_LARDER: Inventory.FindItem('noted Oak plank', NotedPlanksSlot);
      ERSFurniture.MYTH_CAPE: Inventory.FindItem('noted Teak plank', NotedPlanksSlot);
    end;

  if WLSettings.RemoteInput.HUDDebug then
  begin
    DebugObjectArray += BuiltFurniture;
    DebugObjectArray += FurnitureSpace;
    DebugObjectArray += RSObjects.POHPortal;
    DebugMMDotArray += RSNPCs.Phials;
    DebugMMDotArray += ServantNPC;
  end;
end;


function TBuilder.EnterHouse(): Boolean;
begin
  if RSObjects.POHPortal.WalkSelectOption(['Build', 'mode']) then
  begin
    Minimap.WaitMoving();
    WaitUntil((BankPin.IsOpen() and BankPin.Enter(Login.GetPlayerPin())) or MainScreen.LoadingPOH(), 300, 10000);
    Result := MainScreen.WaitLoadingPOH(10000);
  end;
end;

function TBuilder.BuildFurniture(): Boolean;
var
  tpa: TPointArray;
begin
  if Self.LastPoint <> [0, 0] then
  begin
    tpa := [Self.LastPoint];
    tpa := tpa.Grow(8);

    Mouse.Move(tpa);
  end;

  if FurnitureSpace.WalkSelectOption(['Build La', 'Build Gu']) then
  begin
    Minimap.WaitMoving();
    Result := WaitUntil(MainScreen.IsClassicOpen(), 200, 5000);
  end
  else if ChooseOption.HasOption(['Remove La', 'Remove My']) then
  begin
    ChooseOption.Close();
    FurnitureIsBuilt := True;
  end
  else
  begin
    Antiban.RandomRotate();
    Self.LastPoint := [0,0];
  end;
end;

function TBuilder.DestroyFurniture(): Boolean;
var
  tpa: TPointArray;
begin
  if Self.LastPoint <> [0, 0] then
  begin
    tpa := [Self.LastPoint];
    tpa := tpa.Grow(8);

    Mouse.Move(tpa);
  end;

  if BuiltFurniture.WalkSelectOption(['Remove La', 'Remove My']) then
  begin
    Minimap.WaitMoving();
    Result := WaitUntil(Chat.GetChatTitle() = 'Really remove it?', 200, 3000);

  end
  else if ChooseOption.HasOption(['Build La', 'Build Gu']) then
  begin
    ChooseOption.Close();
    FurnitureIsBuilt := False;
  end
  else
  begin
    Antiban.RandomRotate();
    Self.LastPoint := [0,0];
  end;
end;


function TBuilder.CallServant(): Boolean;
begin
  if ServantNPC.Click then
  begin
    Minimap.WaitMoving;
    Result := WaitUntil(Chat.GetChatTitle = 'Repeat last task?',
                        SRL.TruncatedGauss(50, 1500), 5000);
  end
  else
    Result := Options.CallServant();
end;

function TBuilder.PhialsGetPlanks(): Boolean;
begin
  if not RSNPCs.Phials.IsVisible() and not RSW.AtTile(RSNPCs.Phials.Coordinates, 8) then
    RSW.WebWalk(RSNPCs.Phials.Coordinates, 15, 0.2);

  if Inventory.SetSelectedSlot(Self.NotedPlanksSlot) and
     RSNPCs.Phials.SelectOption(['Use']) then
  begin
    Minimap.WaitFlag();
    Result := WaitUntil(Chat.GetChatTitle() = 'Select an Option', 100, 15000);
  end;
end;

function TBuilder.TalkServant(): Boolean;
begin
  if ServantNPC.Click then
  begin
    Minimap.WaitMoving;
    Result := WaitUntil(Inventory.IsFull, 100, 5000);
  end;

  WaitingServant := not Result;
end;


function TBuilder.FillSack(attempt: Int32 = 0): Boolean;
var
  count: Int32;
begin
  count := Inventory.CountItem(Self.Plank);

  if Inventory.ClickItem('Plank sack', 'Fill') then
    Result := WaitUntil(Inventory.CountItem(Self.Plank) < count, 300, 3000);

  if Result then
    Self.PlankSackCount += count
  else if attempt = 2 then
  begin
    Self.PlankSackCount := 28;
    Exit(True);
  end
  else
    Exit(Self.FillSack(attempt + 1));
end;


function TBuilder.HandleServant(): Boolean;
begin
  Result := Chat.ClickOption('Fetch', BioHash);
  if Result then
  begin
    WaitingServant := True;
    WaitUntil(Chat.GetChatTitle() <> 'Repeat last task?', 200, 3000)
  end;
end;

function TBuilder.HandleBuildChat(): Boolean;
begin
  if Chat.ClickOption('Yes') then
    Result := WaitUntil(Chat.GetChatTitle <> 'Really remove it?', 200, 3000);

  if Result then
  begin
    if CurrentFurniture = ERSFurniture.MYTH_CAPE then
      Result := WaitUntil(Inventory.ContainsItem('Mythical cape'), 50, 3000)
    else
      Wait(100, 150);

    FurnitureIsBuilt := not Result;
  end;
end;

function TBuilder.HandleFurniture(): Boolean;
var
  count: Int32;
begin
  count := Inventory.CountItem(Self.Plank);

  case CurrentFurniture of
    ERSFurniture.REGULAR_LARDER: Keyboard.PressKey(VK_1);
    ERSFurniture.OAK_LARDER: Keyboard.PressKey(VK_2);
    ERSFurniture.MYTH_CAPE: Keyboard.PressKey(VK_4);
  end;

  Result := FurnitureIsBuilt := WaitUntil(not MainScreen.IsClassicOpen(), 100, 3000);

  XPBar.EarnedXP();

  if Result then
  begin
    if Self.UsePlankSack and (count < Self.MinPlankCount) then
    begin
      count := Self.MinPlankCount - count;
      Self.PlankSackCount -= count;
    end;

    Self.TotalActions += 1;
    Self.TotalProfit := Self.TotalActions * Self.ActionProfit;
    WL.Activity.Restart();
    Wait(600, 800);
  end else
    MainScreen.CloseInterface();
end;


function TBuilder.GetState(): EBuilderState;
var
  s: String;
begin
  if WL.Activity.IsFinished() then
    Exit(END_SCRIPT);

  if Chat.LeveledUp() then
    Exit(EBuilderState.LEVEL_UP);

  s := Chat.GetChatTitle();

  if s.Contains('remove') then
    Exit(EBuilderState.HANDLE_BUILD_CHAT);

  if s.Contains('Select') then
    Exit(EBuilderState.HANDLE_PHIALS_CHAT);

  if s.Contains('Repeat') then
    Exit(EBuilderState.HANDLE_SERVANT);

  if MainScreen.IsClassicOpen() then
  begin
    if BankPin.IsOpen() then
      Exit(EBuilderState.HANDLE_PIN);
    Exit(EBuilderState.HANDLE_FURNITURE_UI);
  end;

  if not Minimap.InPOH() then
  begin
    if not UsePhials then
      Exit(EBuilderState.ENTER_HOUSE);

    if (CurrentFurniture = ERSFurniture.MYTH_CAPE) and not Inventory.ContainsItem('Mythical cape') then
      Exit(EBuilderState.ENTER_HOUSE);

    if Inventory.IsFull() then
    begin
      if Self.UsePlankSack and (Self.PlankSackCount < Self.MinPlankCount) then
        Exit(EBuilderState.FILL_SACK);
      Exit(EBuilderState.ENTER_HOUSE);
    end;

    Exit(EBuilderState.GET_PLANKS);
  end;

  if Self.FurnitureIsBuilt then
    Exit(EBuilderState.DESTROY_FURNITURE);

  if (Inventory.CountItem(Self.Plank) + Self.PlankSackCount) > Self.MinPlankCount then
  begin
    Self.WaitingServant := False;
    Exit(EBuilderState.BUILD_FURNITURE);
  end;

  if Self.UsePhials then
    Exit(EBuilderState.EXIT_HOUSE);

  if WaitingServant then
  begin
    if ServantNPC.IsVisible() then
      Exit(EBuilderState.TALK_SERVANT);
    Exit(EBuilderState.WAIT_STATE);
  end;

  Exit(EBuilderState.CALL_SERVANT);
end;

procedure TBuilder.Run(maxActions: Int32; maxTime: Int64);
begin
  Self.Init(maxActions, maxTime);

  repeat
    Self.State := Self.GetState();
    Self.SetAction(ToStr(Self.State));

    case Self.State of
      EBuilderState.LEVEL_UP: Chat.HandleLevelUp();

      EBuilderState.HANDLE_BUILD_CHAT: Self.HandleBuildChat();
      EBuilderState.HANDLE_PHIALS_CHAT: Chat.ClickOption('All');
      EBuilderState.HANDLE_FURNITURE_UI: Self.HandleFurniture();
      EBuilderState.HANDLE_SERVANT: Self.HandleServant();
      EBuilderState.HANDLE_PIN: BankPin.Enter(Login.GetPlayerPin());

      EBuilderState.CALL_SERVANT: Self.CallServant();
      EBuilderState.TALK_SERVANT: Self.TalkServant();
      EBuilderState.EXIT_HOUSE: Options.LeaveHouse();
      EBuilderState.GET_PLANKS: Self.PhialsGetPlanks();
      EBuilderState.FILL_SACK: Self.FillSack();

      EBuilderState.ENTER_HOUSE: Self.EnterHouse();
      EBuilderState.BUILD_FURNITURE: Self.BuildFurniture();
      EBuilderState.DESTROY_FURNITURE: Self.DestroyFurniture();

      EBuilderState.WAIT_STATE: Wait(7000, 8000);

      END_SCRIPT: Break;
    end;

    Self.DoAntiban();
  until Self.ShouldStop();
end;

var
  Builder: TBuilder;

function TRSWalker.GetCleanMinimap(): TMufasaBitmap; override;
var
  Bitmap: TMufasaBitmap;
begin
  Bitmap := GetMufasaBitmap(BitmapFromClient(Minimap.Bounds));
  Bitmap.ReplaceColor(0, 1); // We need zero to be unique

  Self.CleanMinimap(Bitmap);
  Bitmap.ReplaceColor(1, $32A072);

  Result.Init();

  Bitmap.RotateBitmapEx(Minimap.GetCompassAngle(False), False, True, Result);
  Bitmap.Free();
end;

function TRSWalkerObject._SelectHelper(action: TStringArray): Boolean; override;
begin
  if Self.CurrentUpText <> [] then
    Self.CurrentUpText := [];

  if Self.RedClicked then
  begin
    Self.RedClicked := False;
    Exit(True);
  end;

  if ChooseOption.IsOpen() and ChooseOption.Select(action) then
    Exit(True);

  if MainScreen.IsUpText(action) then
  begin
    Mouse.Click(MOUSE_LEFT);
    Exit(MainScreen.DidRedClick());
  end;

  Builder.LastPoint := Mouse.Position();

  Result := ChooseOption.Select(action, MOUSE_LEFT, True, (False));
end;

//TEMP OPTION FIXES
function TRSOptions.IsHouseOptionsOpen(): Boolean; override;
begin
  Result := SRL.CountColor(CTS2(4630611, 23, 0.04, 0.79), Self.Bounds()) > 115;
end;

function TRSOptions.CallServant(): Boolean; override;
var
  s: String;
  b: TBox;
begin
  if not Self.OpenHouseOptions() then
    Exit;

  b := Self.Bounds();

  b.X1 += 9;
  b.Y1 += 185;
  b.X2 -= 9;
  b.Y2 -= 50;

  Mouse.Click(b, MOUSE_LEFT);
  Result := WaitUntil(((s := Chat.GetChatTitle) = 'Demon butler') or
                       (s = 'Repeat last task?'), SRL.TruncatedGauss(50, 1500), 5000);
end;

function TRSOptions.LeaveHouse(): Boolean; override;
var
  b: TBox;
begin
  if not Self.OpenHouseOptions() then
    Exit;

  b := Self.Bounds();

  b.X1 += 9;
  b.Y1 += 214;
  b.X2 -= 9;
  b.Y2 -= 21;

  Mouse.Click(b, MOUSE_LEFT);
  Result := WaitUntil(not Self.IsHouseOptionsOpen(), RandomLeft(50, 1500), 5000);
end;

function TRSBankPin.Enter(pin: String): Boolean; override;

  function GetDigitToEnter(): Char;
  begin
    if OCR.LocateText(Self.Bounds, 'FIRST',  RS_FONT_BOLD_12, TOCRColorFilter.Create([$FFFFFF]), 1) then Exit(pin[1]);
    if OCR.LocateText(Self.Bounds, 'SECOND', RS_FONT_BOLD_12, TOCRColorFilter.Create([$FFFFFF]), 1) then Exit(pin[2]);
    if OCR.LocateText(Self.Bounds, 'THIRD',  RS_FONT_BOLD_12, TOCRColorFilter.Create([$FFFFFF]), 1) then Exit(pin[3]);
    if OCR.LocateText(Self.Bounds, 'FOURTH', RS_FONT_BOLD_12, TOCRColorFilter.Create([$FFFFFF]), 1) then Exit(pin[4]);
  end;

var
  i: Int32;
  digit: Char;
begin
  for 1 to 3 do // 3 Attempts
  begin
    for i := 1 to 4 do
    begin
      digit := GetDigitToEnter();
      if digit = '' then
        Break;

      if not Self.ClickDigit(digit) then
        Break;

      Wait(1000, 2000, wdLeft);
    end;

    if 'try again' in Chat.GetChat() then
    begin
      Chat.ClickContinue(True);
      Continue;
    end;

    if MainScreen.WaitLoadingPOH(5000) or Minimap.InPOH() then
      Exit(True);
  end;

  Result := MainScreen.WaitLoadingPOH(5000) or Minimap.InPOH(); // wait 3 extra seconds before failing.
end;


procedure TRSMinimap.SetCompassAngle(Degrees: Double; Accuracy: Double = 5); override;
begin
  if MainScreen.IsUpText('>') then
    ChooseOption.Select('Cancel');

  inherited;
end;

procedure TSRL.Debug(Bitmap: TMufasaBitmap); override;
begin
  inherited;

  Builder.RSW.Draw(Bitmap);
end;

{$IFDEF SCRIPT_GUI}
type
  TBuilderConfig = record(TScriptForm)
    NPCSelector: TLabeledCombobox;
    FurnitureSelector: TLabeledCombobox;
    BuilderInfo: TLabel;
    ImagePanel: TImage;
  end;

procedure TBuilderConfig.StartScript(sender: TObject); override;
begin
  CurrentNPC := ERSNPC(Self.NPCSelector.GetItemIndex());
  CurrentFurniture := ERSFurniture(Self.FurnitureSelector.GetItemIndex());
  inherited;
end;

procedure TBuilderConfig.NPCSelectorOnChange(sender: TObject);
var
  combobox: TComboBox;
begin
  combobox := sender;
  case combobox.getItemIndex() of
    0: Self.BuilderInfo.SetCaption(
       'Make sure you have noted planks with you.' +

       LineEnding + LineEnding +
       'Your house layout must look exactly like the image to the right!' +
       LineEnding +
       'Looking close enough won''t do, it has to look exactly the same.' +
       LineEnding +
       'If you have more rooms, move them 3 squares away so they are never,' +
       LineEnding +
       'ever visible on the minimap.'
       );

    1: Self.BuilderInfo.SetCaption(
       'Make sure you have planks in your bank and you have previously' +
       LineEnding +
       'asked the buttler to fetch you 24 planks.' +
       LineEnding +
       'You also need money in your servant''s money bag and you need to' +
       LineEnding +
       'replenish it manually once in a while.' +

       LineEnding + LineEnding +
       'Your house layout must look exactly like the image to the right!' +
       LineEnding +
       'Looking close enough won''t do, it has to look exactly the same.' +
       LineEnding +
       'If you have more rooms, move them 3 squares away so they are never,' +
       LineEnding +
       'ever visible on the minimap.'
       );
  end;
end;

procedure TBuilderConfig.Run(); override;
var
  tab: TTabSheet;
begin
  Self.Setup('Wasp Builder');
  Self.Start.SetOnClick(@Self.StartScript);

  Self.AddTab('Script Settings');
  tab := Self.Tabs[High(Self.Tabs)];

  Self.CreateAccountManager(tab);

  with Self.NPCSelector do
  begin
    Create(tab);
    SetCaption('Method:');
    SetLeft(TControl.AdjustToDPI(50));
    SetTop(TControl.AdjustToDPI(150));
    SetStyle(csDropDownList);
    AddItemArray(['Phials', 'Demon butler']);
    SetItemIndex(Ord(CurrentNPC));
    Combobox.SetOnChange(@Self.NPCSelectorOnChange);
  end;

  with Self.FurnitureSelector do
  begin
    Create(tab);
    SetCaption('Furniture:');
    SetTop(Self.NPCSelector.GetTop());
    SetLeft(Self.NPCSelector.GetRight() + TControl.AdjustToDPI(15));
    SetStyle(csDropDownList);
    AddItemArray(['Regular larder', 'Oak larder', 'Myth cape']);
    SetItemIndex(Ord(CurrentFurniture));
  end;

  with Self.BuilderInfo do
  begin
    Create(tab);
    SetLeft(Self.NPCSelector.GetLeft());
    SetTop(Self.NPCSelector.GetBottom() + TControl.AdjustToDPI(15));
  end;

  with Self.ImagePanel do
  begin
    Create(tab);
    SetTop(Self.FurnitureSelector.GetTop() + TControl.AdjustToDPI(15));
    SetLeft(Self.FurnitureSelector.GetRight() + TControl.AdjustToDPI(80));
    LoadFromFile(IncludePath+'\WaspLib\docs\images\POH_layout.png');
    SetWidth(TControl.AdjustToDPI(195));
    SetHeight(TControl.AdjustToDPI(194));
  end;

  Self.CreateVersionPanel(tab);
  Self.CreateAntibanManager();
  Self.CreateWaspLibSettings();
  Self.CreateAPISettings();
  Self.NPCSelectorOnChange(Self.NPCSelector.ComboBox);

  inherited;
end;

var
  BuilderConfig: TBuilderConfig;
{$ENDIF}

begin
  {$IFDEF SCRIPT_GUI}
  BuilderConfig.Run();
  {$ENDIF}
  Builder.Run(WLSettings.MaxActions, WLSettings.MaxTime);
end.
